OBJECT Page 600 IC Dimensions
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=IC Dimensions;
               ESM=Dimensiones IC;
               FRC=Dimensions IC;
               ENC=IC Dimensions];
    SourceTable=Table411;
    PageType=List;
    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 15      ;1   ;ActionGroup;
                      CaptionML=[ENU=IC &Dimension;
                                 ESM=&Dimensi¢n IC;
                                 FRC=&Dimension IC;
                                 ENC=IC &Dimension] }
      { 16      ;2   ;Action    ;
                      CaptionML=[ENU=IC Dimension &Values;
                                 ESM=&Valores de dimensi¢n IC;
                                 FRC=&Valeurs de dimension IC;
                                 ENC=IC Dimension &Values];
                      RunObject=Page 601;
                      RunPageLink=Dimension Code=FIELD(Code);
                      Image=ChangeDimensions }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 11      ;1   ;ActionGroup;
                      CaptionML=[ENU=F&unctions;
                                 ESM=Acci&ones;
                                 FRC=F&onctions;
                                 ENC=F&unctions];
                      Image=Action }
      { 17      ;2   ;Action    ;
                      CaptionML=[ENU=Map to Dim. with Same Code;
                                 ESM=Asignar a dim. con igual c¢digo;
                                 FRC=êtablir une correspondance Ö la dimension du màme code;
                                 ENC=Map to Dim. with Same Code];
                      Image=MapDimensions;
                      OnAction=VAR
                                 ICDimension@1001 : Record 411;
                                 ICMapping@1000 : Codeunit 428;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(ICDimension);
                                 IF ICDimension.FIND('-') AND CONFIRM(Text000) THEN
                                   REPEAT
                                     ICMapping.MapIncomingICDimensions(ICDimension);
                                   UNTIL ICDimension.NEXT = 0;
                               END;
                                }
      { 13      ;2   ;Action    ;
                      CaptionML=[ENU=Copy from Dimensions;
                                 ESM=Copiar de Dimensiones;
                                 FRC=Copier Ö partir des dimensions;
                                 ENC=Copy from Dimensions];
                      Image=CopyDimensions;
                      OnAction=BEGIN
                                 CopyFromDimensions;
                               END;
                                }
      { 14      ;2   ;Separator  }
      { 18      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Import;
                                 ESM=Importar;
                                 FRC=Importer;
                                 ENC=Import];
                      Image=Import;
                      OnAction=BEGIN
                                 ImportFromXML;
                               END;
                                }
      { 19      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[ENU=E&xport;
                                 ESM=E&xportar;
                                 FRC=E&xporter;
                                 ENC=E&xport];
                      Image=Export;
                      OnAction=BEGIN
                                 ExportToXML;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Code }

    { 4   ;2   ;Field     ;
                SourceExpr=Name }

    { 6   ;2   ;Field     ;
                SourceExpr=Blocked }

    { 8   ;2   ;Field     ;
                SourceExpr="Map-to Dimension Code" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Are you sure you want to map the selected line(s)?;ESM=®Confirma que desea eliminar las l°neas seleccionadas?;FRC=Voulez-vous vraiment Çtablir une correspondance pour la ou les lignes sÇlectionnÇes?;ENC=Are you sure you want to map the selected line(s)?';
      Text001@1004 : TextConst 'ENU=Select file to import into %1;ESM=Selecc. archivo para imp. en %1;FRC=SÇlect. fichier Ö importer ds %1;ENC=Select file to import into %1';
      Text002@1003 : TextConst 'ENU=ICDim.xml;ESM=ICDim.xml;FRC=ICDim.xml;ENC=ICDim.xml';
      Text004@1001 : TextConst 'ENU=Are you sure you want to copy from Dimensions?;ESM=®Realmente desea copiar los datos de Dimensiones?;FRC=Voulez-vous vraiment copier Ö partir de Dimensions?;ENC=Are you sure you want to copy from Dimensions?';
      Text005@1002 : TextConst 'ENU=Enter the file name.;ESM=Introduzca el nombre del archivo.;FRC=Entrez le nom du fichier.;ENC=Enter the file name.';
      Text006@1005 : TextConst 'ENU=XML Files (*.xml)|*.xml|All Files (*.*)|*.*;ESM=Archivos XML (*.xml)|*.xml|Todos los archivos (*.*)|*.*;FRC=Fichiers XML (*.xml)|*.xml|Tous les fichiers (*.*)|*.*;ENC=XML Files (*.xml)|*.xml|All Files (*.*)|*.*';

    LOCAL PROCEDURE CopyFromDimensions@1();
    VAR
      Dim@1000 : Record 348;
      DimVal@1005 : Record 349;
      ICDim@1001 : Record 411;
      ICDimVal@1006 : Record 412;
      ICDimValEmpty@1002 : Boolean;
      ICDimValExists@1003 : Boolean;
      PrevIndentation@1004 : Integer;
    BEGIN
      IF NOT CONFIRM(Text004,FALSE) THEN
        EXIT;

      ICDimVal.LOCKTABLE;
      ICDim.LOCKTABLE;
      Dim.SETRANGE(Blocked,FALSE);
      IF Dim.FIND('-') THEN
        REPEAT
          IF NOT ICDim.GET(Dim.Code) THEN BEGIN
            ICDim.INIT;
            ICDim.Code := Dim.Code;
            ICDim.Name := Dim.Name;
            ICDim.INSERT;
          END;

          ICDimValExists := FALSE;
          DimVal.SETRANGE("Dimension Code",Dim.Code);
          ICDimVal.SETRANGE("Dimension Code",Dim.Code);
          ICDimValEmpty := NOT ICDimVal.FINDFIRST;
          IF DimVal.FIND('-') THEN
            REPEAT
              IF DimVal."Dimension Value Type" = DimVal."Dimension Value Type"::"End-Total" THEN
                PrevIndentation := PrevIndentation - 1;
              IF NOT ICDimValEmpty THEN
                ICDimValExists := ICDimVal.GET(DimVal."Dimension Code",DimVal.Code);
              IF NOT ICDimValExists AND NOT DimVal.Blocked THEN BEGIN
                ICDimVal.INIT;
                ICDimVal."Dimension Code" := DimVal."Dimension Code";
                ICDimVal.Code := DimVal.Code;
                ICDimVal.Name := DimVal.Name;
                ICDimVal."Dimension Value Type" := DimVal."Dimension Value Type";
                ICDimVal.Indentation := PrevIndentation;
                ICDimVal.INSERT;
              END;
              PrevIndentation := DimVal.Indentation;
              IF DimVal."Dimension Value Type" = DimVal."Dimension Value Type"::"Begin-Total" THEN
                PrevIndentation := PrevIndentation + 1;
            UNTIL DimVal.NEXT = 0;
        UNTIL Dim.NEXT = 0;
    END;

    LOCAL PROCEDURE ImportFromXML@2();
    VAR
      CompanyInfo@1006 : Record 79;
      ICDimIO@1005 : XMLport 11;
      FileName@1004 : Text[1024];
      StartFileName@1003 : Text[1024];
      IFile@1002 : File;
      IStr@1001 : InStream;
    BEGIN
      CompanyInfo.GET;

      StartFileName := CompanyInfo."IC Inbox Details";
      IF StartFileName <> '' THEN BEGIN
        IF StartFileName[STRLEN(StartFileName)] <> '\' THEN
          StartFileName := StartFileName + '\';
        StartFileName := StartFileName + '*.xml';
      END;

      IF NOT UPLOAD(STRSUBSTNO(Text001,TABLECAPTION),'',Text006,StartFileName,FileName) THEN
        ERROR(Text005);

      IFile.OPEN(FileName);
      IFile.CREATEINSTREAM(IStr);
      ICDimIO.SETSOURCE(IStr);
      ICDimIO.IMPORT;
    END;

    LOCAL PROCEDURE ExportToXML@3();
    VAR
      CompanyInfo@1006 : Record 79;
      FileMgt@1000 : Codeunit 419;
      ICDimIO@1005 : XMLport 11;
      FileName@1004 : Text;
      DefaultFileName@1003 : Text;
      OFile@1002 : File;
      OStr@1001 : OutStream;
    BEGIN
      CompanyInfo.GET;

      DefaultFileName := CompanyInfo."IC Inbox Details";
      IF DefaultFileName <> '' THEN
        IF DefaultFileName[STRLEN(DefaultFileName)] <> '\' THEN
          DefaultFileName := DefaultFileName + '\';
      DefaultFileName := DefaultFileName + Text002;

      FileName := FileMgt.ServerTempFileName('xml');
      IF FileName = '' THEN
        EXIT;

      OFile.CREATE(FileName);
      OFile.CREATEOUTSTREAM(OStr);
      ICDimIO.SETDESTINATION(OStr);
      ICDimIO.EXPORT;
      OFile.CLOSE;
      CLEAR(OStr);

      DOWNLOAD(FileName,'Export',TEMPORARYPATH,'',DefaultFileName);
    END;

    BEGIN
    END.
  }
}

