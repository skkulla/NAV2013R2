OBJECT Report 7053 Implement Price Change
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Implement Price Change;
               ESM=Realizar cambio precio;
               FRC=Appliquer la modification de prix;
               ENC=Implement Price Change];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 5995;    ;DataItem;                    ;
               DataItemTable=Table7023;
               DataItemTableView=SORTING(Starting Date,Ending Date,Sales Type,Sales Code,Currency Code,Item No.,Variant Code,Unit of Measure Code,Minimum Quantity);
               OnPreDataItem=BEGIN
                               Window.OPEN(
                                 Text000 +
                                 Text007 +
                                 Text008 +
                                 Text009 +
                                 Text010 +
                                 Text011);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"Item No.");
                                  Window.UPDATE(2,"Sales Type");
                                  Window.UPDATE(3,"Sales Code");
                                  Window.UPDATE(4,"Currency Code");
                                  Window.UPDATE(5,"Starting Date");

                                  SalesPrice.INIT;
                                  SalesPrice.VALIDATE("Item No.","Item No.");
                                  SalesPrice.VALIDATE("Sales Type","Sales Type");
                                  SalesPrice.VALIDATE("Sales Code","Sales Code");
                                  SalesPrice.VALIDATE("Unit of Measure Code","Unit of Measure Code");
                                  SalesPrice.VALIDATE("Variant Code","Variant Code");
                                  SalesPrice.VALIDATE("Starting Date","Starting Date");
                                  SalesPrice.VALIDATE("Ending Date","Ending Date");
                                  SalesPrice."Minimum Quantity" := "Minimum Quantity";
                                  SalesPrice."Currency Code" := "Currency Code";
                                  SalesPrice."Unit Price" := "New Unit Price";
                                  SalesPrice."Price Includes VAT" := "Price Includes VAT";
                                  SalesPrice."Allow Line Disc." := "Allow Line Disc.";
                                  SalesPrice."Allow Invoice Disc." := "Allow Invoice Disc.";
                                  SalesPrice."VAT Bus. Posting Gr. (Price)" := "VAT Bus. Posting Gr. (Price)";
                                  IF SalesPrice."Unit Price" <> 0 THEN
                                    IF NOT SalesPrice.INSERT(TRUE) THEN
                                      SalesPrice.MODIFY(TRUE);
                                END;

               OnPostDataItem=BEGIN
                                COMMIT;
                                IF NOT DeleteWhstLine THEN
                                  DeleteWhstLine := CONFIRM(Text005);
                                IF DeleteWhstLine THEN
                                  DELETEALL;
                                COMMIT;
                                IF SalesPrice.FINDFIRST THEN;
                              END;

               ReqFilterFields=Item No.,Sales Type,Sales Code,Unit of Measure Code,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Updating Unit Prices...\\;ESM=Modificando precios unitarios...\\;FRC=Mise … jour des prix unitaires...\\;ENC=Updating Unit Prices...\\';
      Text005@1001 : TextConst 'ENU=The item prices have now been updated in accordance with the suggested price changes.\\Do you want to delete the suggested price changes?;ESM=Los precios producto han sido actualizados seg£n la sugerencia de cambios de precio.\\¨Quiere borrar llos cambios de precios sugeridos?;FRC=Les prix article ont ‚t‚ mis … jour conform‚ment aux modifications de prix propos‚es.\\Souhaitez-vous supprimer les modifications de prix propos‚es ?;ENC=The item prices have now been updated in accordance with the suggested price changes.\\Do you want to delete the suggested price changes?';
      Text007@1003 : TextConst 'ENU=Item No.               #1##########\;ESM=N§ producto            #1##########\;FRC=Nø article             #1##########\;ENC=Item No.               #1##########\';
      Text008@1004 : TextConst 'ENU=Sales Type             #2##########\;ESM=Tipo ventas            #2##########\;FRC=Type vente             #2##########\;ENC=Sales Type             #2##########\';
      Text009@1005 : TextConst 'ENU=Sales Code             #3##########\;ESM=C¢d. ventas            #3##########\;FRC=Code vente             #3##########\;ENC=Sales Code             #3##########\';
      Text010@1006 : TextConst 'ENU=Currency Code          #4##########\;ESM=C¢d. divisa            #4##########\;FRC=Code devise            #4##########\;ENC=Currency Code          #4##########\';
      Text011@1007 : TextConst 'ENU=Starting Date          #5######;ESM=Fecha inicial          #5######;FRC=Date d‚but             #5######;ENC=Starting Date          #5######';
      SalesPrice@1008 : Record 7002;
      Window@1009 : Dialog;
      DeleteWhstLine@1010 : Boolean;

    PROCEDURE InitializeRequest@1(NewDeleteWhstLine@1000 : Boolean);
    BEGIN
      DeleteWhstLine := NewDeleteWhstLine;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

