OBJECT Report 189 Suggest Reminder Lines
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Suggest Reminder Lines;
               ESM=Proponer l¡neas recordatorio;
               FRC=Proposer lignes rappel;
               ENC=Suggest Reminder Lines];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  CustLedgEntry.COPY(CustLedgEntry2)
                END;

  }
  DATASET
  {
    { 4775;    ;DataItem;                    ;
               DataItemTable=Table295;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=[ENU=Reminder;
                                   ESM=Recordatorio;
                                   FRC=Rappel;
                                   ENC=Reminder];
               OnPreDataItem=BEGIN
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text000)
                               ELSE BEGIN
                                 Window.OPEN(Text001);
                                 OldTime := TIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(MakeReminder);
                                  MakeReminder.SuggestLines("Reminder Header",CustLedgEntry,OverdueEntriesOnly,IncludeEntriesOnHold);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    MakeReminder.Code;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewTime := TIME;
                                    IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldTime := TIME;
                                    END;
                                    MARK := NOT MakeReminder.RUN;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                COMMIT;
                                Window.CLOSE;
                                MARKEDONLY := TRUE;
                                IF FIND('-') THEN
                                  IF CONFIRM(Text002,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Reminder Header");
                              END;

               ReqFilterFields=No. }

    { 9065;    ;DataItem;CustLedgEntry2      ;
               DataItemTable=Table21;
               DataItemTableView=SORTING(Customer No.);
               OnPreDataItem=BEGIN
                               CurrReport.BREAK;
                             END;

               ReqFilterFields=Document Type }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Only Entries with Overdue Amounts;
                             ESM=S¢lo movs. con importes vencidos;
                             FRC=Seules les ‚critures avec des montants en souffrance;
                             ENC=Only Entries with Overdue Amounts];
                  SourceExpr=OverdueEntriesOnly;
                  MultiLine=Yes }

      { 1102601000;2;Field  ;
                  CaptionML=[ENU=Include Entries On Hold;
                             ESM=Incluir movimientos en espera;
                             FRC=Inclure les ‚critures en attente;
                             ENC=Include Entries On Hold];
                  SourceExpr=IncludeEntriesOnHold }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Suggesting lines...;ESM=Proponiendo l¡neas...;FRC=Sugg‚rer lignes...;ENC=Suggesting lines...';
      Text001@1001 : TextConst 'ENU=Suggesting lines @1@@@@@@@@@@@@@;ESM=Proponiendo l¡neas @1@@@@@@@@@@@@@;FRC=Sugg‚rer lignes @1@@@@@@@@@@@@@;ENC=Suggesting lines @1@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=It was not possible to process some of the selected reminders.\Do you want to see these reminders?;ESM=No se han podido procesar algunos de los recordatorios seleccionados.\¨Desea ver estos recordatorios?;FRC=Il n''a pas ‚t‚ possible de traiter certains rappels s‚lectionn‚s.\Voulez-vous visualiser ces rappelsÿ?;ENC=It was not possible to process some of the selected reminders.\Do you want to see these reminders?';
      CustLedgEntry@1004 : Record 21;
      MakeReminder@1005 : Codeunit 392;
      Window@1006 : Dialog;
      NoOfRecords@1007 : Integer;
      RecordNo@1008 : Integer;
      NewProgress@1009 : Integer;
      OldProgress@1010 : Integer;
      NewTime@1011 : Time;
      OldTime@1012 : Time;
      OverdueEntriesOnly@1013 : Boolean;
      IncludeEntriesOnHold@1102601000 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

