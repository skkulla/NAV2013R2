OBJECT Page 5871 Item Availability by BOM Level
{
  OBJECT-PROPERTIES
  {
    Date=03/13/14;
    Time=12:00:00 PM;
    Version List=NAVW17.10.00.36281;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Availability by BOM Level;
               ESM=Disponibilidad prod. por nivel L.M.;
               FRC=DisponibilitÇ article par niveau de nomenclature;
               ENC=Item Availability by BOM Level];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table5870;
    PageType=Worksheet;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 ShowTotalAvailability := TRUE;
                 IF DemandDate = 0D THEN
                   DemandDate := WORKDATE;
                 RefreshPage;
               END;

    OnAfterGetRecord=VAR
                       DummyBOMWarningLog@1000 : Record 5874;
                     BEGIN
                       IsParentExpr := NOT "Is Leaf";

                       HasWarning := NOT IsLineOk(FALSE,DummyBOMWarningLog);
                     END;

    ActionList=ACTIONS
    {
      { 35      ;    ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 33      ;1   ;ActionGroup;
                      CaptionML=[ENU=&Item Availability by;
                                 ESM=&Disponibilidad prod. por;
                                 FRC=DisponibilitÇ d'&article par;
                                 ENC=&Item Availability by];
                      Image=ItemAvailability }
      { 32      ;2   ;Action    ;
                      CaptionML=[ENU=Event;
                                 ESM=Evento;
                                 FRC=êvÇnement;
                                 ENC=Event];
                      Image=Event;
                      OnAction=BEGIN
                                 ItemAvail(ItemAvailFormsMgt.ByEvent);
                               END;
                                }
      { 31      ;2   ;Action    ;
                      CaptionML=[ENU=Period;
                                 ESM=Periodo;
                                 FRC=PÇriode;
                                 ENC=Period];
                      Image=Period;
                      OnAction=BEGIN
                                 ItemAvail(ItemAvailFormsMgt.ByPeriod);
                               END;
                                }
      { 30      ;2   ;Action    ;
                      CaptionML=[ENU=Variant;
                                 ESM=Variante;
                                 FRC=Variante;
                                 ENC=Variant];
                      Image=ItemVariant;
                      OnAction=BEGIN
                                 ItemAvail(ItemAvailFormsMgt.ByVariant);
                               END;
                                }
      { 29      ;2   ;Action    ;
                      CaptionML=[ENU=Location;
                                 ESM=Lugar;
                                 FRC=Emplacement;
                                 ENC=Location];
                      Image=Warehouse;
                      OnAction=BEGIN
                                 ItemAvail(ItemAvailFormsMgt.ByLocation);
                               END;
                                }
      { 28      ;2   ;Action    ;
                      CaptionML=[ENU=BOM Level;
                                 ESM=Nivel L.M.;
                                 FRC=Niveau nomenclature;
                                 ENC=BOM Level];
                      Image=BOMLevel;
                      OnAction=BEGIN
                                 ItemAvail(ItemAvailFormsMgt.ByBOM);
                               END;
                                }
      { 37      ;0   ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 36      ;1   ;Action    ;
                      CaptionML=[ENU=Previous Period;
                                 ESM=Periodo anterior;
                                 FRC=PÇriode prÇcÇdente;
                                 ENC=Previous Period];
                      ToolTipML=[ENU=Previous Period;
                                 ESM=Periodo anterior;
                                 FRC=PÇriode prÇcÇdente;
                                 ENC=Previous Period];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DemandDate := CALCDATE('<-1D>',DemandDate);
                                 RefreshPage;
                               END;
                                }
      { 34      ;1   ;Action    ;
                      CaptionML=[ENU=Next Period;
                                 ESM=Periodo siguiente;
                                 FRC=Prochaine pÇriode;
                                 ENC=Next Period];
                      ToolTipML=[ENU=Next Period;
                                 ESM=Periodo siguiente;
                                 FRC=Prochaine pÇriode;
                                 ENC=Next Period];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextRecord;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 DemandDate := CALCDATE('<+1D>',DemandDate);
                                 RefreshPage;
                               END;
                                }
      { 40      ;1   ;Action    ;
                      CaptionML=[ENU=Show Warnings;
                                 ESM=Mostrar advertencias;
                                 FRC=Afficher des avertissements;
                                 ENC=Show Warnings];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ErrorLog;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 ShowWarningsForAllLines;
                               END;
                                }
      { 38      ;0   ;ActionContainer;
                      ActionContainerType=Reports }
      { 39      ;1   ;Action    ;
                      CaptionML=[ENU=Item - Able to Make (Timeline);
                                 ESM=Producto - Capaz de producir (escala de tiempo);
                                 FRC=Article - Possible Ö Çtablir (chronologie);
                                 ENC=Item - Able to Make (Timeline)];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Trendscape;
                      PromotedCategory=Report;
                      OnAction=BEGIN
                                 ShowAbleToMakeTimeline;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1   ;0   ;Container ;
                ContainerType=ContentArea }

    { 20  ;1   ;Group     ;
                CaptionML=[ENU=Option;
                           ESM=Opci¢n;
                           FRC=Option;
                           ENC=Option];
                GroupType=Group }

    { 19  ;2   ;Field     ;
                CaptionML=[ENU=Item Filter;
                           ESM=Filtro producto;
                           FRC=Filtre article;
                           ENC=Item Filter];
                SourceExpr=ItemFilter;
                OnValidate=BEGIN
                             IsCalculated := FALSE;
                             RefreshPage;
                           END;

                OnLookup=VAR
                           Item@1001 : Record 27;
                           ItemList@1000 : Page 31;
                         BEGIN
                           ItemList.SETTABLEVIEW(Item);
                           ItemList.LOOKUPMODE := TRUE;
                           IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ItemList.GETRECORD(Item);
                             Text := Item."No.";
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 17  ;2   ;Field     ;
                CaptionML=[ENU=Location Filter;
                           ESM=Filtro almacÇn;
                           FRC=Filtre emplacement;
                           ENC=Location Filter];
                SourceExpr=LocationFilter;
                OnValidate=BEGIN
                             IsCalculated := FALSE;
                             RefreshPage;
                           END;

                OnLookup=VAR
                           Location@1001 : Record 14;
                           LocationList@1000 : Page 15;
                         BEGIN
                           LocationList.SETTABLEVIEW(Location);
                           LocationList.LOOKUPMODE := TRUE;
                           IF LocationList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             LocationList.GETRECORD(Location);
                             Text := Location.Code;
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 16  ;2   ;Field     ;
                CaptionML=[ENU=Variant Filter;
                           ESM=Filtro variante;
                           FRC=Filtre variante;
                           ENC=Variant Filter];
                SourceExpr=VariantFilter;
                OnValidate=BEGIN
                             IsCalculated := FALSE;
                             RefreshPage;
                           END;

                OnLookup=VAR
                           ItemVariant@1001 : Record 5401;
                           ItemVariants@1000 : Page 5401;
                         BEGIN
                           ItemVariant.SETFILTER("Item No.",ItemFilter);
                           ItemVariants.SETTABLEVIEW(ItemVariant);
                           ItemVariants.LOOKUPMODE := TRUE;
                           IF ItemVariants.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             ItemVariants.GETRECORD(ItemVariant);
                             Text := ItemVariant.Code;
                             EXIT(TRUE);
                           END;
                           EXIT(FALSE);
                         END;
                          }

    { 18  ;2   ;Field     ;
                CaptionML=[ENU=Demand Date;
                           ESM=Fecha demanda;
                           FRC=Date de rÇquisition;
                           ENC=Demand Date];
                SourceExpr=DemandDate;
                OnValidate=BEGIN
                             IsCalculated := FALSE;
                             RefreshPage;
                           END;
                            }

    { 10  ;2   ;Field     ;
                CaptionML=[ENU=Calculated;
                           ESM=Calculado;
                           FRC=CalculÇ;
                           ENC=Calculated];
                SourceExpr=IsCalculated;
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=Show Total Availability;
                           ESM=Mostrar disponibilidad total;
                           FRC=Afficher disponibilitÇ totale;
                           ENC=Show Total Availability];
                SourceExpr=ShowTotalAvailability;
                OnValidate=BEGIN
                             IsCalculated := FALSE;
                             RefreshPage;
                           END;
                            }

    { 2   ;1   ;Group     ;
                Name=Group;
                CaptionML=[ENU=Lines;
                           ESM=L°neas;
                           FRC=Lignes;
                           ENC=Lines];
                IndentationColumnName=Indentation;
                ShowAsTree=Yes;
                GroupType=Repeater }

    { 4   ;2   ;Field     ;
                SourceExpr="No.";
                Editable=FALSE;
                Style=Strong;
                StyleExpr=IsParentExpr }

    { 14  ;2   ;Field     ;
                SourceExpr=Description;
                Editable=FALSE }

    { 41  ;2   ;Field     ;
                CaptionML=[ENU=Warning;
                           ESM=Advertencia;
                           FRC=Avertissement;
                           ENC=Warning];
                BlankZero=Yes;
                SourceExpr=HasWarning;
                Editable=FALSE;
                Style=Attention;
                StyleExpr=HasWarning;
                OnDrillDown=BEGIN
                              IF HasWarning THEN
                                ShowWarnings;
                            END;
                             }

    { 27  ;2   ;Field     ;
                SourceExpr=Bottleneck;
                Editable=FALSE;
                Style=Strong;
                StyleExpr=TRUE }

    { 6   ;2   ;Field     ;
                SourceExpr="Variant Code";
                Visible=FALSE }

    { 21  ;2   ;Field     ;
                DecimalPlaces=0:5;
                SourceExpr="Qty. per Parent";
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                DecimalPlaces=0:5;
                SourceExpr="Qty. per Top Item";
                Visible=FALSE;
                Editable=FALSE }

    { 23  ;2   ;Field     ;
                SourceExpr="Unit of Measure Code";
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                SourceExpr="Replenishment System";
                Editable=FALSE }

    { 13  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Available Quantity";
                Editable=FALSE }

    { 3   ;2   ;Field     ;
                SourceExpr="Unused Quantity";
                Visible=FALSE }

    { 15  ;2   ;Field     ;
                SourceExpr="Needed by Date";
                Editable=FALSE }

    { 11  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Able to Make Parent";
                Editable=FALSE }

    { 12  ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Able to Make Top Item";
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Gross Requirement";
                Visible=FALSE;
                Editable=FALSE }

    { 7   ;2   ;Field     ;
                BlankZero=Yes;
                SourceExpr="Scheduled Receipts";
                Visible=FALSE;
                Editable=FALSE }

    { 5   ;2   ;Field     ;
                SourceExpr="Safety Lead Time";
                Visible=FALSE;
                Editable=FALSE }

    { 25  ;2   ;Field     ;
                SourceExpr="Rolled-up Lead-Time Offset";
                Visible=FALSE }

  }
  CODE
  {
    VAR
      Item@1004 : Record 27;
      AsmHeader@1009 : Record 900;
      ProdOrderLine@1002 : Record 5406;
      ItemAvailFormsMgt@1011 : Codeunit 353;
      IsParentExpr@1010 : Boolean INDATASET;
      ItemFilter@1008 : Code[250];
      LocationFilter@1007 : Code[250];
      VariantFilter@1006 : Code[250];
      DemandDate@1003 : Date;
      IsCalculated@1000 : Boolean;
      ShowTotalAvailability@1001 : Boolean;
      ShowBy@1005 : 'Item,Assembly,Production';
      Text000@1012 : TextConst 'ENU=Could not find items with BOM levels.;ESM=No se encontraron productos con niveles L.M.;FRC=Impossible de trouver des articles avec des niveaux de nomenclature.;ENC=Could not find items with BOM levels.';
      Text001@1013 : TextConst 'ENU=There are no warnings.;ESM=No hay advertencias.;FRC=Il n''y a pas d''alerte.;ENC=There are no warnings.';
      HasWarning@1014 : Boolean INDATASET;

    PROCEDURE InitItem@1(VAR NewItem@1000 : Record 27);
    BEGIN
      Item.COPY(NewItem);
      ItemFilter := Item."No.";
      VariantFilter := Item.GETFILTER("Variant Filter");
      LocationFilter := Item.GETFILTER("Location Filter");
      ShowBy := ShowBy::Item;
    END;

    PROCEDURE InitAsmOrder@5(NewAsmHeader@1000 : Record 900);
    BEGIN
      AsmHeader := NewAsmHeader;
      ShowBy := ShowBy::Assembly;
    END;

    PROCEDURE InitProdOrder@6(NewProdOrderLine@1000 : Record 5406);
    BEGIN
      ProdOrderLine := NewProdOrderLine;
      ShowBy := ShowBy::Production;
    END;

    PROCEDURE InitDate@4(NewDemandDate@1000 : Date);
    BEGIN
      DemandDate := NewDemandDate;
    END;

    LOCAL PROCEDURE RefreshPage@2();
    VAR
      CalcBOMTree@1000 : Codeunit 5870;
    BEGIN
      CalcBOMTree.SetShowTotalAvailability(ShowTotalAvailability);
      CASE ShowBy OF
        ShowBy::Item:
          BEGIN
            Item.SETRANGE("Date Filter",0D,DemandDate);
            Item.SETFILTER("Location Filter",LocationFilter);
            Item.SETFILTER("Variant Filter",VariantFilter);
            Item.SETFILTER("No.",ItemFilter);
            Item.FINDFIRST;
            IF NOT Item.HasBOM THEN
              ERROR(Text000);
            CalcBOMTree.GenerateTreeForItems(Item,Rec,1);
          END;
        ShowBy::Production:
          BEGIN
            ProdOrderLine."Due Date" := DemandDate;
            CalcBOMTree.GenerateTreeForProdLine(ProdOrderLine,Rec,1);
          END;
        ShowBy::Assembly:
          BEGIN
            AsmHeader."Due Date" := DemandDate;
            CalcBOMTree.GenerateTreeForAsm(AsmHeader,Rec,1);
          END;
      END;

      CurrPage.UPDATE(FALSE);
      IsCalculated := TRUE;
    END;

    PROCEDURE GetSelectedDate@3() : Date;
    BEGIN
      EXIT(DemandDate);
    END;

    LOCAL PROCEDURE ItemAvail@7(AvailType@1000 : Option);
    VAR
      Item@1001 : Record 27;
    BEGIN
      TESTFIELD(Type,Type::Item);

      Item.GET("No.");
      Item.SETFILTER("No.","No.");
      Item.SETRANGE("Date Filter",0D,"Needed by Date");
      Item.SETFILTER("Location Filter",LocationFilter);
      Item.SETFILTER("Variant Filter","Variant Code");
      IF ShowBy <> ShowBy::Item THEN
        Item.SETFILTER("Location Filter","Location Code");
      IF Indentation = 0 THEN
        Item.SETFILTER("Variant Filter",VariantFilter);

      ItemAvailFormsMgt.ShowItemAvailFromItem(Item,AvailType);
    END;

    LOCAL PROCEDURE ShowAbleToMakeTimeline@8();
    VAR
      Item@1001 : Record 27;
      ItemAbleToMakeTimeline@1002 : Report 5871;
    BEGIN
      TESTFIELD(Type,Type::Item);

      Item.GET("No.");
      Item.SETFILTER("No.","No.");

      WITH ItemAbleToMakeTimeline DO BEGIN
        IF Indentation = 0 THEN BEGIN
          CASE ShowBy OF
            ShowBy::Item:
              BEGIN
                Item.SETFILTER("Location Filter",LocationFilter);
                Item.SETFILTER("Variant Filter",VariantFilter);
              END;
            ShowBy::Assembly:
              InitAsmOrder(AsmHeader);
            ShowBy::Production:
              InitProdOrder(ProdOrderLine);
          END;
        END ELSE BEGIN
          Item.SETFILTER("Location Filter",LocationFilter);
          Item.SETFILTER("Variant Filter",VariantFilter);
        END;

        SETTABLEVIEW(Item);
        Initialize("Needed by Date",0,7,TRUE);
        RUN;
      END;
    END;

    LOCAL PROCEDURE ShowWarnings@10();
    VAR
      TempBOMWarningLog@1001 : TEMPORARY Record 5874;
    BEGIN
      IF IsLineOk(TRUE,TempBOMWarningLog) THEN
        MESSAGE(Text001)
      ELSE
        PAGE.RUNMODAL(PAGE::"BOM Warning Log",TempBOMWarningLog);
    END;

    LOCAL PROCEDURE ShowWarningsForAllLines@27();
    VAR
      TempBOMWarningLog@1001 : TEMPORARY Record 5874;
    BEGIN
      IF AreAllLinesOk(TempBOMWarningLog) THEN
        MESSAGE(Text001)
      ELSE
        PAGE.RUNMODAL(PAGE::"BOM Warning Log",TempBOMWarningLog);
    END;

    BEGIN
    END.
  }
}

