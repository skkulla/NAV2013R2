OBJECT Page 10007 Customer Credit Information
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVNA7.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Customer Credit Information;
               ESM=Informaci¢n crÇdito cliente;
               FRC=Renseignements sur crÇdit client;
               ENC=Customer Credit Information];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table18;
    PageType=ListPart;
    OnInit=BEGIN
             "Payment Method CodeEditable" := TRUE;
             "Collection MethodEditable" := TRUE;
             "Payment Terms CodeEditable" := TRUE;
             "Credit Limit (LCY)Editable" := TRUE;
             BlockedEditable := TRUE;
             "Phone No.Editable" := TRUE;
             ContactEditable := TRUE;
             // Default the Aging Period to 30D
             EVALUATE(AgingPeriod,'<30D>');
           END;

    OnOpenPage=BEGIN
                 // Initialize Record Variables
                 LatestCustLedgerEntry.RESET;
                 LatestCustLedgerEntry.SETCURRENTKEY("Document Type","Customer No.","Posting Date");
                 LatestCustLedgerEntry.SETRANGE("Document Type",LatestCustLedgerEntry."Document Type"::Payment);
                 FOR I := 1 TO ARRAYLEN(CustLedgerEntry) DO BEGIN
                   CustLedgerEntry[I].RESET;
                   CustLedgerEntry[I].SETCURRENTKEY("Customer No.",Open,Positive,"Due Date");
                   CustLedgerEntry[I].SETRANGE(Open,TRUE);
                 END;
                 // Turn off editability on Certain fields by default
                 OnCreditManagementForm(FALSE);
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 6   ;1   ;Field     ;
                SourceExpr=Contact;
                Editable=ContactEditable }

    { 8   ;1   ;Field     ;
                SourceExpr="Phone No.";
                Editable="Phone No.Editable" }

    { 26  ;1   ;Field     ;
                SourceExpr="Collection Method";
                Editable="Collection MethodEditable" }

    { 1020000;1;Field     ;
                SourceExpr=Blocked;
                Editable=BlockedEditable }

    { 39  ;1   ;Group      }

    { 41  ;2   ;Field     ;
                SourceExpr="Credit Limit (LCY)";
                Editable="Credit Limit (LCY)Editable" }

    { 42  ;2   ;Field     ;
                SourceExpr="Balance (LCY)";
                Editable=FALSE }

    { 47  ;2   ;Field     ;
                CaptionML=[ENU=Difference;
                           ESM=Diferencia;
                           FRC=DiffÇrence;
                           ENC=Difference];
                SourceExpr="Credit Limit (LCY)" - "Balance (LCY)";
                Editable=FALSE }

    { 53  ;2   ;Field     ;
                SourceExpr=CustLedgerEntry[1]."Remaining Amt. (LCY)";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDown(1);
                            END;
                             }

    { 46  ;2   ;Field     ;
                CaptionML=[ENU=Aging Date;
                           ESM=Fecha antig.;
                           FRC=Dates classÇes chrono.;
                           ENC=Aging Date];
                CaptionClass=FORMAT(STRSUBSTNO(Text000,WORKDATE));
                Editable=FALSE }

    { 51  ;2   ;Field     ;
                SourceExpr=CustLedgerEntry[2]."Remaining Amt. (LCY)";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDown(2);
                            END;
                             }

    { 1   ;2   ;Field     ;
                SourceExpr=AgingTitle[4];
                CaptionClass=FORMAT(AgingTitle[3]);
                Editable=FALSE }

    { 50  ;2   ;Field     ;
                SourceExpr=CustLedgerEntry[3]."Remaining Amt. (LCY)";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDown(3);
                            END;
                             }

    { 45  ;2   ;Field     ;
                SourceExpr=CustLedgerEntry[4]."Remaining Amt. (LCY)";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDown(4);
                            END;
                             }

    { 14  ;1   ;Field     ;
                SourceExpr="Payment Terms Code";
                Editable="Payment Terms CodeEditable" }

    { 28  ;1   ;Field     ;
                SourceExpr="Payment Method Code";
                Editable="Payment Method CodeEditable" }

    { 18  ;1   ;Field     ;
                CaptionML=[ENU=Latest Payment Date;
                           ESM=Èltima fecha pago;
                           FRC=Date du plus rÇcent paiement;
                           ENC=Latest Payment Date];
                SourceExpr=LatestCustLedgerEntry."Posting Date";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDown(0);
                            END;
                             }

    { 21  ;1   ;Field     ;
                CaptionML=[ENU=Latest Payment Amount;
                           ESM=Èltimo importe pagos;
                           FRC=Montant du plus rÇcent paiement;
                           ENC=Latest Payment Amount];
                SourceExpr=-LatestCustLedgerEntry."Amount (LCY)";
                Editable=FALSE;
                OnDrillDown=BEGIN
                              DrillDown(0);
                            END;
                             }

  }
  CODE
  {
    VAR
      LatestCustLedgerEntry@1020000 : Record 21;
      CustLedgerEntry@1020001 : ARRAY [4] OF Record 21;
      AgingTitle@1020002 : ARRAY [4] OF Text[30];
      AgingPeriod@1020003 : DateFormula;
      I@1020004 : Integer;
      PeriodStart@1020005 : Date;
      PeriodEnd@1020006 : Date;
      Text000@1020007 : TextConst 'ENU=Aging as of %1 (showing days overdue);ESM=AntigÅedad: %1 (d°as de retraso);FRC=Classement chrono. Ö partir du %1;ENC=Aging as of %1 (showing days overdue)';
      Text002@1020009 : TextConst 'ENU=Not Yet Due;ESM=Sin retraso;FRC=Pas encore dñ;ENC=Not Yet Due';
      Text003@1020010 : TextConst 'ENU=Over %1 Days;ESM=M†s de %1 d°as;FRC=Plus de %1 jours;ENC=Over %1 Days';
      Text004@1020011 : TextConst 'ENU=%1-%2 Days;ESM=%1-%2 d°as;FRC=%1 Ö %2 jours;ENC=%1-%2 Days';
      ContactEditable@19051436 : Boolean INDATASET;
      "Phone No.Editable"@19012872 : Boolean INDATASET;
      BlockedEditable@19000077 : Boolean INDATASET;
      "Credit Limit (LCY)Editable"@19017780 : Boolean INDATASET;
      "Payment Terms CodeEditable"@19002263 : Boolean INDATASET;
      "Collection MethodEditable"@19059156 : Boolean INDATASET;
      "Payment Method CodeEditable"@19074465 : Boolean INDATASET;

    PROCEDURE OnCreditManagementForm@1(SetOnCreditManagementForm@1020000 : Boolean);
    BEGIN
      // Make certain Fields editable
      ContactEditable := SetOnCreditManagementForm;
      "Phone No.Editable" := SetOnCreditManagementForm;
      BlockedEditable := SetOnCreditManagementForm;
      "Credit Limit (LCY)Editable" := SetOnCreditManagementForm;
      "Payment Terms CodeEditable" := SetOnCreditManagementForm;
      "Collection MethodEditable" := SetOnCreditManagementForm;
      "Payment Method CodeEditable" := SetOnCreditManagementForm;
    END;

    PROCEDURE CalculateAgingForPeriod@7(PeriodBeginDate@1020000 : Date;PeriodEndDate@1020001 : Date;Index@1020002 : Integer);
    VAR
      CustLedgerEntry2@1020005 : Record 21;
      NumDaysToBegin@1020003 : Integer;
      NumDaysToEnd@1020004 : Integer;
    BEGIN
      // Calculate the Aged Balance for a particular Date Range
      IF PeriodEndDate = 0D THEN
        CustLedgerEntry[Index].SETFILTER("Due Date",'%1..',PeriodBeginDate)
      ELSE
        CustLedgerEntry[Index].SETRANGE("Due Date",PeriodBeginDate,PeriodEndDate);

      CustLedgerEntry2.COPY(CustLedgerEntry[Index]);
      CustLedgerEntry[Index]."Remaining Amt. (LCY)" := 0;
      IF CustLedgerEntry2.FIND('-') THEN
        REPEAT
          CustLedgerEntry2.CALCFIELDS("Remaining Amt. (LCY)");
          CustLedgerEntry[Index]."Remaining Amt. (LCY)" :=
            CustLedgerEntry[Index]."Remaining Amt. (LCY)" + CustLedgerEntry2."Remaining Amt. (LCY)";
        UNTIL CustLedgerEntry2.NEXT = 0;

      IF PeriodBeginDate <> 0D THEN
        NumDaysToBegin := WORKDATE - PeriodBeginDate;
      IF PeriodEndDate <> 0D THEN
        NumDaysToEnd := WORKDATE - PeriodEndDate;
      IF PeriodEndDate = 0D THEN
        AgingTitle[Index] := Text002
      ELSE
        IF PeriodBeginDate = 0D THEN
          AgingTitle[Index] := STRSUBSTNO(Text003,NumDaysToEnd - 1)
        ELSE
          AgingTitle[Index] := STRSUBSTNO(Text004,NumDaysToEnd,NumDaysToBegin);
    END;

    PROCEDURE CalculateAging@5();
    BEGIN
      // Calculate the Entire Aging (four Periods)
      FOR I := 1 TO ARRAYLEN(CustLedgerEntry) DO BEGIN
        CASE I OF
          1:
            BEGIN
              PeriodEnd := 0D;
              PeriodStart := WORKDATE;
            END;
          ARRAYLEN(CustLedgerEntry):
            BEGIN
              PeriodEnd := PeriodStart - 1;
              PeriodStart := 0D;
            END;
          ELSE
            BEGIN
            PeriodEnd := PeriodStart - 1;
            PeriodStart := CALCDATE('-' + FORMAT(AgingPeriod),PeriodStart);
          END;
        END;
        CalculateAgingForPeriod(PeriodStart,PeriodEnd,I);
      END;
    END;

    PROCEDURE GetLatestPayment@6();
    BEGIN
      // Find the Latest Payment
      IF LatestCustLedgerEntry.FINDLAST THEN
        LatestCustLedgerEntry.CALCFIELDS("Amount (LCY)")
      ELSE
        LatestCustLedgerEntry.INIT;
    END;

    PROCEDURE ChangeCustomer@2();
    BEGIN
      // Change the Customer Filters
      LatestCustLedgerEntry.SETRANGE("Customer No.","No.");
      FOR I := 1 TO ARRAYLEN(CustLedgerEntry) DO
        CustLedgerEntry[I].SETRANGE("Customer No.","No.");
    END;

    PROCEDURE DrillDown@11(Index@1020000 : Integer);
    BEGIN
      IF Index = 0 THEN
        PAGE.RUNMODAL(PAGE::"Customer Ledger Entries",LatestCustLedgerEntry)
      ELSE
        PAGE.RUNMODAL(PAGE::"Customer Ledger Entries",CustLedgerEntry[Index]);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      ChangeCustomer;
      GetLatestPayment;
      CalculateAging;
    END;

    BEGIN
    END.
  }
}

