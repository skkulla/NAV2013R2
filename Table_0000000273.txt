OBJECT Table 273 Bank Acc. Reconciliation
{
  OBJECT-PROPERTIES
  {
    Date=12/02/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10.00.35800;
  }
  PROPERTIES
  {
    DataCaptionFields=Bank Account No.,Statement No.;
    OnInsert=BEGIN
               TESTFIELD("Bank Account No.");
               TESTFIELD("Statement No.");
               BankAccRecon.RESET;
               BankAccRecon.SETRANGE("Bank Account No.","Bank Account No.");
               IF PostedBankAccStmt.GET("Bank Account No.","Statement No.") THEN
                 ERROR(Text000,"Statement No.");
             END;

    OnDelete=BEGIN
               BankAccReconLine.RESET;
               BankAccReconLine.SETRANGE("Bank Account No.","Bank Account No.");
               BankAccReconLine.SETRANGE("Statement No.","Statement No.");
               BankAccReconLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text001,TABLECAPTION);
             END;

    CaptionML=[ENU=Bank Acc. Reconciliation;
               ESM=Conciliaci¢n banco;
               FRC=Rapprochement compte bancaire;
               ENC=Bank Acc. Reconciliation];
    LookupPageID=Page388;
  }
  FIELDS
  {
    { 1   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   OnValidate=BEGIN
                                                                IF "Statement No." = '' THEN BEGIN
                                                                  BankAcc.LOCKTABLE;
                                                                  BankAcc.GET("Bank Account No.");
                                                                  "Statement No." := INCSTR(BankAcc."Last Statement No.");
                                                                  "Balance Last Statement" := BankAcc."Balance Last Statement";
                                                                  BankAcc."Last Statement No." := "Statement No.";
                                                                  BankAcc.MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              ESM=C¢d. cuenta banco;
                                                              FRC=Nø compte bancaire;
                                                              ENC=Bank Account No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Statement No.       ;Code20        ;OnValidate=BEGIN
                                                                TESTFIELD("Bank Account No.");
                                                                BankAcc.LOCKTABLE;
                                                                BankAcc.GET("Bank Account No.");
                                                                BankAcc."Last Statement No." := "Statement No.";
                                                                BankAcc.MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Statement No.;
                                                              ESM=N§ estado de cta. banco;
                                                              FRC=Nø de relev‚;
                                                              ENC=Statement No.];
                                                   NotBlank=Yes }
    { 3   ;   ;Statement Ending Balance;Decimal   ;CaptionML=[ENU=Statement Ending Balance;
                                                              ESM=Saldo final estado de cuenta;
                                                              FRC=Solde final du relev‚;
                                                              ENC=Statement Ending Balance];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 4   ;   ;Statement Date      ;Date          ;CaptionML=[ENU=Statement Date;
                                                              ESM=Fecha estado de cuenta banco;
                                                              FRC=Date du relev‚;
                                                              ENC=Statement Date] }
    { 5   ;   ;Balance Last Statement;Decimal     ;OnValidate=BEGIN
                                                                BankAcc.GET("Bank Account No.");
                                                                IF "Balance Last Statement" <> BankAcc."Balance Last Statement" THEN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       Text002,FALSE,
                                                                       FIELDCAPTION("Balance Last Statement"),BankAcc.FIELDCAPTION("Balance Last Statement"),
                                                                       BankAcc.TABLECAPTION)
                                                                  THEN
                                                                    "Balance Last Statement" := xRec."Balance Last Statement";
                                                              END;

                                                   CaptionML=[ENU=Balance Last Statement;
                                                              ESM=Saldo £lt. edo. cta. banco;
                                                              FRC=Solde dernier relev‚;
                                                              ENC=Balance Last Statement];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 6   ;   ;Bank Statement      ;BLOB          ;CaptionML=[ENU=Bank Statement;
                                                              ESM=Estado de cuenta banco;
                                                              FRC=Relev‚ bancaire;
                                                              ENC=Bank Statement] }
  }
  KEYS
  {
    {    ;Bank Account No.,Statement No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Statement %1 already exists.;ESM=El estado de cta. banco %1 ya existe.;FRC=Le relev‚ %1 existe d‚j….;ENC=Statement %1 already exists.';
      Text001@1001 : TextConst 'ENU=You cannot rename a %1.;ESM=No se puede cambiar el nombre a %1.;FRC=Vous ne pouvez pas renommer un %1.;ENC=You cannot rename a %1.';
      Text002@1002 : TextConst 'ENU=%1 is different from %2 on the %3. Do you want to change the value?;ESM=%1 es diferente del %2 en el %3. ¨Confirma que desea cambiar el valor?;FRC=%1 est diff‚rent de %2 sur le %3. Voulez-vous changer la valeur?;ENC=%1 is different from %2 on the %3. Do you want to change the value?';
      BankAcc@1003 : Record 270;
      BankAccRecon@1004 : Record 273;
      BankAccReconLine@1005 : Record 274;
      PostedBankAccStmt@1006 : Record 275;

    PROCEDURE GetCurrencyCode@1() : Code[10];
    VAR
      BankAcc2@1000 : Record 270;
    BEGIN
      IF "Bank Account No." = BankAcc2."No." THEN
        EXIT(BankAcc2."Currency Code");

      IF BankAcc2.GET("Bank Account No.") THEN
        EXIT(BankAcc2."Currency Code");

      EXIT('');
    END;

    PROCEDURE MatchSingle@5(DateRange@1008 : Integer);
    VAR
      MatchBankRecLines@1000 : Codeunit 1252;
    BEGIN
      MatchBankRecLines.MatchSingle(Rec,DateRange);
    END;

    PROCEDURE ImportBankStatement@6();
    VAR
      ProcessBankAccRecLines@1000 : Codeunit 1248;
    BEGIN
      ProcessBankAccRecLines.ImportBankStatement(Rec);
    END;

    BEGIN
    END.
  }
}

