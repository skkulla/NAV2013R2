OBJECT Codeunit 5531 Calc. Inventory Page Data
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempInvtEventBuf@1003 : TEMPORARY Record 5530;
      CalcItemAvailability@1001 : Codeunit 5530;
      PageLineNo@1000 : Integer;
      Text0000@1002 : TextConst 'ENU="Illegal Entity Source Type = %1, Source Subtype = %2.";ESM="Tipo de origen de entidad no v lido = %1, subtipo de origen = %2.";FRC="Entit‚ non conforme Type origine = %1, Sous-type origine = %2.";ENC="Illegal Entity Source Type = %1, Source Subtype = %2."';
      Text0032@1013 : TextConst 'ENU=Inventory at SKU;ESM=Inventario en UA;FRC=Inventaire … l''unit‚ de stock;ENC=Inventory at SKU';
      Text0036@1012 : TextConst 'ENU=Sales %1;ESM=Ventas %1;FRC=Vente %1;ENC=Sales %1';
      Text0038@1011 : TextConst 'ENU=Purchase %1;ESM=Compra %1;FRC=Achat %1;ENC=Purchase %1';
      Text0246@1015 : TextConst 'ENU=Plan Reverted;ESM=Planificar revertidos;FRC=Changement de plan;ENC=Plan Reverted';
      Text1003@1009 : TextConst 'ENU=Job %1;ESM=Proyecto %1;FRC=Projet %1;ENC=Job %1';
      Text5405@1008 : TextConst 'ENU=Production %1;ESM=Producci¢n %1;FRC=Fabrication %1;ENC=Production %1';
      Text5407@1007 : TextConst 'ENU=Component %1;ESM=Componente %1;FRC=Composante %1;ENC=Component %1';
      Text5740@1006 : TextConst 'ENU=Transfer %1;ESM=Transferencia %1;FRC=Transfert %1;ENC=Transfer %1';
      Text5900@1005 : TextConst 'ENU=Service %1;ESM=Servicio %1;FRC=Service %1;ENC=Service %1';
      Text9851@1004 : TextConst 'ENU=Forecast Sales;ESM=Ventas previstas;FRC=Pr‚voir ventes;ENC=Forecast Sales';
      Text9852@1014 : TextConst 'ENU=Forecast Component;ESM=Componente de previsi¢n;FRC=Pr‚voir composante;ENC=Forecast Component';
      Text900@1017 : TextConst 'ENU=Assembly Order %1;ESM=Pedido de ensamblado %1;FRC=Ordre d''assemblage %1;ENC=Assembly Order %1';
      Text901@1018 : TextConst 'ENU=Assembly Component %1;ESM=Componente del ensamblado %1;FRC=Composante d''assemblage %1;ENC=Assembly Component %1';

    PROCEDURE Initialize@1(VAR Item@1004 : Record 27;ForecastName@1003 : Code[10];IncludeBlanketOrders@1002 : Boolean;ExcludeForecastBefore@1001 : Date;IncludePlan@1000 : Boolean);
    BEGIN
      TempInvtEventBuf.RESET;
      TempInvtEventBuf.DELETEALL;

      CalcItemAvailability.CalcNewInvtEventBuf(Item,ForecastName,IncludeBlanketOrders,ExcludeForecastBefore,IncludePlan);
      CalcItemAvailability.GetInvEventBuffer(TempInvtEventBuf);
    END;

    PROCEDURE CreatePeriodEntries@18(VAR InvtPageData@1001 : Record 5531;PeriodType@1002 : 'Day,Week,Month,Quarter,Year');
    VAR
      Date@1000 : Record 2000000007;
      LastDateInPeriod@1003 : Date;
    BEGIN
      TempInvtEventBuf.RESET;
      TempInvtEventBuf.SETCURRENTKEY("Availability Date",Type);
      IF TempInvtEventBuf.FIND('-') THEN
        REPEAT
          IF TempInvtEventBuf.Type = TempInvtEventBuf.Type::Inventory THEN BEGIN
            InvtPageData.INIT;
            InvtPageData.Code := '';
            InvtPageData."Line No." := NextPageLineNo;
            InvtPageData."Period Type" := Date."Period Type";
            InvtPageData."Period Start" := TempInvtEventBuf."Availability Date";
            InvtPageData.Description := FORMAT(TempInvtEventBuf.Type);
            InvtPageData.Level := 0;
            InvtPageData.INSERT;
            LastDateInPeriod := TempInvtEventBuf."Availability Date";
          END ELSE BEGIN
            Date.SETRANGE("Period Type",PeriodType);
            Date.SETFILTER("Period Start",'<=%1',TempInvtEventBuf."Availability Date");
            IF Date.FINDLAST THEN BEGIN
              InvtPageData.INIT;
              InvtPageData.Code := FORMAT(Date."Period Start");
              InvtPageData."Line No." := NextPageLineNo;
              InvtPageData."Period Type" := Date."Period Type";
              InvtPageData."Period Start" := Date."Period Start";
              InvtPageData."Period End" := Date."Period End";
              InvtPageData.Description := FormatPeriodDescription(Date);
              InvtPageData.Level := 0;
              InvtPageData.INSERT;
              LastDateInPeriod := Date."Period End";
            END;
          END;
          TempInvtEventBuf.SETFILTER("Availability Date",'<=%1',LastDateInPeriod);
          TempInvtEventBuf.FIND('+');
          TempInvtEventBuf.SETRANGE("Availability Date");
        UNTIL TempInvtEventBuf.NEXT = 0;
      TempInvtEventBuf.RESET;
    END;

    PROCEDURE DetailsForPeriodEntry@21(VAR InvtPageData@1000 : Record 5531;Positive@1002 : Boolean);
    VAR
      PeriodInvtPageData@1001 : Record 5531;
    BEGIN
      PeriodInvtPageData.COPY(InvtPageData);
      TempInvtEventBuf.RESET;
      TempInvtEventBuf.SETCURRENTKEY("Availability Date",Type);
      TempInvtEventBuf.SETRANGE("Availability Date",InvtPageData."Period Start",InvtPageData."Period End");
      TempInvtEventBuf.SETRANGE(Positive,Positive);
      IF TempInvtEventBuf.FIND('-') THEN
        REPEAT
          TransferToPeriodDetails(InvtPageData,TempInvtEventBuf);
          UpdateInventory(InvtPageData,TempInvtEventBuf);
          InvtPageData."Line No." := NextPageLineNo;
          InvtPageData.INSERT;
          UpdatePeriodTotals(PeriodInvtPageData,InvtPageData);
          UpdateInventory(PeriodInvtPageData,TempInvtEventBuf);
        UNTIL TempInvtEventBuf.NEXT = 0;
      InvtPageData.COPY(PeriodInvtPageData);
      InvtPageData.MODIFY;
    END;

    LOCAL PROCEDURE TransferToPeriodDetails@20(VAR InventoryPageData@1001 : Record 5531;VAR FromInvtEventBuf@1000 : Record 5530);
    VAR
      SalesHeader@1008 : Record 36;
      PurchHeader@1010 : Record 38;
      ReqLine@1015 : Record 246;
      TransHeader@1011 : Record 5740;
      ServHeader@1013 : Record 5900;
      Job@1014 : Record 167;
      ProdOrder@1012 : Record 5405;
      ProdForecastName@1016 : Record 99000851;
      ProdForecastEntry@1017 : Record 99000852;
      AssemblyHeader@1018 : Record 900;
      AssemblyLine@1019 : Record 901;
      RecRef@1009 : RecordRef;
      SourceType@1007 : Integer;
      SourceSubtype@1006 : '0,1,2,3,4,5,6,7,8,9,10';
      SourceID@1005 : Code[20];
      SourceBatchName@1004 : Code[10];
      SourceProdOrderLine@1003 : Integer;
      SourceRefNo@1002 : Integer;
    BEGIN
      InventoryPageData.INIT;

      WITH FromInvtEventBuf DO BEGIN
        InventoryPageData.Code := FORMAT("Availability Date");
        InventoryPageData."Period Start" := "Availability Date";
        InventoryPageData."Availability Date" := "Availability Date";
        InventoryPageData.Level := 1;
        InventoryPageData."Source Line ID" := "Source Line ID";
        InventoryPageData."Item No." := "Item No.";
        InventoryPageData."Variant Code" := "Variant Code";
        InventoryPageData."Location Code" := "Location Code";
        InventoryPageData."Remaining Quantity (Base)" := "Remaining Quantity (Base)";
        InventoryPageData.Positive := Positive;
        CalcItemAvailability.GetSourceReferences("Source Line ID","Transfer Direction",
          SourceType,SourceSubtype,SourceID,SourceBatchName,SourceProdOrderLine,SourceRefNo);
        CASE SourceType OF
          DATABASE::"Item Ledger Entry":
            BEGIN
              InventoryPageData.Type := InventoryPageData.Type::" ";
              InventoryPageData.Description := Text0032;
              InventoryPageData.Source := "Location Code" + ' ' + "Variant Code";
              InventoryPageData."Scheduled Receipt" := "Remaining Quantity (Base)";
            END;
          DATABASE::"Sales Line":
            BEGIN
              SalesHeader.GET(SourceSubtype,SourceID);
              RecRef.GETTABLE(SalesHeader);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData.Description := SalesHeader."Sell-to Customer Name";
              InventoryPageData.Source := STRSUBSTNO(Text0036,FORMAT(SalesHeader."Document Type"));
              InventoryPageData."Document No." := SalesHeader."No.";
              CASE SourceSubtype OF
                SourceSubtype::"1",
                SourceSubtype::"2",
                SourceSubtype::"3":
                  BEGIN
                    // Sales Order or similar to go into Gross Requirements
                    InventoryPageData.Type := InventoryPageData.Type::Sale;
                    InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)";
                  END;
                SourceSubtype::"4":
                  BEGIN
                    // Blanket Order
                    InventoryPageData.Type := InventoryPageData.Type::"Blanket Sales Order";
                    InventoryPageData.Forecast := "Orig. Quantity (Base)";
                    InventoryPageData."Remaining Forecast" := "Remaining Quantity (Base)";
                  END;
                SourceSubtype::"5":
                  BEGIN
                    // Sales Return Order
                    InventoryPageData.Type := InventoryPageData.Type::"Sales Return";
                    InventoryPageData."Scheduled Receipt" := "Remaining Quantity (Base)";
                  END;
                ELSE
                  ERROR(Text0000,SourceType,SourceSubtype);
              END;
            END;
          DATABASE::"Purchase Line":
            BEGIN
              PurchHeader.GET(SourceSubtype,SourceID);
              RecRef.GETTABLE(PurchHeader);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData.Description := PurchHeader."Buy-from Vendor Name";
              InventoryPageData.Source := STRSUBSTNO(Text0038,FORMAT(PurchHeader."Document Type"));
              InventoryPageData."Document No." := PurchHeader."No.";
              CASE SourceSubtype OF
                SourceSubtype::"1",
                SourceSubtype::"2",
                SourceSubtype::"3":
                  BEGIN
                    // Purchase Order or similar to go into Scheduled Receipts
                    InventoryPageData.Type := InventoryPageData.Type::Purchase;
                    InventoryPageData."Scheduled Receipt" := "Remaining Quantity (Base)";
                  END;
                SourceSubtype::"5":
                  BEGIN
                    // Purchase Return Order
                    InventoryPageData.Type := InventoryPageData.Type::"Purch. Return";
                    InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)"
                  END;
                ELSE
                  ERROR(Text0000,SourceType,SourceSubtype);
              END;
            END;
          DATABASE::"Transfer Line":
            BEGIN
              TransHeader.GET(SourceID);
              RecRef.GETTABLE(TransHeader);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := TransHeader."No.";
              CASE SourceSubtype OF
                SourceSubtype::"0":
                  CASE Type OF
                    Type::Transfer:
                      BEGIN
                        // Outbound Transfer
                        InventoryPageData.Type := InventoryPageData.Type::Transfer;
                        InventoryPageData.Description := TransHeader."Transfer-to Name";
                        InventoryPageData.Source := STRSUBSTNO(Text5740,FORMAT(TransHeader."Transfer-to Code"));
                        InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)"
                      END;
                    Type::"Plan Revert":
                      BEGIN
                        InventoryPageData.Type := InventoryPageData.Type::"Plan Revert";
                        InventoryPageData.Description := TransHeader."Transfer-to Name";
                        InventoryPageData.Source := Text0246;
                        InventoryPageData."Action Message Qty." := "Remaining Quantity (Base)";
                        InventoryPageData."Action Message" := "Action Message";
                      END;
                  END;
                SourceSubtype::"1":
                  BEGIN
                    // Inbound Transfer
                    InventoryPageData.Type := InventoryPageData.Type::Transfer;
                    InventoryPageData.Description := TransHeader."Transfer-from Name";
                    InventoryPageData.Source := STRSUBSTNO(Text5740,FORMAT(TransHeader."Transfer-from Code"));
                    InventoryPageData."Scheduled Receipt" := "Remaining Quantity (Base)";
                  END;
                ELSE
                  ERROR(Text0000,SourceType,SourceSubtype);
              END;
            END;
          DATABASE::"Prod. Order Line":
            BEGIN
              ProdOrder.GET(SourceSubtype,SourceID);
              RecRef.GETTABLE(ProdOrder);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := ProdOrder."No.";
              InventoryPageData.Type := InventoryPageData.Type::Production;
              InventoryPageData.Description := ProdOrder.Description;
              InventoryPageData.Source := STRSUBSTNO(Text5405,FORMAT(ProdOrder.Status));
              InventoryPageData."Scheduled Receipt" := "Remaining Quantity (Base)";
            END;
          DATABASE::"Prod. Order Component":
            BEGIN
              ProdOrder.GET(SourceSubtype,SourceID);
              RecRef.GETTABLE(ProdOrder);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := ProdOrder."No.";
              InventoryPageData.Description := ProdOrder.Description;
              CASE Type OF
                Type::Component:
                  BEGIN
                    InventoryPageData.Type := InventoryPageData.Type::Component;
                    InventoryPageData.Source := STRSUBSTNO(Text5407,FORMAT(ProdOrder.Status));
                    InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)"
                  END;
                Type::"Plan Revert":
                  BEGIN
                    InventoryPageData.Type := InventoryPageData.Type::"Plan Revert";
                    InventoryPageData.Source := Text0246;
                    InventoryPageData."Action Message Qty." := "Remaining Quantity (Base)";
                    InventoryPageData."Action Message" := "Action Message";
                  END;
              END;
            END;
          DATABASE::"Service Line":
            BEGIN
              ServHeader.GET(SourceSubtype,SourceID);
              RecRef.GETTABLE(ServHeader);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := ServHeader."No.";
              InventoryPageData.Type := InventoryPageData.Type::Service;
              InventoryPageData.Description := ServHeader."Ship-to Name";
              InventoryPageData.Source := STRSUBSTNO(Text5900,FORMAT(ServHeader."Document Type"));
              InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)"
            END;
          DATABASE::"Job Planning Line":
            BEGIN
              Job.GET(SourceID);
              RecRef.GETTABLE(Job);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := Job."No.";
              InventoryPageData.Type := InventoryPageData.Type::Job;
              InventoryPageData.Description := Job."Bill-to Customer No.";
              InventoryPageData.Source := STRSUBSTNO(Text1003,FORMAT(Job.Status));
              InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)"
            END;
          DATABASE::"Requisition Line":
            BEGIN
              ReqLine.GET(SourceID,SourceBatchName,SourceRefNo);
              RecRef.GETTABLE(ReqLine);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := ReqLine."Ref. Order No.";
              InventoryPageData.Type := InventoryPageData.Type::Plan;
              InventoryPageData.Source := FORMAT("Ref. Order Type") + ' ' + FORMAT("Action Message");
              InventoryPageData.Description := ReqLine.Description;
              InventoryPageData."Action Message Qty." := "Remaining Quantity (Base)";
              InventoryPageData."Action Message" := "Action Message";
            END;
          DATABASE::"Planning Component":
            BEGIN
              ReqLine.GET(SourceID,SourceBatchName,SourceProdOrderLine);
              RecRef.GETTABLE(ReqLine);
              InventoryPageData."Source Document ID" := "Source Line ID";
              InventoryPageData."Document No." := ReqLine."Ref. Order No.";
              InventoryPageData.Type := InventoryPageData.Type::Plan;
              InventoryPageData.Description := ReqLine.Description;
              InventoryPageData.Source := STRSUBSTNO(Text5407,FORMAT("Action Message"));
              InventoryPageData."Action Message Qty." := "Remaining Quantity (Base)";
              InventoryPageData."Action Message" := "Action Message";
            END;
          DATABASE::"Production Forecast Entry":
            BEGIN
              ProdForecastEntry.GET(SourceRefNo);
              ProdForecastName.GET(ProdForecastEntry."Production Forecast Name");
              RecRef.GETTABLE(ProdForecastName);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := ProdForecastName.Name;
              InventoryPageData.Type := InventoryPageData.Type::Forecast;
              InventoryPageData.Description := ProdForecastName.Description;
              IF "Forecast Type" = "Forecast Type"::Sales THEN
                InventoryPageData.Source := Text9851
              ELSE
                InventoryPageData.Source := Text9852;
              InventoryPageData.Forecast := "Orig. Quantity (Base)";
              InventoryPageData."Remaining Forecast" := "Remaining Quantity (Base)";
            END;
          DATABASE::"Assembly Header":
            BEGIN
              AssemblyHeader.GET(SourceSubtype,SourceID);
              RecRef.GETTABLE(AssemblyHeader);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := AssemblyHeader."No.";
              InventoryPageData.Type := InventoryPageData.Type::"Assembly Order";
              InventoryPageData.Description := AssemblyHeader.Description;
              InventoryPageData.Source := STRSUBSTNO(Text900,FORMAT(AssemblyHeader."Document Type"));
              InventoryPageData."Scheduled Receipt" := "Remaining Quantity (Base)";
            END;
          DATABASE::"Assembly Line":
            BEGIN
              AssemblyLine.GET(SourceSubtype,SourceID,SourceRefNo);
              RecRef.GETTABLE(AssemblyLine);
              InventoryPageData."Source Document ID" := RecRef.RECORDID;
              InventoryPageData."Document No." := AssemblyLine."Document No.";
              InventoryPageData."Line No." := AssemblyLine."Line No.";
              InventoryPageData.Type := InventoryPageData.Type::"Assembly Component";
              InventoryPageData.Description := AssemblyLine.Description;
              InventoryPageData.Source := STRSUBSTNO(Text901,FORMAT(AssemblyLine."Document Type"));
              InventoryPageData."Gross Requirement" := "Remaining Quantity (Base)";
            END;
          ELSE
            ERROR(Text0000,SourceType,SourceSubtype);
        END;
      END;
    END;

    LOCAL PROCEDURE UpdatePeriodTotals@24(VAR PeriodInvtPageData@1000 : Record 5531;VAR DetailInvtPageData@1001 : Record 5531);
    BEGIN
      WITH DetailInvtPageData DO BEGIN
        PeriodInvtPageData."Remaining Quantity (Base)" += "Remaining Quantity (Base)";
        PeriodInvtPageData."Gross Requirement" += "Gross Requirement";
        PeriodInvtPageData."Scheduled Receipt" += "Scheduled Receipt";
        PeriodInvtPageData.Forecast += Forecast;
        PeriodInvtPageData."Remaining Forecast" += "Remaining Forecast";
        PeriodInvtPageData."Action Message Qty." += "Action Message Qty.";
      END;
    END;

    LOCAL PROCEDURE UpdateInventory@25(VAR InvtPageData@1001 : Record 5531;VAR InvtEventBuf@1000 : Record 5530);
    BEGIN
      WITH InvtEventBuf DO BEGIN
        IF "Action Message" <> "Action Message"::" " THEN
          InvtPageData."Suggested Projected Inventory" += "Remaining Quantity (Base)"
        ELSE
          IF Type = Type::Forecast THEN
            InvtPageData."Forecasted Projected Inventory" += "Remaining Quantity (Base)"
          ELSE
            InvtPageData."Projected Inventory" += "Remaining Quantity (Base)";
      END;
    END;

    LOCAL PROCEDURE NextPageLineNo@23() : Integer;
    BEGIN
      PageLineNo += 1;
      EXIT(PageLineNo);
    END;

    PROCEDURE ShowDocument@2(RecordID@1000 : RecordID);
    BEGIN
      CalcItemAvailability.ShowDocument(RecordID);
    END;

    LOCAL PROCEDURE FormatPeriodDescription@3(Date@1000 : Record 2000000007) PeriodDescription : Text[50];
    BEGIN
      CASE Date."Period Type" OF
        Date."Period Type"::Week,
        Date."Period Type"::Quarter,
        Date."Period Type"::Year:
          PeriodDescription := STRSUBSTNO('%1 %2',FORMAT(Date."Period Type"),Date."Period Name");
        ELSE
          PeriodDescription := Date."Period Name";
      END;
    END;

    BEGIN
    END.
  }
}

