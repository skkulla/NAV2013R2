OBJECT Codeunit 1023 Job Jnl.-B.Post
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=237;
    OnRun=BEGIN
            JobJnlBatch.COPY(Rec);
            Code;
            COPY(JobJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals?;ESM=Confirma que desea registrar los diarios?;FRC=Voulez-vous reporter les journaux?;ENC=Do you want to post the journals?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ESM=Se han registrado correctamente los diarios.;FRC=Le journal a t report avec succs.;ENC=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ESM="No ha sido posible registrar todos los diarios. ";FRC="Il a t impossible de reporter tous les journaux. ";ENC="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ESM=Los diarios que no se han registrado estn marcados.;FRC=Les journaux qui n''ont pas t reports sont maintenant indiqus.;ENC=The journals that were not successfully posted are now marked.';
      JobJnlTemplate@1004 : Record 209;
      JobJnlBatch@1005 : Record 237;
      JobJnlLine@1006 : Record 210;
      JobJnlPostbatch@1007 : Codeunit 1013;
      JnlWithErrors@1008 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH JobJnlBatch DO BEGIN
        JobJnlTemplate.GET("Journal Template Name");
        JobJnlTemplate.TESTFIELD("Force Posting Report",FALSE);

        IF NOT CONFIRM(Text000) THEN
          EXIT;

        FIND('-');
        REPEAT
          JobJnlLine."Journal Template Name" := "Journal Template Name";
          JobJnlLine."Journal Batch Name" := Name;
          JobJnlLine."Line No." := 1;
          CLEAR(JobJnlPostbatch);
          IF JobJnlPostbatch.RUN(JobJnlLine) THEN
            MARK(FALSE)
          ELSE BEGIN
            MARK(TRUE);
            JnlWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JnlWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP(0);
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

