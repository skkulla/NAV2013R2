OBJECT Codeunit 409 Dimension Value-Indent
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=349;
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text002 +
                   Text003,"Dimension Code"),TRUE)
            THEN
              EXIT;

            DimVal.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the dimension values for dimension %1. ";ESM="Esta funci¢n actualiza la indentaci¢n de todos los valores de la dimensi¢n %1. ";FRC="Cette fonction met … jour l''indentation de toutes les valeurs de dimension pour la dimension %1. ";ENC="This function updates the indentation of all the dimension values for dimension %1. "';
      Text001@1001 : TextConst 'ENU="All dimension values between a Begin-Total and the matching End-Total are indented by one level. ";ESM="Todos valores dims. entre inicio-total y fin-total y ajuste fin-total son indentados por un nivel. ";FRC="Toutes les valeurs de dimension entre un D‚but total et le Fin total correspondant sont indent‚es d''un niveau. ";ENC="All dimension values between a Begin-Total and the matching End-Total are indented by one level. "';
      Text002@1002 : TextConst 'ENU=The Totaling field for each End-Total is also updated.\\;ESM=Campo totales para cada Fin-Total est  tambi‚n actualiz.\\;FRC=La totalisation pour chaque Fin total est aussi mise … jour.\\;ENC=The Totalling field for each End-Total is also updated.\\';
      Text003@1003 : TextConst 'ENU=Do you want to indent the dimension values?;ESM=¨Confirma que desea indentar los valores dimensi¢n ?;FRC=Voulez-vous indenter les valeurs de dimension?;ENC=Do you want to indent the dimension values?';
      Text004@1004 : TextConst 'ENU=Indenting Dimension Values @1@@@@@@@@@@@@@@@@@@;ESM=Indentando valores dimensi¢n @1@@@@@@@@@@@@@@@@@@;FRC=Indentation des valeurs de dimension @1@@@@@@@@@@@@@@@@@@;ENC=Indenting Dimension Values @1@@@@@@@@@@@@@@@@@@';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;ESM=Fin-Tot %1 est  perdiendo un ajuste de inicio-tot.;FRC=Total-fin %1 n''a pas de Total-d‚but correspondant.;ENC=End-Total %1 is missing a matching Begin-Total.';
      DimVal@1006 : Record 349;
      Window@1007 : Dialog;
      DimValCode@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1();
    VAR
      NoOfDimVals@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfDimVals := DimVal.COUNT;
      IF NoOfDimVals = 0 THEN
        NoOfDimVals := 1;
      WITH DimVal DO
        IF FIND('-') THEN
          REPEAT
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress DIV NoOfDimVals);

            IF "Dimension Value Type" = "Dimension Value Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  Code);
              Totaling := DimValCode[i] + '..' + Code;
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Dimension Value Type" = "Dimension Value Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              DimValCode[i] := Code;
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

