OBJECT Report 5854 Roll Up Standard Cost
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Roll Up Standard Cost;
               ESM=Distribuir costo est ndar;
               FRC=Cumuler le co–t standard;
               ENC=Roll Up Standard Cost];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  StdCostWkshName@1000 : Record 5840;
                BEGIN
                  RolledUp := FALSE;

                  IF ToStdCostWkshName = '' THEN
                    ERROR(STRSUBSTNO(Text003));
                  StdCostWkshName.GET(ToStdCostWkshName);
                END;

  }
  DATASET
  {
    { 8129;    ;DataItem;                    ;
               DataItemTable=Table27;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               StdCostWksh.LOCKTABLE;
                               CLEAR(CalcStdCost);
                               CalcStdCost.SetProperties(CalculationDate,TRUE,FALSE,FALSE,ToStdCostWkshName,TRUE);
                               CalcStdCost.CalcItems(Item,TempItem);

                               TempItem.SETFILTER("Replenishment System",'%1|%2',
                                 TempItem."Replenishment System"::"Prod. Order",
                                 TempItem."Replenishment System"::Assembly);
                               IF TempItem.FIND('-') THEN
                                 REPEAT
                                   UpdateStdCostWksh;
                                   RolledUp := TRUE;
                                 UNTIL TempItem.NEXT = 0;
                             END;

               OnPostDataItem=BEGIN
                                IF NOT NoMessage THEN
                                  IF RolledUp THEN
                                    MESSAGE(Text000)
                                  ELSE
                                    MESSAGE(Text001);
                              END;

               ReqFilterFields=No.,Costing Method }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF CalculationDate = 0D THEN
                     CalculationDate := WORKDATE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 15  ;2   ;Field     ;
                  Name=CalculationDate;
                  CaptionML=[ENU=Calculation Date;
                             ESM=Fecha c lculo;
                             FRC=Date du calcul;
                             ENC=Calculation Date];
                  SourceExpr=CalculationDate;
                  OnValidate=BEGIN
                               IF CalculationDate = 0D THEN
                                 ERROR(Text002);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempItem@1003 : TEMPORARY Record 27;
      StdCostWksh@1004 : Record 5841;
      CalcStdCost@1000 : Codeunit 5812;
      CalculationDate@1001 : Date;
      ToStdCostWkshName@1002 : Code[10];
      RolledUp@1006 : Boolean;
      Text000@1007 : TextConst 'ENU=The standard costs have been rolled up successfully.;ESM=Los costos estandar han sido distribuidos satisfactoriamente.;FRC=Les co–ts standards ont ‚t‚ cumul‚s avec succŠs.;ENC=The standard costs have been rolled up successfully.';
      Text001@1008 : TextConst 'ENU=There is nothing to roll up.;ESM=No hay nada que distribuir.;FRC=Il n''y a rien … cumuler.;ENC=There is nothing to roll up.';
      Text002@1009 : TextConst 'ENU=You must enter a calculation date.;ESM=Debe introducir una fecha de c lculo.;FRC=Vous devez entrer une date de calcul.;ENC=You must enter a calculation date.';
      Text003@1010 : TextConst 'ENU=You must specify a worksheet name to roll up to.;ESM=Debe especif. un nombre hoja trab. para distrib.;FRC=Vous devez sp‚cifier le nom de la feuille de travail vers laquelle le cumul sera ex‚cut‚.;ENC=You must specify a worksheet name to roll up to.';
      NoMessage@1011 : Boolean;

    PROCEDURE UpdateStdCostWksh@3();
    VAR
      Found@1000 : Boolean;
    BEGIN
      WITH StdCostWksh DO BEGIN
        Found := GET(ToStdCostWkshName,Type::Item,TempItem."No.");
        VALIDATE("Standard Cost Worksheet Name",ToStdCostWkshName);
        VALIDATE(Type,Type::Item);
        VALIDATE("No.",TempItem."No.");
        "New Standard Cost" := TempItem."Standard Cost";

        "New Single-Lvl Material Cost" := TempItem."Single-Level Material Cost";
        "New Single-Lvl Cap. Cost" := TempItem."Single-Level Capacity Cost";
        "New Single-Lvl Subcontrd Cost" := TempItem."Single-Level Subcontrd. Cost";
        "New Single-Lvl Cap. Ovhd Cost" := TempItem."Single-Level Cap. Ovhd Cost";
        "New Single-Lvl Mfg. Ovhd Cost" := TempItem."Single-Level Mfg. Ovhd Cost";

        "New Rolled-up Material Cost" := TempItem."Rolled-up Material Cost";
        "New Rolled-up Cap. Cost" := TempItem."Rolled-up Capacity Cost";
        "New Rolled-up Subcontrd Cost" := TempItem."Rolled-up Subcontracted Cost";
        "New Rolled-up Cap. Ovhd Cost" := TempItem."Rolled-up Cap. Overhead Cost";
        "New Rolled-up Mfg. Ovhd Cost" := TempItem."Rolled-up Mfg. Ovhd Cost";

        IF Found THEN
          MODIFY(TRUE)
        ELSE
          INSERT(TRUE);
      END;
    END;

    PROCEDURE SetStdCostWksh@1(NewStdCostWkshName@1000 : Code[10]);
    BEGIN
      ToStdCostWkshName := NewStdCostWkshName;
    END;

    PROCEDURE Initialize@2(StdCostWkshName2@1000 : Code[10];NoMessage2@1001 : Boolean);
    BEGIN
      ToStdCostWkshName := StdCostWkshName2;
      NoMessage := NoMessage2;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

