OBJECT Report 799 Delete Empty Item Registers
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 46=rimd;
    CaptionML=[ENU=Delete Empty Item Registers;
               ESM=Eliminar regs. movs. producto;
               FRC=Supprimer registres article vides;
               ENC=Delete Empty Item Registers];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4073;    ;DataItem;                    ;
               DataItemTable=Table46;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               Window.OPEN(
                                 Text001 +
                                 Text002 +
                                 Text003 +
                                 Text004);
                             END;

               OnAfterGetRecord=BEGIN
                                  ItemLedgEntry.SETRANGE("Entry No.","From Entry No.","To Entry No.");
                                  PhysInvtLedgEntry.SETRANGE("Entry No.","From Phys. Inventory Entry No.","To Phys. Inventory Entry No.");
                                  CapLedgEntry.SETRANGE("Entry No.","From Capacity Entry No.","To Capacity Entry No.");
                                  IF ItemLedgEntry.FINDFIRST OR
                                     PhysInvtLedgEntry.FINDFIRST OR
                                     CapLedgEntry.FINDFIRST
                                  THEN
                                    CurrReport.SKIP;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,"Creation Date");
                                  DELETE;
                                  NoOfDeleted := NoOfDeleted + 1;
                                  Window.UPDATE(3,NoOfDeleted);
                                  IF NoOfDeleted >= NoOfDeleted2 + 10 THEN BEGIN
                                    NoOfDeleted2 := NoOfDeleted;
                                    COMMIT;
                                  END;
                                END;

               ReqFilterFields=Creation Date }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to delete the registers?;ESM=¨Confirma que desea eliminar los registros?;FRC=Voulez-vous supprimer les registres?;ENC=Do you want to delete the registers?';
      Text001@1001 : TextConst 'ENU=Deleting item registers...\\;ESM=Borrando registros movs. productos...\\;FRC=Suppression registres article...\\;ENC=Deleting item registers...\\';
      Text002@1002 : TextConst 'ENU=No.                      #1######\;ESM=N§                       #1######\;FRC=Nø                       #1######\;ENC=No.                      #1######\';
      Text003@1003 : TextConst 'ENU=Posted on                #2######\\;ESM=Registrado en            #2######\\;FRC=Report‚ le               #2######\\;ENC=Posted on                #2######\\';
      Text004@1004 : TextConst 'ENU=No. of registers deleted #3######;ESM=N§ registros borrados    #3######;FRC=Nbre registres supprim‚s #3######;ENC=No. of registers deleted #3######';
      ItemLedgEntry@1005 : Record 32;
      PhysInvtLedgEntry@1006 : Record 281;
      CapLedgEntry@1010 : Record 5832;
      Window@1007 : Dialog;
      NoOfDeleted@1008 : Integer;
      NoOfDeleted2@1009 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

