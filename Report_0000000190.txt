OBJECT Report 190 Issue Reminders
{
  OBJECT-PROPERTIES
  {
    Date=12/02/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10.00.35800;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Issue Reminders;
               ESM=Emitir recordatorios;
               FRC=êmettre rappels;
               ENC=Issue Reminders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4775;    ;DataItem;                    ;
               DataItemTable=Table295;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=[ENU=Reminder;
                                   ESM=Recordatorio;
                                   FRC=Rappel;
                                   ENC=Reminder];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldTime := TIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(ReminderIssue);
                                  ReminderIssue.Set("Reminder Header",ReplacePostingDate,PostingDateReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    ReminderIssue.RUN;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewTime := TIME;
                                    IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldTime := TIME;
                                    END;
                                    COMMIT;
                                    MARK := NOT ReminderIssue.RUN;
                                  END;

                                  IF PrintDoc THEN BEGIN
                                    ReminderIssue.GetIssuedReminder(IssuedReminderHeader);
                                    TempIssuedReminderHeader := IssuedReminderHeader;
                                    TempIssuedReminderHeader.INSERT;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                COMMIT;
                                IF PrintDoc THEN
                                  IF TempIssuedReminderHeader.FINDSET THEN
                                    REPEAT
                                      TempIssuedReminderHeader.PrintRecords(FALSE);
                                    UNTIL TempIssuedReminderHeader.NEXT = 0;
                                MARKEDONLY := TRUE;
                                IF FIND('-') THEN
                                  IF CONFIRM(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Reminder Header");
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Print;
                             ESM=Imprimir;
                             FRC=Imprimer;
                             ENC=Print];
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             ESM=Reemplazar fecha reg.;
                             FRC=Remplacer la date de report;
                             ENC=Replace Posting Date];
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  SourceExpr=PostingDateReq }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESM=Introduzca la fecha registro.;FRC=Entrez la date de report.;ENC=Please enter the posting date.';
      Text001@1001 : TextConst 'ENU=Issuing reminder...;ESM=Emitiendo recordatorio...;FRC=êmettre rappel...;ENC=Issuing reminder...';
      Text002@1002 : TextConst 'ENU=Issuing reminders @1@@@@@@@@@@@@@;ESM=Emitiendo recordatorio @1@@@@@@@@@@@@@;FRC=êmettre rappels   @1@@@@@@@@@@@@@;ENC=Issuing reminders @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected reminders.\Do you want to see these reminders?;ESM=No se han podido emitir algunos de los recordatorios seleccionados.\®Desea ver estos recordatorios?;FRC=Il n''a pas ÇtÇ possible d''Çmettre certains rappels sÇlectionnÇs.\Voulez-vous visualiser ces rappelsˇ?;ENC=It was not possible to issue some of the selected reminders.\Do you want to see these reminders?';
      IssuedReminderHeader@1005 : Record 297;
      TempIssuedReminderHeader@1017 : TEMPORARY Record 297;
      ReminderIssue@1006 : Codeunit 393;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

