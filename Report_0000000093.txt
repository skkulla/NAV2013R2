OBJECT Report 93 Create Fiscal Year
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Fiscal Year;
               ESM=Crear ejercicio;
               FRC=Cr‚er p‚riode comptable;
               ENC=Create Fiscal Year];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  AccountingPeriod."Starting Date" := FiscalYearStartDate;
                  AccountingPeriod.TESTFIELD("Starting Date");

                  IF AccountingPeriod.FIND('-') THEN BEGIN
                    FirstPeriodStartDate := AccountingPeriod."Starting Date";
                    FirstPeriodLocked := AccountingPeriod."Date Locked";
                    IF (FiscalYearStartDate < FirstPeriodStartDate) AND FirstPeriodLocked THEN
                      IF NOT
                         CONFIRM(
                           Text000 +
                           Text001)
                      THEN
                        EXIT;
                    IF AccountingPeriod.FIND('+') THEN
                      LastPeriodStartDate := AccountingPeriod."Starting Date";
                  END ELSE
                    IF NOT
                       CONFIRM(
                         Text002 +
                         Text003)
                    THEN
                      EXIT;

                  FiscalYearStartDate2 := FiscalYearStartDate;

                  FOR i := 1 TO NoOfPeriods + 1 DO BEGIN
                    IF (FiscalYearStartDate <= FirstPeriodStartDate) AND (i = NoOfPeriods + 1) THEN
                      EXIT;

                    IF FirstPeriodStartDate <> 0D THEN
                      IF (FiscalYearStartDate >= FirstPeriodStartDate) AND (FiscalYearStartDate < LastPeriodStartDate) THEN
                        ERROR(Text004);
                    AccountingPeriod.INIT;
                    AccountingPeriod."Starting Date" := FiscalYearStartDate;
                    AccountingPeriod.VALIDATE("Starting Date");
                    IF (i = 1) OR (i = NoOfPeriods + 1) THEN BEGIN
                      AccountingPeriod."New Fiscal Year" := TRUE;
                      InvtSetup.GET;
                      AccountingPeriod."Average Cost Calc. Type" := InvtSetup."Average Cost Calc. Type";
                      AccountingPeriod."Average Cost Period" := InvtSetup."Average Cost Period";
                    END;
                    IF (FirstPeriodStartDate = 0D) AND (i = 1) THEN
                      AccountingPeriod."Date Locked" := TRUE;
                    IF (AccountingPeriod."Starting Date" < FirstPeriodStartDate) AND FirstPeriodLocked THEN BEGIN
                      AccountingPeriod.Closed := TRUE;
                      AccountingPeriod."Date Locked" := TRUE;
                    END;
                    IF NOT AccountingPeriod.FIND('=') THEN
                      AccountingPeriod.INSERT;
                    FiscalYearStartDate := CALCDATE(PeriodLength,FiscalYearStartDate);
                  END;

                  AccountingPeriod.GET(FiscalYearStartDate2);
                  AccountingPeriod.UpdateAvgItems(0);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF NoOfPeriods = 0 THEN BEGIN
                     NoOfPeriods := 12;
                     EVALUATE(PeriodLength,'<1M>');
                   END;
                   IF AccountingPeriod.FIND('+') THEN
                     FiscalYearStartDate := AccountingPeriod."Starting Date";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  SourceExpr=FiscalYearStartDate }

      { 2   ;2   ;Field     ;
                  Name=NoOfPeriods;
                  CaptionML=[ENU=No. of Periods;
                             ESM=N§ de periodos;
                             FRC=Nombre de p‚riodes;
                             ENC=No. of Periods];
                  SourceExpr=NoOfPeriods }

      { 3   ;2   ;Field     ;
                  Name=PeriodLength;
                  CaptionML=[ENU=Period Length;
                             ESM=Longitud periodo;
                             FRC=Dur‚e de la p‚riode;
                             ENC=Period Length];
                  SourceExpr=PeriodLength }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The new fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\;ESM=El nuevo ejercicio comienza antes de un ejercicio ya existente, por tanto el nuevo ejercicio se cerrar  autom ticamente.\\;FRC=Puisque le nouvel exercice financier commence avant un exercice financier existant, ce premier est ferm‚ automatiquement.\\;ENC=The new fiscal year begins before an existing fiscal year, so the new year will be closed automatically.\\';
      Text001@1001 : TextConst 'ENU=Do you want to create and close the fiscal year?;ESM=¨Confirma que desea crear y cerrar el ejercicio?;FRC=Voulez-vous cr‚er et fermer l''exercice financier?;ENC=Do you want to create and close the fiscal year?';
      Text002@1002 : TextConst 'ENU=Once you create the new fiscal year you cannot change its starting date.\\;ESM=Una vez creado un ejercicio no se puede cambiar su fecha inicial.\\;FRC=Une fois que vous avez cr‚‚ le nouvel exercice financier vous ne pouvez changer sa date de d‚but.\\;ENC=Once you create the new fiscal year you cannot change its starting date.\\';
      Text003@1003 : TextConst 'ENU=Do you want to create the fiscal year?;ESM=¨Confirma que desea crear el ejercicio?;FRC=Voulez-vous cr‚er l''exercice financier?;ENC=Do you want to create the fiscal year?';
      Text004@1004 : TextConst 'ENU=It is only possible to create new fiscal years before or after the existing ones.;ESM=S¢lo se pueden crear ejercicios antes o despu‚s de los ejercicios existentes.;FRC=Vous pouvez cr‚er de nouveaux exercices financiers qu''avant ou aprŠs ceux qui existent d‚j….;ENC=It is only possible to create new fiscal years before or after the existing ones.';
      AccountingPeriod@1005 : Record 50;
      InvtSetup@1013 : Record 313;
      NoOfPeriods@1006 : Integer;
      PeriodLength@1007 : DateFormula;
      FiscalYearStartDate@1008 : Date;
      FiscalYearStartDate2@1015 : Date;
      FirstPeriodStartDate@1009 : Date;
      LastPeriodStartDate@1010 : Date;
      FirstPeriodLocked@1011 : Boolean;
      i@1012 : Integer;

    PROCEDURE InitializeRequest@1(NewNoOfPeriods@1000 : Integer;NewPeriodLength@1001 : DateFormula;StartingDate@1002 : Date);
    BEGIN
      NoOfPeriods := NewNoOfPeriods;
      PeriodLength := NewPeriodLength;
      IF AccountingPeriod.FINDLAST THEN
        FiscalYearStartDate := AccountingPeriod."Starting Date"
      ELSE
        FiscalYearStartDate := StartingDate;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

