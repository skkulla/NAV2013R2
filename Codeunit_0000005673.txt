OBJECT Codeunit 5673 Insurance Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=5634;
    OnRun=BEGIN
            InsuranceJnlBatch.COPY(Rec);
            Code;
            COPY(InsuranceJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to post the journals and print the posting report?;ESM=Confirma que desea registrar los diarios e imprimir el informe de registro?;FRC=Dsirez-vous reporter le journal et imprimer le rapport?;ENC=Do you want to post the journals and print the posting report?';
      Text001@1001 : TextConst 'ENU=The journals were successfully posted.;ESM=Se han registrado correctamente los diarios.;FRC=Le journal a t report avec succs.;ENC=The journals were successfully posted.';
      Text002@1002 : TextConst 'ENU="It was not possible to post all of the journals. ";ESM="No ha sido posible registrar todos los diarios. ";FRC="Il a t impossible de reporter tous les journaux. ";ENC="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'ENU=The journals that were not successfully posted are now marked.;ESM=Los diarios que no se han registrado estn marcados.;FRC=Les journaux qui n''ont pas t reports sont maintenant indiqus.;ENC=The journals that were not successfully posted are now marked.';
      InsuranceJnlTempl@1004 : Record 5633;
      InsuranceJnlBatch@1005 : Record 5634;
      InsuranceJnlLine@1006 : Record 5635;
      InsuranceReg@1007 : Record 5636;
      InsuranceJnlPostBatch@1008 : Codeunit 5653;
      JournalWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH InsuranceJnlBatch DO BEGIN
        InsuranceJnlTempl.GET("Journal Template Name");
        InsuranceJnlTempl.TESTFIELD("Posting Report ID");

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          InsuranceJnlLine."Journal Template Name" := "Journal Template Name";
          InsuranceJnlLine."Journal Batch Name" := Name;
          InsuranceJnlLine."Line No." := 1;

          CLEAR(InsuranceJnlPostBatch);
          IF InsuranceJnlPostBatch.RUN(InsuranceJnlLine) THEN BEGIN
            IF InsuranceReg.GET(InsuranceJnlLine."Line No.") THEN BEGIN
              InsuranceReg.SETRECFILTER;
              REPORT.RUN(InsuranceJnlTempl."Posting Report ID",FALSE,FALSE,InsuranceReg);
            END;
            MARK(FALSE);
          END
          ELSE BEGIN
            MARK(TRUE);
            JournalWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JournalWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

