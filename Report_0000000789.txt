OBJECT Report 789 Delete Phys. Inventory Ledger
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 87=rimd,
                TableData 281=rimd,
                TableData 5815=rimd;
    CaptionML=[ENU=Delete Phys. Inventory Ledger;
               ESM=Eliminar movs. inv. f¡sico;
               FRC=Supprimer le grand livre d'inventaire physique;
               ENC=Delete Phys. Inventory Ledger];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3360;    ;DataItem;                    ;
               DataItemTable=Table281;
               OnPreDataItem=BEGIN
                               IF NOT CONFIRM(Text000,FALSE) THEN
                                 CurrReport.BREAK;

                               IF EntrdDateComprReg."Ending Date" = 0D THEN
                                 ERROR(STRSUBSTNO(Text003,EntrdDateComprReg.FIELDCAPTION("Ending Date")));

                               Window.OPEN(
                                 Text004 +
                                 Text005 +
                                 Text006 +
                                 Text007,
                                 "Item No.",
                                 "Posting Date",
                                 NoOfDeleted);

                               IF PhysInvtLedgEntry2.FINDLAST THEN;
                               LastEntryNo := PhysInvtLedgEntry2."Entry No.";
                               SETRANGE("Entry No.",0,LastEntryNo);
                               SETRANGE("Posting Date",EntrdDateComprReg."Starting Date",EntrdDateComprReg."Ending Date");
                             END;

               OnAfterGetRecord=VAR
                                  InvtPeriodEntry@1000 : Record 5815;
                                  InvtPeriod@1001 : Record 5814;
                                BEGIN
                                  IF NOT InvtPeriod.IsValidDate("Posting Date") THEN
                                    InvtPeriod.ShowError("Posting Date");

                                  PhysInvtLedgEntry2 := "Phys. Inventory Ledger Entry";
                                  WITH PhysInvtLedgEntry2 DO BEGIN
                                    SETCURRENTKEY("Item No.","Variant Code","Location Code","Posting Date");
                                    COPYFILTERS("Phys. Inventory Ledger Entry");
                                    SETRANGE("Item No.","Item No.");
                                    SETFILTER("Posting Date",DateComprMgt.GetDateFilter("Posting Date",EntrdDateComprReg,TRUE));
                                    SETRANGE("Inventory Posting Group","Inventory Posting Group");
                                    SETRANGE("Entry Type","Entry Type");

                                    Window.UPDATE(1);
                                    Window.UPDATE(2);

                                    DELETE;

                                    InvtPeriodEntry.RemoveItemRegNo("Entry No.",TRUE);

                                    NoOfDeleted := NoOfDeleted + 1;
                                    Window.UPDATE(3);
                                  END;

                                  IF NoOfDeleted >= LastNoOfDeleted + 10 THEN BEGIN
                                    LastNoOfDeleted := NoOfDeleted;
                                    COMMIT;
                                  END;
                                END;

               ReqFilterFields=Item No.,Inventory Posting Group }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF EntrdDateComprReg."Ending Date" = 0D THEN
                     EntrdDateComprReg."Ending Date" := TODAY;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=StartingDate;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  SourceExpr=EntrdDateComprReg."Starting Date" }

      { 2   ;2   ;Field     ;
                  Name=EndingDate;
                  CaptionML=[ENU=Ending Date;
                             ESM=Fecha final;
                             FRC=Date fin;
                             ENC=Ending Date];
                  SourceExpr=EntrdDateComprReg."Ending Date" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to delete the entries?;ESM=Este trabajo por lotes elimina movimientos. Es muy importante realizar una copia de seguridad de la base de datos antes de ejecutar el trabajo por lotes.\\¨Desea eliminar los movimientos?;FRC=Ce traitement en lot supprime des ‚critures. Par cons‚quent, il est important d''effectuer une sauvegarde de la base de donn‚es avant d''ex‚cuter le traitement en lot.\\Voulez-vous supprimer les ‚crituresÿ?;ENC=This batch job deletes entries. Therefore, it is important that you make a backup of the database before you run the batch job.\\Do you want to delete the entries?';
      Text003@1003 : TextConst 'ENU=%1 must be specified.;ESM=Se debe indicar %1.;FRC=%1 doit ˆtre sp‚cifi‚.;ENC=%1 must be specified.';
      Text004@1004 : TextConst 'ENU=Deleting phys. inventory ledger entries...\\;ESM=Borrando movs. inventario f¡sico...\\;FRC=Suppression des ‚critures d‚compte physique inventaire...\\;ENC=Deleting phys. inventory ledger entries...\\';
      Text005@1005 : TextConst 'ENU=Item No.             #1##########\;ESM=N§ producto          #1##########\;FRC=Nø d''article                  #1##########\;ENC=Item No.             #1##########\';
      Text006@1006 : TextConst 'ENU=Date                 #2######\\;ESM=Fecha                #2######\\;FRC=Date                          #2######\\;ENC=Date                 #2######\\';
      Text007@1007 : TextConst 'ENU=No. of entries del.  #3######;ESM=N§ movs. borrados    #3######;FRC=Nbre d''‚critures supprim‚es   #3######;ENC=No. of entries del.  #3######';
      EntrdDateComprReg@1008 : Record 87;
      PhysInvtLedgEntry2@1009 : Record 281;
      DateComprMgt@1010 : Codeunit 356;
      Window@1011 : Dialog;
      LastEntryNo@1012 : Integer;
      NoOfDeleted@1013 : Integer;
      LastNoOfDeleted@1014 : Integer;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

