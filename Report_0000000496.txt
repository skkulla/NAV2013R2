OBJECT Report 496 Batch Post Purchase Orders
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Purchase Orders;
               ESM=Reg. lotes pedidos compra;
               FRC=Ex‚cuter en lot bons de commande;
               ENC=Batch Post Purchase Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 4458;    ;DataItem;                    ;
               DataItemTable=Table38;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               ReqFilterHeadingML=[ENU=Purchase Order;
                                   ESM=Pedido compra;
                                   FRC=Bon de commande;
                                   ENC=Purchase Order];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);

                               Approvaltemplate.SETCURRENTKEY("Table ID","Document Type",Enabled);
                               Approvaltemplate.SETRANGE("Table ID",DATABASE::"Purchase Header");
                               Approvaltemplate.SETRANGE("Document Type","Document Type"::Order);
                               Approvaltemplate.SETRANGE(Enabled,TRUE);
                               IF Approvaltemplate.FINDFIRST THEN
                                 SETRANGE(Status,Status::Released);

                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  Receive := ReceiveReq;
                                  Invoice := InvReq;
                                  CLEAR(PurchPost);
                                  PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  IF IsApprovedForPostingBatch THEN
                                    IF PurchPost.RUN("Purchase Header") THEN BEGIN
                                      CounterOK := CounterOK + 1;
                                      IF MARKEDONLY THEN
                                        MARK(FALSE);
                                    END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitValue;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=Receive;
                  CaptionML=[ENU=Receive;
                             ESM=Recepci¢n;
                             FRC=Recevoir;
                             ENC=Receive];
                  SourceExpr=ReceiveReq }

      { 2   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[ENU=Invoice;
                             ESM=Factura;
                             FRC=Facture;
                             ENC=Invoice];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=ReplacePostingDate;
                  CaptionML=[ENU=Replace Posting Date;
                             ESM=Reemplazar fecha reg.;
                             FRC=Remplacer la date de report;
                             ENC=Replace Posting Date];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ReplaceDocumentDate;
                  CaptionML=[ENU=Replace Document Date;
                             ESM=Reemplazar fecha documento;
                             FRC=Remplacer date document;
                             ENC=Replace Document Date];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  Name=CalcInvDiscount;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             ESM=Calc. dto. factura;
                             FRC=Calculer escompte facture;
                             ENC=Calc. Inv. Discount];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESM=Introduzca la fecha registro.;FRC=Entrez la date de report.;ENC=Please enter the posting date.';
      Text001@1001 : TextConst 'ENU=Posting orders  #1########## @2@@@@@@@@@@@@@;ESM=Reg. pedidos    #1########## @2@@@@@@@@@@@@@;FRC=Report des commandes  #1########## @2@@@@@@@@@@@@@;ENC=Posting orders  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 orders out of a total of %2 have now been posted.;ESM=Se han registrado %1 pedidos de un total de %2.;FRC=%1 commandes sur un total de %2 ont maintenant ‚t‚ report‚es.;ENC=%1 orders out of a total of %2 have now been posted.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      Approvaltemplate@1018 : Record 464;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      ReceiveReq@1008 : Boolean;
      InvReq@1009 : Boolean;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.;ESM=El tipo de cambio asociado con la nueva fecha de registro indicada en el encabezado de compra no se aplicar  a las l¡neas de compra.;FRC=Le taux de change associ‚ … la nouvelle date de report sur l''en-tˆte d''achat ne s''appliquera pas aux lignes d''achat.;ENC=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.';

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FINDFIRST THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitializeRequest@2(NewReceiveReq@1006 : Boolean;NewInvReq@1003 : Boolean;NewPostingDateReq@1001 : Date;NewReplacePostingDate@1002 : Boolean;NewReplaceDocumentDate@1004 : Boolean;NewCalcInvDisc@1005 : Boolean);
    BEGIN
      InitValue;
      ReceiveReq := NewReceiveReq;
      InvReq := NewInvReq;
      PostingDateReq := NewPostingDateReq;
      ReplacePostingDate := NewReplacePostingDate;
      ReplaceDocumentDate := NewReplaceDocumentDate;
      IF NewCalcInvDisc THEN
        PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
      CalcInvDisc := NewCalcInvDisc;
    END;

    LOCAL PROCEDURE InitValue@3();
    BEGIN
      PurchSetup.GET;
      CalcInvDisc := PurchSetup."Calc. Inv. Discount";
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

