OBJECT Codeunit 1247 Process Gen. Journal  Lines
{
  OBJECT-PROPERTIES
  {
    Date=12/02/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10.00.35800;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=VAR
            PostingExch@1001 : Record 1220;
            ProcessPostingExch@1002 : Codeunit 1201;
            RecRef@1000 : RecordRef;
          BEGIN
            PostingExch.GET("Posting Exch. Entry No.");
            RecRef.GETTABLE(Rec);
            ProcessPostingExch.ProcessColumnMapping(PostingExch,RecRef);
          END;

  }
  CODE
  {
    VAR
      ProgressWindowMsg@1000 : TextConst 'ENU=Wait while the operation is being completed.;ESM=Espere mientras se completa la operaci¢n.;FRC=Patientez pendant l''ex‚cution de l''op‚ration.;ENC=Wait while the operation is being completed.';

    PROCEDURE ImportBankStatement@700(GenJnlLine@1004 : Record 81);
    VAR
      GenJnlBatch@1005 : Record 232;
      BankAcc@1000 : Record 270;
      BankExportImportSetup@1009 : Record 1200;
      PostExchDef@1001 : Record 1222;
      PostExchMapping@1002 : Record 1224;
      PostExchLineDef@1007 : Record 1227;
      PostExch@1003 : Record 1220;
      GenJnlLineTemplate@1006 : Record 81;
      ProgressWindow@1008 : Dialog;
    BEGIN
      GenJnlBatch.GET(GenJnlLine."Journal Template Name",GenJnlLine."Journal Batch Name");

      CASE GenJnlBatch."Bal. Account Type" OF
        GenJnlBatch."Bal. Account Type"::"Bank Account":
          BEGIN
            GenJnlBatch.TESTFIELD("Bal. Account No.");
            BankAcc.GET(GenJnlBatch."Bal. Account No.");
            BankAcc.GetPostExchDef(PostExchDef);
          END;
        GenJnlBatch."Bal. Account Type"::"G/L Account":
          BEGIN
            GenJnlBatch.TESTFIELD("Bank Statement Import Format");
            BankExportImportSetup.GET(GenJnlBatch."Bank Statement Import Format");
            BankExportImportSetup.TESTFIELD("Posting Exch. Def. Code");
            PostExchDef.GET(BankExportImportSetup."Posting Exch. Def. Code");
            PostExchDef.TESTFIELD(Type,PostExchDef.Type::"Bank Statement Import");
          END;
        ELSE
          GenJnlBatch.FIELDERROR("Bal. Account Type");
      END;
      CreateGeneralJournalLineTemplate(GenJnlLineTemplate,GenJnlLine);

      IF NOT PostExch.ImportToPostExch(PostExchDef)THEN
        EXIT;

      GenJnlLineTemplate."Posting Exch. Entry No." := PostExch."Entry No.";
      ProgressWindow.OPEN(ProgressWindowMsg);
      PostExchLineDef.SETRANGE("Posting Exch. Def Code",PostExchDef.Code);
      PostExchLineDef.FINDFIRST;
      PostExchMapping.GET(PostExchDef.Code,PostExchLineDef.Code,DATABASE::"Gen. Journal Line");
      CODEUNIT.RUN(PostExchMapping."Mapping Codeunit",GenJnlLineTemplate);

      UpdateGenJournalLines(GenJnlLineTemplate);
      ProgressWindow.CLOSE;
    END;

    LOCAL PROCEDURE CreateGeneralJournalLineTemplate@701(VAR GenJournalLineTemplate@1003 : Record 81;GenJournalLine@1002 : Record 81);
    BEGIN
      WITH GenJournalLineTemplate DO BEGIN
        "Journal Template Name" := GenJournalLine."Journal Template Name";
        "Journal Batch Name" := GenJournalLine."Journal Batch Name";
        SetUpNewLine(GenJournalLine,GenJournalLine."Balance (LCY)",TRUE);
        "Account Type" := "Account Type"::"G/L Account";

        GenJournalLine.SETRANGE("Journal Template Name",GenJournalLine."Journal Template Name");
        GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLine."Journal Batch Name");
        IF GenJournalLine.FINDLAST THEN BEGIN
          "Line No." := GenJournalLine."Line No.";
          "Document No." := INCSTR(GenJournalLine."Document No.");
        END ELSE
          "Document No." := GenJournalLine."Document No.";
      END;
    END;

    LOCAL PROCEDURE UpdateGenJournalLines@702(VAR GenJournalLineTemplate@1000 : Record 81);
    VAR
      GenJournalLine@1002 : Record 81;
      DocNo@1001 : Code[20];
    BEGIN
      GenJournalLine.SETRANGE("Journal Template Name",GenJournalLineTemplate."Journal Template Name");
      GenJournalLine.SETRANGE("Journal Batch Name",GenJournalLineTemplate."Journal Batch Name");
      GenJournalLine.SETFILTER("Line No.",'>%1',GenJournalLineTemplate."Line No.");
      IF GenJournalLine.FINDSET THEN BEGIN
        DocNo := GenJournalLineTemplate."Document No.";
        REPEAT
          GenJournalLine.VALIDATE("Document No.",DocNo);
          GenJournalLine.MODIFY(TRUE);
          DocNo := INCSTR(DocNo);
        UNTIL GenJournalLine.NEXT = 0;
      END;
    END;

    BEGIN
    END.
  }
}

