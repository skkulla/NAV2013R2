OBJECT Codeunit 10101 Post- Print Sales Tax Jnl
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVNA7.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      GenJnlTemplate@1030000 : Record 80;
      GenJnlLine@1030001 : Record 81;
      GLReg@1030002 : Record 45;
      CustLedgEntry@1030003 : Record 21;
      VendLedgEntry@1030004 : Record 25;
      GenJnlPostBatch@1030005 : Codeunit 13;
      ManageSalesTaxJournal@1030006 : Codeunit 10102;
      TempJnlBatchName@1030007 : Code[10];
      Text000@1030008 : TextConst '@@@=Posting Date cannot be filtered when posting recurring journals.;ENU=cannot be filtered when posting recurring journals.;ESM=no puede contener un filtro cuando se registra un diario peri¢dico.;FRC=ne peut pas ˆtre filtr‚ lors du report des journaux r‚currents.;ENC=cannot be filtered when posting recurring journals.';
      Text001@1030009 : TextConst 'ENU=Do you want to post the journal lines and print the report(s)?;ESM=¨Confirma que desea registrar las l¡ns. del diario e imprimir el o los informes?;FRC=Voulez-vous reporter les lignes du journal et imprimer les rapports?;ENC=Do you want to post the journal lines and print the report(s)?';
      Text002@1030010 : TextConst 'ENU=There is nothing to post.;ESM=No hay nada que registrar.;FRC=Il n''y a rien … reporter.;ENC=There is nothing to post.';
      Text003@1030011 : TextConst 'ENU=The journal lines were successfully posted.;ESM=Se han registrado correctamente las l¡neas del diario.;FRC=Les lignes de journal ont ‚t‚ report‚es avec succŠs.;ENC=The journal lines were successfully posted.';
      Text004@1030012 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;ESM=Se registraron correctamente las l¡neas diario. Se encuentra en el diario %1.;FRC=Les lignes du journal ont ‚t‚ report‚es correctement. Vous ˆtes maintenant dans le journal %1.;ENC=The journal lines were successfully posted. You are now in the %1 journal.';

    PROCEDURE Code@1030000();
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        IF GenJnlTemplate."Force Posting Report" OR
           (GenJnlTemplate."Cust. Receipt Report ID" = 0) AND (GenJnlTemplate."Vendor Receipt Report ID" = 0)
        THEN
          GenJnlTemplate.TESTFIELD("Posting Report ID");
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        ManageSalesTaxJournal.PostToVAT(GenJnlLine);
        ManageSalesTaxJournal.CreateGenJnlLines(GenJnlLine);
        GenJnlPostBatch.RUN(GenJnlLine);

        IF GLReg.GET("Line No.") THEN BEGIN
          IF GenJnlTemplate."Cust. Receipt Report ID" <> 0 THEN BEGIN
            CustLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Cust. Receipt Report ID",FALSE,FALSE,CustLedgEntry);
          END;
          IF GenJnlTemplate."Vendor Receipt Report ID" <> 0 THEN BEGIN
            VendLedgEntry.SETRANGE("Entry No.",GLReg."From Entry No.",GLReg."To Entry No.");
            REPORT.RUN(GenJnlTemplate."Vendor Receipt Report ID",FALSE,FALSE,VendLedgEntry);
          END;
          IF GenJnlTemplate."Posting Report ID" <> 0 THEN BEGIN
            GLReg.SETRECFILTER;
            REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg);
          END;
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

