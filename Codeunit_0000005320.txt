OBJECT Codeunit 5320 Exchange Web Services Client
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      TempExchangeFolder@1002 : TEMPORARY Record 5320;
      Service@1000 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=7.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.ExchangeServiceWrapper" RUNONCLIENT;
      Text001@1003 : TextConst 'ENU=Connection to the Exchange server failed.;ESM=Error al conectar con Exchange Server.;FRC=chec de connexion au serveur Exchange.;ENC=Connection to the Exchange server failed.';
      Text002@1004 : TextConst 'ENU=Folders with a path that exceeds 250 characters have been omitted.;ESM=Se han omitido las carpetas con una ruta de acceso que supera los 250 caracteres.;FRC=Les dossiers dont le chemin d''accŠs comporte plus de 250ÿcaractŠres ont ‚t‚ omis.;ENC=Folders with a path that exceeds 250 characters have been omitted.';
      LongPathsDetected@1001 : Boolean;

    PROCEDURE GetPublicFolders@2(VAR ExchangeFolder@1000 : Record 5320) FoundAny : Boolean;
    VAR
      ParentInfo@1003 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=7.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.FolderInfo" RUNONCLIENT;
      SubFolders@1002 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=7.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.FolderInfoEnumerator" RUNONCLIENT;
    BEGIN
      IF NOT IsServiceValid THEN
        ERROR(Text001);

      IF ExchangeFolder.Cached THEN
        EXIT;

      IF ExchangeFolder."Unique ID".HASVALUE THEN BEGIN
        ParentInfo := ParentInfo.FolderInfo(ExchangeFolder.GetUniqueID,ExchangeFolder.FullPath);
        ExchangeFolder.Cached := TRUE;
        ExchangeFolder.MODIFY;
      END;

      FoundAny := FALSE;
      LongPathsDetected := FALSE;
      SubFolders := Service.GetPublicFolders(ParentInfo,1000);

      IF NOT ISNULL(SubFolders) THEN BEGIN
        WHILE SubFolders.MoveNextPage DO
          WHILE SubFolders.MoveNext DO
            IF STRLEN(SubFolders.Current.FullPath) > 250 THEN
              LongPathsDetected := TRUE
            ELSE
              IF NOT TempExchangeFolder.GET(SubFolders.Current.FullPath) THEN
                IF IsAllowedFolderType(SubFolders.Current.FolderClass) THEN BEGIN
                  FoundAny := TRUE;
                  TempExchangeFolder.INIT;
                  TempExchangeFolder.FullPath := SubFolders.Current.FullPath;
                  TempExchangeFolder.Depth := SubFolders.Current.Depth;
                  TempExchangeFolder.SetUniqueID(SubFolders.Current.UniqueId);
                  TempExchangeFolder.Name := SubFolders.Current.Name;
                  TempExchangeFolder.Cached := FALSE;
                  TempExchangeFolder.INSERT;
                END;
        IF LongPathsDetected THEN
          MESSAGE(Text002);
        ReadBuffer(ExchangeFolder);
      END;

      IF Service.LastError <> '' THEN
        MESSAGE(Service.LastError);

      EXIT(FoundAny);
    END;

    PROCEDURE Initialize@3(AutodiscoveryEmail@1000 : Text[250]) Result : Boolean;
    VAR
      ServiceFactory@1002 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=7.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.ServiceWrapperFactory" RUNONCLIENT;
    BEGIN
      IF ISNULL(Service) THEN
        Service := ServiceFactory.CreateServiceWrapper;

      IF Service.ExchangeServiceUrl = '' THEN
        Result := Service.AutodiscoverServiceUrl(AutodiscoveryEmail)
      ELSE
        Result := TRUE;
    END;

    PROCEDURE FolderExists@1(UniqueID@1000 : Text) : Boolean;
    BEGIN
      IF NOT IsServiceValid THEN
        ERROR(Text001);
      EXIT(Service.FolderExists(UniqueID));
    END;

    PROCEDURE ReadBuffer@6(VAR DestExchangeFolder@1000 : Record 5320) : Boolean;
    BEGIN
      IF TempExchangeFolder.FINDSET THEN
        REPEAT
          IF NOT DestExchangeFolder.GET(TempExchangeFolder.FullPath) THEN BEGIN
            TempExchangeFolder.CALCFIELDS("Unique ID");
            DestExchangeFolder.INIT;
            DestExchangeFolder.TRANSFERFIELDS(TempExchangeFolder);
            DestExchangeFolder.INSERT;
          END;
        UNTIL TempExchangeFolder.NEXT = 0
      ELSE
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE IsAllowedFolderType@7(FolderClass@1000 : Text) : Boolean;
    BEGIN
      IF FolderClass = '' THEN
        EXIT(TRUE);

      IF FolderClass = 'IPF.Note' THEN
        EXIT(TRUE);

      EXIT(FALSE);
    END;

    LOCAL PROCEDURE IsServiceValid@4() : Boolean;
    BEGIN
      IF ISNULL(Service) THEN
        EXIT(FALSE);

      EXIT(Service.ExchangeServiceUrl <> '');
    END;

    PROCEDURE InvalidateService@5();
    BEGIN
      CLEAR(Service);
    END;

    BEGIN
    END.
  }
}

