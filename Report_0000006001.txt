OBJECT Report 6001 Batch Post Service Orders
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Service Orders;
               ESM=Reg. lotes ped. servicio;
               FRC=TPL Commandes service;
               ENC=Batch Post Service Orders];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 1634;    ;DataItem;                    ;
               DataItemTable=Table5900;
               DataItemTableView=SORTING(Document Type,No.)
                                 WHERE(Document Type=CONST(Order));
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               CounterTotal := COUNT;
                               Window.OPEN(Text001);
                             END;

               OnAfterGetRecord=BEGIN
                                  IF CalcInvDisc THEN
                                    CalculateInvoiceDiscount;

                                  Counter := Counter + 1;
                                  Window.UPDATE(1,"No.");
                                  Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                                  CLEAR(ServPost);
                                  ServPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                                  ServPost.SetPostingOptions(ShipReq,FALSE,InvReq);
                                  ServPost.SetHideValidationDialog(TRUE);
                                  IF ServPost.RUN("Service Header") THEN BEGIN
                                    CounterOK := CounterOK + 1;
                                    IF MARKEDONLY THEN
                                      MARK(FALSE);
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                MESSAGE(Text002,CounterOK,CounterTotal);
                              END;

               ReqFilterFields=No.,Status,Priority }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   InitValues;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=Ship;
                  CaptionML=[ENU=Ship;
                             ESM=Env¡o;
                             FRC=Livrer;
                             ENC=Ship];
                  SourceExpr=ShipReq }

      { 2   ;2   ;Field     ;
                  Name=Invoice;
                  CaptionML=[ENU=Invoice;
                             ESM=Factura;
                             FRC=Facture;
                             ENC=Invoice];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  Name=PostingDate;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  Name=ReplacePostingDate_Option;
                  CaptionML=[ENU=Replace Posting Date;
                             ESM=Reemplazar fecha reg.;
                             FRC=Remplacer la date de report;
                             ENC=Replace Posting Date];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  Name=ReplaceDocumentDate_Option;
                  CaptionML=[ENU=Replace Document Date;
                             ESM=Reemplazar fecha documento;
                             FRC=Remplacer date document;
                             ENC=Replace Document Date];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  Name=CalcInvDiscount;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             ESM=Calc. dto. factura;
                             FRC=Calculer escompte facture;
                             ENC=Calc. Inv. Discount];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESM=Introduzca la fecha registro.;FRC=Entrez la date de report.;ENC=Please enter the posting date.';
      Text001@1001 : TextConst 'ENU=Posting orders  #1########## @2@@@@@@@@@@@@@;ESM=Reg. pedidos    #1########## @2@@@@@@@@@@@@@;FRC=Report des commandes  #1########## @2@@@@@@@@@@@@@;ENC=Posting orders  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 orders out of a total of %2 have now been posted.;ESM=Se han registrado %1 pedidos de un total de %2.;FRC=%1 commandes sur un total de %2 ont maintenant ‚t‚ report‚es.;ENC=%1 orders out of a total of %2 have now been posted.';
      Text003@1003 : TextConst 'ENU=The exchange rate associated with the new posting date on the service header will not apply to the service lines.;ESM=El tipo de cambio asociado a la nueva fecha de registro en la cabecera de servicio no se aplicar  a las l¡neas de servicio.;FRC=Le taux de change associ‚ … la nouvelle date de report sur l''en-tˆte de service ne s''appliquera pas aux lignes de service.;ENC=The exchange rate associated with the new posting date on the service header will not apply to the service lines.';
      ServLine@1005 : Record 5902;
      SalesSetup@1006 : Record 311;
      ServCalcDisc@1007 : Codeunit 5950;
      ServPost@1008 : Codeunit 5980;
      Window@1009 : Dialog;
      ShipReq@1010 : Boolean;
      InvReq@1011 : Boolean;
      PostingDateReq@1012 : Date;
      CounterTotal@1013 : Integer;
      Counter@1014 : Integer;
      CounterOK@1015 : Integer;
      ReplacePostingDate@1016 : Boolean;
      ReplaceDocumentDate@1017 : Boolean;
      CalcInvDisc@1018 : Boolean;

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      ServLine.RESET;
      ServLine.SETRANGE("Document Type","Service Header"."Document Type");
      ServLine.SETRANGE("Document No.","Service Header"."No.");
      IF ServLine.FINDFIRST THEN
        IF ServCalcDisc.RUN(ServLine) THEN BEGIN
          "Service Header".GET("Service Header"."Document Type","Service Header"."No.");
          COMMIT;
        END;
    END;

    PROCEDURE InitializeRequest@2(ShipReqFrom@1004 : Boolean;InvReqFrom@1005 : Boolean;PostingDateReqFrom@1000 : Date;ReplacePostingDateFrom@1003 : Boolean;ReplaceDocumentDateFrom@1002 : Boolean;CalcInvDiscFrom@1001 : Boolean);
    BEGIN
      InitValues;
      ShipReq := ShipReqFrom;
      InvReq := InvReqFrom;
      PostingDateReq := PostingDateReqFrom;
      ReplacePostingDate := ReplacePostingDateFrom;
      ReplaceDocumentDate := ReplaceDocumentDateFrom;
      IF CalcInvDiscFrom THEN
        SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
      CalcInvDisc := CalcInvDiscFrom;
    END;

    PROCEDURE InitValues@6();
    BEGIN
      SalesSetup.GET;
      CalcInvDisc := SalesSetup."Calc. Inv. Discount";
      ReplacePostingDate := FALSE;
      ReplaceDocumentDate := FALSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

