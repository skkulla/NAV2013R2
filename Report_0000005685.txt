OBJECT Report 5685 Copy Fixed Asset
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy Fixed Asset;
               ESM=Copiar activo fijo;
               FRC=Copier l'immobilisation;
               ENC=Copy Fixed Asset];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  DefaultDim.LOCKTABLE;
                  FADeprBook.LOCKTABLE;
                  FA.LOCKTABLE;
                  IF FANo = '' THEN
                    ERROR(Text000,FA.TABLECAPTION,FA.FIELDCAPTION("No."));
                  IF (FirstFANo = '') AND NOT UseFANoSeries THEN
                    ERROR(Text001);
                  FA.GET(FANo);
                  FADeprBook."FA No." := FANo;
                  FADeprBook.SETRANGE("FA No.",FANo);
                  DefaultDim."Table ID" := DATABASE::"Fixed Asset";
                  DefaultDim."No." := FANo;
                  DefaultDim.SETRANGE("Table ID",DATABASE::"Fixed Asset");
                  DefaultDim.SETRANGE("No.",FANo);
                  DefaultDim2 := DefaultDim;
                  FOR I := 1 TO NumberofCopies DO BEGIN
                    FA2 := FA;
                    FA2."No." := '';
                    FA2."Last Date Modified" := 0D;
                    FA2."Main Asset/Component" := FA2."Main Asset/Component"::" ";
                    FA2."Component of Main Asset" := '';
                    IF UseFANoSeries THEN
                      FA2.INSERT(TRUE)
                    ELSE BEGIN
                      FA2."No." := FirstFANo;
                      IF NumberofCopies > 1 THEN
                        FirstFANo := INCSTR(FirstFANo);
                      IF FA2."No." = '' THEN
                        ERROR(Text002,FA.TABLECAPTION,FA.FIELDCAPTION("No."));
                      FA2.INSERT(TRUE);
                    END;
                    IF DefaultDim.FIND('-') THEN
                      REPEAT
                        DefaultDim2 := DefaultDim;
                        DefaultDim2."No." := FA2."No.";
                        DefaultDim2.INSERT(TRUE);
                      UNTIL DefaultDim.NEXT = 0;
                    IF FADeprBook.FIND('-') THEN
                      REPEAT
                        FADeprBook2 := FADeprBook;
                        FADeprBook2."FA No." := FA2."No.";
                        FADeprBook2.INSERT(TRUE);
                      UNTIL FADeprBook.NEXT = 0;
                    IF FA2.FIND THEN BEGIN;
                      FA2."Last Date Modified" := 0D;
                      FA2.MODIFY;
                    END;
                  END;
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF NumberofCopies < 1 THEN
                     NumberofCopies := 1;
                   FANo := FANo2;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Copy from FA No.;
                             ESM=A/F Copiar desde n§;
                             FRC=Copier du nø immo.;
                             ENC=Copy from FA No.];
                  SourceExpr=FANo;
                  TableRelation="Fixed Asset" }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=No. of Copies;
                             ESM=N§ copias;
                             FRC=Nombre de copies;
                             ENC=No. of Copies];
                  SourceExpr=NumberofCopies;
                  MinValue=1 }

      { 9   ;2   ;Field     ;
                  CaptionML=[ENU=First FA No.;
                             ESM=N§ primer A/F;
                             FRC=Premier Nø d'immobilisation;
                             ENC=First FA No.];
                  SourceExpr=FirstFANo;
                  OnValidate=BEGIN
                               IF FirstFANo <> '' THEN
                                 UseFANoSeries := FALSE;
                             END;
                              }

      { 7   ;2   ;Field     ;
                  CaptionML=[ENU=Use FA No. Series;
                             ESM=Usar nos. serie A/F;
                             FRC=Utiliser les num‚ros de s‚rie d'immobilisations;
                             ENC=Use FA No. Series];
                  SourceExpr=UseFANoSeries;
                  OnValidate=BEGIN
                               IF UseFANoSeries THEN
                                 FirstFANo := '';
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      DefaultDim@1008 : Record 352;
      DefaultDim2@1009 : Record 352;
      FA@1004 : Record 5600;
      FA2@1006 : Record 5600;
      FADeprBook@1010 : Record 5612;
      FADeprBook2@1011 : Record 5612;
      FANo@1000 : Code[20];
      FANo2@1015 : Code[20];
      FirstFANo@1002 : Code[20];
      UseFANoSeries@1001 : Boolean;
      NumberofCopies@1003 : Integer;
      I@1005 : Integer;
      Text000@1012 : TextConst 'ENU="You must specify a number in the Copy from FA No. field ";ESM="Debe especificar un n£mero en el campo Copiar de n§ A/F ";FRC="Vous devez sp‚cifier un num‚ro dans le champ Copier … partir du Nø d''immobilisation ";ENC="You must specify a number in the Copy from FA No. field "';
      Text001@1013 : TextConst 'ENU=You must specify a number in First FA No. field or use the FA No. Series.;ESM=Debe especificar un n£mero en el campo N§ primer A/F ¢ usar el n§ serie A/F;FRC=Vous devez sp‚cifier un num‚ro dans le champ Premier Nø d''immobilisation ou utiliser les Nos de s‚rie d''immobilisations.;ENC=You must specify a number in First FA No. field or use the FA No. Series.';
      Text002@1014 : TextConst 'ENU=You must include a number in the First FA No. field.;ESM=Debe incluir un n£mero en el campo N§ primer A/F.;FRC=Vous devez sp‚cifier un num‚ro dans le champ Premier Nø d''immobilisation;ENC=You must include a number in the First FA No. field.';

    PROCEDURE SetFANo@1(NewFANo@1000 : Code[20]);
    BEGIN
      FANo2 := NewFANo;
    END;

    PROCEDURE InitializeRequest@2(NewFANo@1000 : Code[20];NewNumberofCopies@1001 : Integer;NewFirstFANo@1002 : Code[20];NewUseFANoSeries@1003 : Boolean);
    BEGIN
      NumberofCopies := NewNumberofCopies;
      FirstFANo := NewFirstFANo;
      UseFANoSeries := NewUseFANoSeries;
      FANo := NewFANo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

