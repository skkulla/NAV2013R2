OBJECT Report 1137 Transfer Budget to Actual
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Transfer Budget to Actual;
               ESM=Transferir presupuesto a real;
               FRC=Transf‚rer le budget vers R‚el;
               ENC=Transfer Budget to Actual];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3233;    ;DataItem;                    ;
               DataItemTable=Table1109;
               DataItemTableView=SORTING(Budget Name,Cost Type No.,Date);
               OnPreDataItem=BEGIN
                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text000);

                               IF GETFILTER(Date) = '' THEN
                                 ERROR(Text001);

                               IF NOT CONFIRM(Text002,TRUE,GETFILTER("Budget Name"),GETFILTER(Date)) THEN
                                 ERROR('');

                               LOCKTABLE;

                               Window.OPEN(Text003);

                               Window.UPDATE(1,COUNT);
                             END;

               OnAfterGetRecord=VAR
                                  SourceCodeSetup@1001 : Record 242;
                                BEGIN
                                  SourceCodeSetup.GET;
                                  SourceCodeSetup.TESTFIELD("Transfer Budget to Actual");
                                  TempCostJnlLine.INIT;
                                  LastEntryNo := LastEntryNo + 1;
                                  TempCostJnlLine."Line No." := LastEntryNo;
                                  TempCostJnlLine."Cost Type No." := "Cost Type No.";
                                  TempCostJnlLine."Posting Date" := Date;
                                  TempCostJnlLine."Document No." := "Document No.";
                                  IF TempCostJnlLine."Document No." = '' THEN
                                    TempCostJnlLine."Document No." := 'BUDGET';
                                  TempCostJnlLine.Description := Description;
                                  TempCostJnlLine.Amount := Amount;
                                  TempCostJnlLine."Cost Center Code" := "Cost Center Code";
                                  TempCostJnlLine."Cost Object Code" := "Cost Object Code";
                                  TempCostJnlLine."Source Code" := SourceCodeSetup."Transfer Budget to Actual";
                                  TempCostJnlLine."Allocation Description" := "Allocation Description";
                                  TempCostJnlLine."Allocation ID" := "Allocation ID";
                                  TempCostJnlLine.INSERT;

                                  NoInserted := NoInserted + 1;
                                  IF (NoInserted MOD 100) = 0 THEN
                                    Window.UPDATE(2,NoInserted);
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF NOT CONFIRM(Text004,TRUE,NoInserted) THEN
                                  ERROR('');

                                PostCostJournalLines;
                              END;

               ReqFilterFields=Budget Name,Date,Allocated,Cost Type No.,Cost Center Code,Cost Object Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      TempCostJnlLine@1000 : TEMPORARY Record 1101;
      Window@1001 : Dialog;
      LastEntryNo@1002 : Integer;
      NoInserted@1003 : Integer;
      Text000@1004 : TextConst 'ENU=Define the name of the source budget.;ESM=Defina el nombre del presupuesto de origen.;FRC=D‚finissez le nom du budget origine.;ENC=Define the name of the source budget.';
      Text001@1005 : TextConst 'ENU=Date range must be defined.;ESM=Debe definirse un rango de fechas.;FRC=La plage de dates doit ˆtre d‚finie.;ENC=Date range must be defined.';
      Text002@1006 : TextConst 'ENU=The cost budget "%1" for the date range of "%2" will be transferred to the actual cost entries. \Do you want to start the job?;ESM=El ppto. costos "%1" para el rango de fechas de "%2" se transferir  a movs. de costo real. \¨Desea iniciar el proyecto?;FRC=Le budget des co–ts ®ÿ%1ÿ¯ pour la plage de dates ®ÿ%2ÿ¯ sera transf‚r‚ vers les ‚critures de co–ts r‚els. \Voulez-vous commencer le projetÿ?;ENC=The cost budget "%1" for the date range of "%2" will be transferred to the actual cost entries. \Do you want to start the job?';
      Text003@1007 : TextConst 'ENU=Copying budget entries\No of entries #1#####\Copied        #2#####;ESM=Copiando movs. ppto.\N§ movs.    #1#####\Copiados        #2#####;FRC=Copie ‚critures budget\Nombre d''‚critures #1#####\Copi‚        #2#####;ENC=Copying budget entries\No of entries #1#####\Copied        #2#####';
      Text004@1008 : TextConst 'ENU=%1 budget entries were transferred to actual cost entries.\\Do you want to copy entries?;ESM=%1 movs. ppto. transferidos a movs. de costo real.\\¨Desea copiar los movs.?;FRC=%1 ‚critures budget ont ‚t‚ transf‚r‚es vers les ‚critures de co–ts r‚els.\\Voulez-vous copier les ‚crituresÿ?;ENC=%1 budget entries were transferred to actual cost entries.\\Do you want to copy entries?';
      Text005@1009 : TextConst 'ENU=Posting Cost Entries                 @1@@@@@@@@@@\;ESM=Registrando movimientos de costo @1@@@@@@@@@@\;FRC=Report des ‚critures de co–ts             @1@@@@@@@@@@\;ENC=Posting Cost Entries                 @1@@@@@@@@@@\';

    PROCEDURE PostCostJournalLines@1();
    VAR
      CostJnlLine@1000 : Record 1101;
      CAJnlPostLine@1001 : Codeunit 1102;
      Window2@1002 : Dialog;
      JournalLineCount@1003 : Integer;
      CostJnlLineStep@1005 : Integer;
    BEGIN
      TempCostJnlLine.RESET;
      Window2.OPEN(
        Text005);
      IF TempCostJnlLine.COUNT > 0 THEN
        JournalLineCount := 10000 * 100000 DIV TempCostJnlLine.COUNT;
      IF TempCostJnlLine.FINDSET THEN
        REPEAT
          CostJnlLineStep := CostJnlLineStep + JournalLineCount;
          Window2.UPDATE(1,CostJnlLineStep DIV 100000);
          CostJnlLine := TempCostJnlLine;
          CAJnlPostLine.RunWithCheck(CostJnlLine);
        UNTIL TempCostJnlLine.NEXT = 0;
      Window2.CLOSE;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

