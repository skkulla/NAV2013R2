OBJECT Report 1139 Delete Cost Budget Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 96=rm;
    CaptionML=[ENU=Delete Cost Budget Entries;
               ESM=Eliminar movs. ppto. costos;
               FRC=Supprimer ‚critures budget des co–ts;
               ENC=Delete Cost Budget Entries];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF CostBudgetRegister2."No." > CostBudgetRegister3."No." THEN
                    ERROR(Text000);

                  IF NOT CONFIRM(Text001,FALSE,CostBudgetRegister2."No.",CostBudgetRegister3."No.") THEN
                    ERROR('');

                  IF NOT CONFIRM(Text004) THEN
                    ERROR('');

                  Window.OPEN(Text005 +
                    Text006 );
                END;

  }
  DATASET
  {
    { 6875;    ;DataItem;                    ;
               DataItemTable=Table1111;
               DataItemTableView=SORTING(No.)
                                 ORDER(Descending);
               OnPreDataItem=BEGIN
                               // Sort descending. Registers are deleted backwards
                               SETRANGE("No.",CostBudgetRegister2."No.",CostBudgetRegister3."No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");

                                  IF Closed THEN
                                    ERROR(Text007,"No.");

                                  IF Source = Source::Allocation THEN BEGIN
                                    CostBudgetEntry.SETRANGE("Allocated with Journal No.","No.");
                                    CostBudgetEntry.MODIFYALL(Allocated,FALSE);
                                    CostBudgetEntry.MODIFYALL("Allocated with Journal No.",0);
                                  END;

                                  CostBudgetEntry.SETRANGE("Entry No.","From Cost Budget Entry No.","To Cost Budget Entry No.");
                                  CostBudgetEntry.DELETEALL;
                                  CostBudgetEntry.RESET;
                                END;

               OnPostDataItem=VAR
                                CostAccSetup@1000 : Record 1108;
                              BEGIN
                                DELETEALL;
                                RESET;
                                SETRANGE(Source,Source::Allocation);

                                IF FINDLAST THEN BEGIN
                                  CostBudgetEntry.GET("To Cost Budget Entry No.");
                                  CostAccSetup.GET;
                                  CostAccSetup."Last Allocation Doc. No." := CostBudgetEntry."Document No.";
                                  CostAccSetup.MODIFY;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   CostBudgetRegister2.FINDLAST;
                   CostBudgetRegister3.FINDLAST;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=FromRegisterNo;
                  Lookup=Yes;
                  CaptionML=[ENU=From Register No.;
                             ESM=Desde n§ registro;
                             FRC=Nø hist. transaction d'origine;
                             ENC=From Register No.];
                  SourceExpr=CostBudgetRegister2."No.";
                  TableRelation="Cost Budget Register" WHERE (Closed=CONST(No)) }

      { 3   ;2   ;Field     ;
                  Name=ToRegisterNo;
                  CaptionML=[ENU=To Register No.;
                             ESM=Hasta n§ registro;
                             FRC=Nø hist. transaction de destination;
                             ENC=To Register No.];
                  SourceExpr=CostBudgetRegister3."No.";
                  TableRelation="Cost Budget Register" WHERE (Closed=CONST(No));
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=From Register No. must not be higher than To Register No..;ESM=Desde n§ reg. no puede ser superior a Hasta n§ registro.;FRC=Le nø hist. transaction d''origine ne doit pas ˆtre sup‚rieur au nø hist. transaction de destination.;ENC=From Register No. must not be higher than To Register No..';
      Text001@1001 : TextConst 'ENU=All corresponding cost budget entries and budget register entries will be deleted. Do you want to delete cost budget register %1 to %2?;ESM=Se eliminar n todos los movs. presupuesto de costos y registro de costos correspondientes. ¨Desea eliminar el registro de presupuesto de costos %1 hasta %2?;FRC=Toutes les ‚critures de budget des co–ts et de registre du budget seront supprim‚es. Voulez-vous supprimer le registre du budget des co–ts %1 de %2ÿ?;ENC=All corresponding cost budget entries and budget register entries will be deleted. Do you want to delete cost budget register %1 to %2?';
      Text004@1002 : TextConst 'ENU=Are you sure?;ESM=¨Est  seguro?;FRC=Òtes-vous certainÿ?;ENC=Are you sure?';
      Text005@1003 : TextConst 'ENU=Delete cost register\;ESM=Eliminar reg. costos\;FRC=Supprimer registre de co–ts\;ENC=Delete cost register\';
      Text006@1004 : TextConst 'ENU=Register  no.      #1######;ESM=N§ reg.       #1######;FRC=Nø hist. transaction      #1######;ENC=Register  no.      #1######';
      Text007@1005 : TextConst 'ENU=Register %1 can no longer be deleted because it is marked as closed.;ESM=El registro %1 ya no se puede borrar porque est  marcado como cerrado.;FRC=Le registre %1 ne peut plus ˆtre supprim‚ car il est marqu‚ comme ferm‚.;ENC=Register %1 can no longer be deleted because it is marked as closed.';
      CostBudgetRegister2@1006 : Record 1111;
      CostBudgetRegister3@1007 : Record 1111;
      CostBudgetEntry@1008 : Record 1109;
      Window@1009 : Dialog;

    PROCEDURE InitializeRequest@1(FromEntryNo@1000 : Integer;ToEntryNo@1001 : Integer);
    BEGIN
      CostBudgetRegister2."No." := FromEntryNo;
      CostBudgetRegister3."No." := ToEntryNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

