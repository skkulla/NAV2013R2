OBJECT Codeunit 51 BOM-Explode BOM
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=90;
    OnRun=BEGIN
            TESTFIELD(Type,Type::Item);
            IF "No." = "Parent Item No." THEN
              ERROR(Text000);

            FromBOMComp.SETRANGE("Parent Item No.","No.");
            ToBOMComp.SETRANGE("Parent Item No.","Parent Item No.");

            NoOfBOMComp := FromBOMComp.COUNT;
            IF NoOfBOMComp = 0 THEN
              ERROR(
                Text001,
                "No.");

            ToBOMComp := Rec;
            IF ToBOMComp.FIND('>') THEN BEGIN
              LineSpacing := (ToBOMComp."Line No." - "Line No.") DIV (1 + NoOfBOMComp);
              IF LineSpacing = 0 THEN
                ERROR(Text002);
            END ELSE
              LineSpacing := 10000;

            Item.GET("No.");
            QtyPerUnitOfMeasure := UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code");

            FromBOMComp.FIND('-');
            NextLineNo := "Line No.";
            REPEAT
              NextLineNo := NextLineNo + LineSpacing;
              ToBOMComp := FromBOMComp;
              ToBOMComp."Parent Item No." := "Parent Item No.";
              ToBOMComp."Line No." := NextLineNo;
              ToBOMComp."Quantity per" := ROUND(QtyPerUnitOfMeasure * "Quantity per" * FromBOMComp."Quantity per",0.00001);
              ToBOMComp.Position := STRSUBSTNO(Position,FromBOMComp.Position);
              ToBOMComp."Installed in Line No." := "Installed in Line No.";
              ToBOMComp."Installed in Item No." := "Installed in Item No.";
              ToBOMComp.INSERT;
            UNTIL FromBOMComp.NEXT = 0;

            DELETE;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=A bill of materials cannot be a component of itself.;ESM=Una lista de materiales no puede contenerse as¡ misma.;FRC=Un article ne peut-ˆtre une composante de sa propre nomenclature.;ENC=A bill of materials cannot be a component of itself.';
      Text001@1001 : TextConst 'ENU=Item %1 is not a BOM.;ESM=El producto %1 no es una L.M.;FRC=L''article %1 n''est pas une nomenclature.;ENC=Item %1 is not a BOM.';
      Text002@1002 : TextConst 'ENU=There is not enough space to explode the BOM.;ESM=No hay suficiente espacio para desplegar la lista de materiales.;FRC=Il n''y a pas assez d''espace pour ‚clater la nomenclature.;ENC=There is not enough space to explode the BOM.';
      FromBOMComp@1003 : Record 90;
      ToBOMComp@1004 : Record 90;
      Item@1008 : Record 27;
      UOMMgt@1009 : Codeunit 5402;
      LineSpacing@1005 : Integer;
      NextLineNo@1006 : Integer;
      NoOfBOMComp@1007 : Integer;
      QtyPerUnitOfMeasure@1010 : Decimal;

    BEGIN
    END.
  }
}

