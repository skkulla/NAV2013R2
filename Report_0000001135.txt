OBJECT Report 1135 Copy G/L Budget to Cost Acctg.
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Copy G/L Budget to Cost Acctg.;
               ESM=Copiar Ppto. contable en Ppto. costos;
               FRC=Copier budget GL vers comptabilit‚ analytique;
               ENC=Copy G/L Budget to Cost Acctg.];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 3459;    ;DataItem;                    ;
               DataItemTable=Table96;
               DataItemTableView=SORTING(Budget Name,G/L Account No.,Date);
               OnPreDataItem=BEGIN
                               IF GETFILTER("Budget Name") = '' THEN
                                 ERROR(Text004);

                               IF CostBudgetEntryTarget."Budget Name" = '' THEN
                                 ERROR(Text005);

                               IF NOT CONFIRM(Text000,TRUE,GETFILTER("Budget Name"),CostBudgetEntryTarget."Budget Name") THEN
                                 ERROR('');

                               LOCKTABLE;

                               IF CostBudgetEntry.FINDLAST THEN
                                 NextEntryNo := CostBudgetEntry."Entry No.";
                               NextEntryNo := NextEntryNo + 1;

                               Window.OPEN(Text002);

                               Window.UPDATE(1,COUNT);

                               FirstCostBudgetEntryNo := NextEntryNo;
                               FINDFIRST;
                               FirstGLBudgetEntryNo := "Entry No.";
                             END;

               OnAfterGetRecord=BEGIN
                                  CostBudgetEntry.INIT;
                                  CostBudgetEntry."Entry No." := NextEntryNo;

                                  CostBudgetEntry."Budget Name" := CostBudgetEntryTarget."Budget Name";

                                  IF DateFormulaChange <> '' THEN
                                    CostBudgetEntry.Date := CALCDATE(DateFormula,Date)
                                  ELSE
                                    CostBudgetEntry.Date := Date;

                                  IF CostBudgetEntryTarget."Cost Type No." <> '' THEN
                                    CostBudgetEntry."Cost Type No." := CostBudgetEntryTarget."Cost Type No."
                                  ELSE BEGIN
                                    IF NOT GLAccount.GET("G/L Account No.") OR (GLAccount."Cost Type No." = '') THEN BEGIN
                                      NoSkipped := NoSkipped + 1;
                                      CurrReport.SKIP;
                                    END;
                                    CostBudgetEntry."Cost Type No." := GLAccount."Cost Type No.";
                                  END;

                                  IF CostBudgetEntryTarget."Cost Center Code" <> '' THEN
                                    CostBudgetEntry."Cost Center Code" := CostBudgetEntryTarget."Cost Center Code"
                                  ELSE BEGIN
                                    CostBudgetEntry."Cost Center Code" := CostAccMgt.GetCostCenterCodeFromDimSet("Dimension Set ID");
                                    IF NOT CostAccMgt.CostCenterExists(CostBudgetEntry."Cost Center Code") THEN
                                      CostBudgetEntry."Cost Center Code" := '';
                                  END;

                                  IF CostBudgetEntry."Cost Center Code" = '' THEN
                                    IF CostBudgetEntryTarget."Cost Object Code" <> '' THEN
                                      CostBudgetEntry."Cost Object Code" := CostBudgetEntryTarget."Cost Object Code"
                                    ELSE BEGIN
                                      CostBudgetEntry."Cost Object Code" := CostAccMgt.GetCostObjectCodeFromDimSet("Dimension Set ID");
                                      IF NOT CostAccMgt.CostObjectExists(CostBudgetEntry."Cost Object Code") THEN
                                        CostBudgetEntry."Cost Object Code" := '';
                                    END;

                                  IF (CostBudgetEntry."Cost Center Code" = '') AND (CostBudgetEntry."Cost Object Code" = '') THEN BEGIN
                                    NoSkipped := NoSkipped + 1;
                                    CurrReport.SKIP;
                                  END;

                                  CostBudgetEntry.Amount := Amount;
                                  CostBudgetEntry.Description := Description;
                                  TotalAmount := TotalAmount + Amount;

                                  CostBudgetEntry.INSERT;
                                  NextEntryNo := NextEntryNo + 1;

                                  NoInserted := NoInserted + 1;
                                  IF (NoInserted MOD 100) = 0 THEN
                                    Window.UPDATE(2,NoInserted);
                                END;

               OnPostDataItem=BEGIN
                                LastGLBudgetEntryNo := "Entry No.";
                                Window.CLOSE;

                                IF NoInserted = 0 THEN BEGIN
                                  MESSAGE(Text006,NoSkipped);
                                  ERROR('');
                                END;

                                IF NOT CONFIRM(Text003,TRUE,NoInserted,CostBudgetEntryTarget."Budget Name",NoSkipped) THEN
                                  ERROR('');

                                LastCostBudgetEntryNo := NextEntryNo - 1;

                                CostBudgetRegister.LOCKTABLE;
                                IF CostBudgetRegister.FINDLAST THEN
                                  LastRegisterNo := CostBudgetRegister."No.";

                                CostBudgetRegister.INIT;
                                CostBudgetRegister."No." := LastRegisterNo + 1;
                                CostBudgetRegister."Journal Batch Name" := '';
                                CostBudgetRegister."Cost Budget Name" := CostBudgetEntryTarget."Budget Name";
                                CostBudgetRegister.Source := CostBudgetRegister.Source::"Transfer from G/L Budget";
                                CostBudgetRegister."From Budget Entry No." := FirstGLBudgetEntryNo;
                                CostBudgetRegister."To Budget Entry No." := LastGLBudgetEntryNo;
                                CostBudgetRegister."From Cost Budget Entry No." := FirstCostBudgetEntryNo;
                                CostBudgetRegister."To Cost Budget Entry No." := LastCostBudgetEntryNo;
                                CostBudgetRegister."No. of Entries" := NoInserted;
                                CostBudgetRegister.Amount := TotalAmount;
                                CostBudgetRegister."User ID" := USERID;
                                CostBudgetRegister."Processed Date" := TODAY;
                                CostBudgetRegister.INSERT;
                              END;

               ReqFilterFields=Budget Name,G/L Account No.,Date,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code,Budget Dimension 4 Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   CostBudgetEntryTarget.INIT;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 21  ;2   ;Group     ;
                  CaptionML=[ENU=Copy to...;
                             ESM=Copiar a...;
                             FRC=Copier vers...;
                             ENC=Copy to...] }

      { 1   ;3   ;Field     ;
                  Name=Budget Name;
                  Lookup=Yes;
                  CaptionML=[ENU=Budget Name;
                             ESM=Nombre ppto.;
                             FRC=Nom du budget;
                             ENC=Budget Name];
                  SourceExpr=CostBudgetEntryTarget."Budget Name";
                  TableRelation="Cost Budget Name" }

      { 2   ;3   ;Field     ;
                  Name=Cost Type No.;
                  Lookup=Yes;
                  CaptionML=[ENU=Cost Type No.;
                             ESM=N§ tipo costo;
                             FRC=Nø type co–t;
                             ENC=Cost Type No.];
                  SourceExpr=CostBudgetEntryTarget."Cost Type No.";
                  TableRelation="Cost Type" }

      { 4   ;3   ;Field     ;
                  Name=Cost Center Code;
                  Lookup=Yes;
                  CaptionML=[ENU=Cost Center Code;
                             ESM=C¢digo centro costo;
                             FRC=Code centre de co–ts;
                             ENC=Cost Centre Code];
                  SourceExpr=CostBudgetEntryTarget."Cost Center Code";
                  TableRelation="Cost Center" }

      { 5   ;3   ;Field     ;
                  Name=Cost Object Code;
                  Lookup=Yes;
                  CaptionML=[ENU=Cost Object Code;
                             ESM=C¢digo objeto costo;
                             FRC=Code objet de co–ts;
                             ENC=Cost Object Code];
                  SourceExpr=CostBudgetEntryTarget."Cost Object Code";
                  TableRelation="Cost Object" }

      { 6   ;2   ;Field     ;
                  Name=Date Change Formula;
                  CaptionML=[ENU=Date Change Formula;
                             ESM=F¢rmula de cambio fecha;
                             FRC=Formule de changement de date;
                             ENC=Date Change Formula];
                  DateFormula=Yes;
                  SourceExpr=DateFormulaChange;
                  OnValidate=BEGIN
                               EVALUATE(DateFormula,DateFormulaChange);
                             END;
                              }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      CostBudgetEntryTarget@1000 : Record 1109;
      CostBudgetEntry@1001 : Record 1109;
      GLAccount@1002 : Record 15;
      CostBudgetRegister@1003 : Record 1111;
      CostAccMgt@1004 : Codeunit 1100;
      Window@1005 : Dialog;
      DateFormulaChange@1006 : Code[10];
      DateFormula@1007 : DateFormula;
      NextEntryNo@1008 : Integer;
      NoInserted@1009 : Integer;
      NoSkipped@1010 : Integer;
      FirstGLBudgetEntryNo@1011 : Integer;
      LastGLBudgetEntryNo@1012 : Integer;
      FirstCostBudgetEntryNo@1013 : Integer;
      LastCostBudgetEntryNo@1014 : Integer;
      TotalAmount@1015 : Decimal;
      LastRegisterNo@1016 : Integer;
      Text000@1017 : TextConst 'ENU=Do you want to copy the general ledger budget "%1" to cost budget "%2"?;ESM=¨Desea copiar el ppto. contable "%1" en el ppto. costos "%2"?;FRC=Souhaitez-vous copier le budget grand livre ®ÿ%1ÿ¯ dans le budget des co–ts ®ÿ%2ÿ¯ÿ?;ENC=Do you want to copy the general ledger budget "%1" to cost budget "%2"?';
      Text002@1019 : TextConst 'ENU=Copying budget entries\No of entries #1#####\Copied        #2#####;ESM=Copiando movs. ppto.\N§ movs. #1#####\Copiados        #2#####;FRC=Copie ‚critures budget\Nombre d''‚critures #1#####\Copi‚        #2#####;ENC=Copying budget entries\No of entries #1#####\Copied        #2#####';
      Text003@1020 : TextConst '@@@="%2=budget name;%3=integer value";ENU=%1 entries generated in budget %2.\\%3 entries were skipped because there were either no corresponding G/L accounts defined or cost center and cost object were missing.\\Copy budget?;ESM=%1 movimientos generados en el presupuesto %2.\\Se omitieron %3 movs. porque no se definieron las cuentas correspondientes o faltaba el centro de costo o el objeto de costo.\\¨Desea copiar el presupuesto?;FRC=%1 ‚critures g‚n‚r‚es dans le budget %2.\\%3 ‚critures ont ‚t‚ ignor‚es car aucun compte GL correspondant n''a ‚t‚ d‚fini ou le centre de co–ts ou les objets de co–ts sont manquants.\\Copier le budgetÿ?;ENC=%1 entries generated in budget %2.\\%3 entries were skipped because there were either no corresponding G/L accounts defined or cost centre and cost object were missing.\\Copy budget?';
      Text004@1021 : TextConst 'ENU=Define name of source budget.;ESM=Defina el nombre del presupuesto de origen.;FRC=D‚finissez le nom du budget origine.;ENC=Define name of source budget.';
      Text005@1022 : TextConst 'ENU=Define name of target budget.;ESM=Defina el nombre del presupuesto de destino.;FRC=D‚finissez le nom du budget cible.;ENC=Define name of target budget.';
      Text006@1023 : TextConst 'ENU=No entries were copied. %1 entries were skipped because no corresponding general ledger accounts were defined or because cost center and cost object were missing.;ESM=No se ha copiado ning£n movimiento. Se omitieron %1 movs. porque no se definieron cuentas correspondientes o faltaba el centro de costo o el objeto de costo.;FRC=Aucune ‚criture n''a ‚t‚ copi‚e. %1 ‚critures ont ‚t‚ ignor‚es car aucun compte GL correspondant n''a ‚t‚ d‚fini ou le centre de co–ts ou les objets de co–ts sont manquants.;ENC=No entries were copied. %1 entries were skipped because no corresponding general ledger accounts were defined or because cost centre and cost object were missing.';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

