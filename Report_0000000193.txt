OBJECT Report 193 Issue Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10.00.36040;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Issue Finance Charge Memos;
               ESM=Emitir doc. interÇs;
               FRC=êmettre notes de frais financiers;
               ENC=Issue Finance Charge Memos];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 8733;    ;DataItem;                    ;
               DataItemTable=Table302;
               DataItemTableView=SORTING(No.);
               ReqFilterHeadingML=[ENU=Finance Charge Memo;
                                   ESM=Documento interÇs;
                                   FRC=Note de frais financiers;
                                   ENC=Finance Charge Memo];
               OnPreDataItem=BEGIN
                               IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                                 ERROR(Text000);
                               NoOfRecords := COUNT;
                               IF NoOfRecords = 1 THEN
                                 Window.OPEN(Text001)
                               ELSE BEGIN
                                 Window.OPEN(Text002);
                                 OldTime := TIME;
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  RecordNo := RecordNo + 1;
                                  CLEAR(FinChrgMemoIssue);
                                  FinChrgMemoIssue.Set("Finance Charge Memo Header",ReplacePostingDate,PostingDateReq);
                                  IF NoOfRecords = 1 THEN BEGIN
                                    FinChrgMemoIssue.RUN;
                                    MARK := FALSE;
                                  END ELSE BEGIN
                                    NewTime := TIME;
                                    IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                                      NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                                      IF NewProgress <> OldProgress THEN BEGIN
                                        Window.UPDATE(1,NewProgress * 100);
                                        OldProgress := NewProgress;
                                      END;
                                      OldTime := TIME;
                                    END;
                                    MARK := NOT FinChrgMemoIssue.RUN;
                                  END;

                                  IF PrintDoc AND NOT MARK THEN BEGIN
                                    FinChrgMemoIssue.GetIssuedFinChrgMemo(IssuedFinChrgMemoHeader);
                                    TempIssuedFinChrgMemoHeader := IssuedFinChrgMemoHeader;
                                    TempIssuedFinChrgMemoHeader.INSERT;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;
                                COMMIT;
                                IF PrintDoc THEN
                                  IF TempIssuedFinChrgMemoHeader.FINDSET THEN
                                    REPEAT
                                      TempIssuedFinChrgMemoHeader.PrintRecords(FALSE);
                                    UNTIL TempIssuedFinChrgMemoHeader.NEXT = 0;
                                MARKEDONLY := TRUE;
                                IF FIND('-') THEN
                                  IF CONFIRM(Text003,TRUE) THEN
                                    PAGE.RUNMODAL(0,"Finance Charge Memo Header");
                              END;

               ReqFilterFields=No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Print;
                             ESM=Imprimir;
                             FRC=Imprimer;
                             ENC=Print];
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             ESM=Reemplazar fecha reg.;
                             FRC=Remplacer la date de report;
                             ENC=Replace Posting Date];
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             ESM=Fecha registro;
                             FRC=Date de report;
                             ENC=Posting Date];
                  SourceExpr=PostingDateReq }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;ESM=Introduzca la fecha registro.;FRC=Entrez la date de report.;ENC=Please enter the posting date.';
      Text001@1001 : TextConst 'ENU=Issuing finance charge memo...;ESM=Emitiendo docs. interÇs...;FRC=êmettre notes de frais financiers...;ENC=Issuing finance charge memo...';
      Text002@1002 : TextConst 'ENU=Issuing finance charge memos @1@@@@@@@@@@@@@;ESM=Emitiendo docs. interÇs @1@@@@@@@@@@@@@;FRC=êmettre notes de frais financiers @1@@@@@@@@@@@@@;ENC=Issuing finance charge memos @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected finance charge memos.\Do you want to see these finance charge memos?;ESM=No se han podido emitir algunos de los documentos de interÇs seleccionados.\®Desea ver estos documentos de interÇs?;FRC=Il n''a pas ÇtÇ possible d''Çmettre certaines factures d''intÇràts sÇlectionnÇes.\Voulez-vous visualiser ces factures d''intÇràtˇ?;ENC=It was not possible to issue some of the selected finance charge memos.\Do you want to see these finance charge memos?';
      IssuedFinChrgMemoHeader@1005 : Record 304;
      TempIssuedFinChrgMemoHeader@1004 : TEMPORARY Record 304;
      FinChrgMemoIssue@1006 : Codeunit 395;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

