OBJECT Codeunit 1003 Job Task-Indent
{
  OBJECT-PROPERTIES
  {
    Date=02/13/07;
    Time=12:00:00 PM;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=1001;
    OnRun=BEGIN
            TESTFIELD("Job No.");
            IF NOT
               CONFIRM(
                 Text000 +
                 Text001 +
                 Text002 +
                 Text003,TRUE)
            THEN
              EXIT;
            JT := Rec;
            Indent("Job No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This function updates the indentation of all the Job Tasks.;ESM=Esta funci¢n actualiza la sangr¡a de todas las Tareas proyecto.;FRC=Cette fonction met … jour le d‚calage de toutes les tƒches du projet.;ENC=This function updates the indentation of all the Job Tasks.';
      Text001@1001 : TextConst 'ENU="All Job Tasks between a Begin-Total and the matching End-Total are indented one level. ";ESM="Se establece una sangr¡a de un nivel en las Tareas proyecto entre un Inicio Total y el Fin Total correspondiente. ";FRC="Toutes les tƒches du projet entre un total de d‚but et un total de fin correspondant sont d‚cal‚es d''un niveau. ";ENC="All Job Tasks between a Begin-Total and the matching End-Total are indented one level. "';
      Text002@1002 : TextConst 'ENU=The Totaling for each End-total is also updated.;ESM=Tambi‚n se actualiza el total de cada fin-total.;FRC=Le total pour chaque total-fin est aussi mis … jour.\\;ENC=The Totalling for each End-total is also updated.';
      Text003@1003 : TextConst 'ENU=\\Do you want to indent the Job Tasks?;ESM=\\¨Desea aplicar la sangr¡a a las Tareas proyecto?;FRC=\\Voulez-vous d‚caler les tƒches du projet?;ENC=\\Do you want to indent the Job Tasks?';
      Text004@1004 : TextConst 'ENU=Indenting the Job Tasks #1##########.;ESM=Sangr¡a a Tareas proy.  #1##########.;FRC=D‚calage des tƒches du projet #1##########.;ENC=Indenting the Job Tasks #1##########.';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;ESM=Fin-Tot %1 est  perdiendo un ajuste de inicio-tot.;FRC=Total-fin %1 n''a pas de Total-d‚but correspondant.;ENC=End-Total %1 is missing a matching Begin-Total.';
      JT@1006 : Record 1001;
      Window@1007 : Dialog;
      JTNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1(JobNo@1000 : Code[20]);
    BEGIN
      Window.OPEN(Text004);
      JT.SETRANGE("Job No.",JobNo);
      WITH JT DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"Job Task No.");

            IF "Job Task Type" = "Job Task Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "Job Task No.");
              Totaling := JTNo[i] + '..' + "Job Task No.";
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Job Task Type" = "Job Task Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              JTNo[i] := "Job Task No.";
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

