OBJECT Codeunit 3 G/L Account-Indent
{
  OBJECT-PROPERTIES
  {
    Date=09/15/04;
    Time=12:00:00 PM;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 Text000 +
                 Text001 +
                 Text002 +
                 Text003,TRUE)
            THEN
              EXIT;

            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="This function updates the indentation of all the G/L accounts in the chart of accounts. ";ESM="Esta funci¢n actualiza la indentaci¢n de todas las cuentas del cat logo de cuentas. ";FRC="Cette fonction met … jour l''indentation de tous les comptes de GL du plan comptable ";ENC="This function updates the indentation of all the G/L accounts in the chart of accounts. "';
      Text001@1001 : TextConst 'ENU="All accounts between a Begin-Total and the matching End-Total are indented one level. ";ESM="Se indentan un nivel todas las cuentas que est‚n entre los valores inicio-total y fin-total. ";FRC="Tous les comptes entre un total-d‚but et le total-fin correspondant sont indent‚s d''un niveau. ";ENC="All accounts between a Begin-Total and the matching End-Total are indented one level. "';
      Text002@1002 : TextConst 'ENU=The Totaling for each End-total is also updated.;ESM=Tambi‚n se actualiza el total de cada fin-total.;FRC=Le total pour chaque total-fin est aussi mis … jour.\\;ENC=The Totalling for each End-total is also updated.';
      Text003@1003 : TextConst 'ENU=\\Do you want to indent the chart of accounts?;ESM=\\¨Desea indentar el cat logo de cuentas?;FRC=\\Voulez-vous d‚caler le plan comptable?;ENC=\\Do you want to indent the chart of accounts?';
      Text004@1004 : TextConst 'ENU=Indenting the Chart of Accounts #1##########;ESM=Indentaci¢n del cat logo de cuentas #1##########;FRC=Margination du plan comptable #1##########;ENC=Indenting the Chart of Accounts #1##########';
      Text005@1005 : TextConst 'ENU=End-Total %1 is missing a matching Begin-Total.;ESM=Fin-Tot %1 est  perdiendo un ajuste de inicio-tot.;FRC=Total-fin %1 n''a pas de Total-d‚but correspondant.;ENC=End-Total %1 is missing a matching Begin-Total.';
      GLAcc@1006 : Record 15;
      Window@1007 : Dialog;
      AccNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1();
    BEGIN
      Window.OPEN(Text004);

      WITH GLAcc DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"No.");

            IF "Account Type" = "Account Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "No.");
              Totaling := AccNo[i] + '..' + "No.";
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Account Type" = "Account Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              AccNo[i] := "No.";
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    PROCEDURE RunICAccountIndent@4();
    BEGIN
      IF NOT
         CONFIRM(
           Text000 +
           Text001 +
           Text003,TRUE)
      THEN
        EXIT;

      IndentICAccount;
    END;

    PROCEDURE IndentICAccount@2();
    VAR
      ICGLAcc@1000 : Record 410;
    BEGIN
      Window.OPEN(Text004);
      WITH ICGLAcc DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"No.");

            IF "Account Type" = "Account Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "No.");
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Account Type" = "Account Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              AccNo[i] := "No.";
            END;
          UNTIL NEXT = 0;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

