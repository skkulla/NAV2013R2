OBJECT Report 5683 Create Sum of Digits Table
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Sum of Digits Table;
               ESM=Crear suma de tabla de d¡gitos;
               FRC=Cr‚er table somme des chiffres;
               ENC=Create Sum of Digits Table];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NoOfYears < 1 THEN
                    ERROR(Text000);
                  IF NoOfYears >= 200 THEN
                    ERROR(Text001);

                  CreateDeprTableLines(DeprTableCode,NoOfYears);
                END;

  }
  DATASET
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=NoOfYears;
                  CaptionML=[ENU=No. of Years;
                             ESM=N§ a¤os;
                             FRC=Nbre ann‚es;
                             ENC=No. of Years];
                  BlankZero=Yes;
                  SourceExpr=NoOfYears;
                  MinValue=0 }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      NoOfYears@1000 : Integer;
      DeprTableCode@1001 : Code[20];
      Text000@1002 : TextConst 'ENU=You must specify No. of Years.;ESM=Debe especificar N§ a¤os;FRC=Vous devez sp‚cifier le nombre d''ann‚es.;ENC=You must specify No. of Years.';
      Text001@1003 : TextConst 'ENU=No. of Years. must be less than 200.;ESM=N§ a¤os debe ser inferior a 200.;FRC=Le nombre d''ann‚es doit ˆtre inf‚rieur … 200.;ENC=No. of Years. must be less than 200.';

    PROCEDURE SetTableCode@1(DeprTableCode2@1000 : Code[20]);
    BEGIN
      DeprTableCode := DeprTableCode2;
    END;

    PROCEDURE CreateDeprTableLines@2(DeprTableCode@1000 : Code[10];NoOfYears@1001 : Integer);
    VAR
      DeprTableHeader@1002 : Record 5642;
      DeprTableLine@1003 : Record 5643;
      TotalDigitSum@1004 : Integer;
      TotalPercentSum@1007 : Decimal;
      CurrentDigit@1005 : Integer;
      CurrentDigitSum@1006 : Integer;
    BEGIN
      DeprTableHeader.GET(DeprTableCode);
      DeprTableHeader."Period Length" := DeprTableHeader."Period Length"::Year;
      DeprTableHeader."Total No. of Units" := 0;
      DeprTableLine.SETRANGE("Depreciation Table Code",DeprTableCode);
      DeprTableLine.DELETEALL;
      DeprTableHeader.MODIFY;

      CLEAR(DeprTableLine);
      DeprTableLine."Depreciation Table Code" := DeprTableCode;
      DeprTableLine."Period No." := 0;
      TotalDigitSum := 0;
      CurrentDigitSum := 0;
      TotalPercentSum := 0;
      FOR CurrentDigit := 1 TO NoOfYears DO
        TotalDigitSum := TotalDigitSum + CurrentDigit;
      FOR CurrentDigit := NoOfYears DOWNTO 1 DO BEGIN
        DeprTableLine."Period Depreciation %" := ROUND((CurrentDigit + CurrentDigitSum) / TotalDigitSum * 100,0.00000001) -
          TotalPercentSum;
        CurrentDigitSum := CurrentDigitSum + CurrentDigit;
        TotalPercentSum := TotalPercentSum + DeprTableLine."Period Depreciation %";
        DeprTableLine."Period No." := DeprTableLine."Period No." + 1;
        DeprTableLine.INSERT;
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

