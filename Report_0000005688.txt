OBJECT Report 5688 Cancel FA Ledger Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Cancel FA Ledger Entries;
               ESM=A/F Anular movs.;
               FRC=Annuler les ‚critures de grand livre d'immobilisation;
               ENC=Cancel FA Ledger Entries];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF (EndingDate > 0D) AND (StartingDate > EndingDate) THEN
                    ERROR(Text000);
                  IF UseNewPostingDate THEN
                    IF NewPostingDate = 0D THEN
                      ERROR(Text002);
                  IF NOT UseNewPostingDate THEN
                    IF NewPostingDate > 0D THEN
                      ERROR(Text003);
                  IF NewPostingDate > 0D THEN
                    IF NORMALDATE(NewPostingDate) <> NewPostingDate THEN
                      ERROR(Text004);

                  IF EndingDate = 0D THEN
                    EndingDate2 := 12319999D
                  ELSE
                    EndingDate2 := EndingDate;
                  DeprBook.GET(DeprBookCode);
                  IF UseNewPostingDate THEN
                    DeprBook.TESTFIELD("Use Same FA+G/L Posting Dates",FALSE);
                  DeprBook.IndexGLIntegration(GLIntegration);
                  FirstGenJnl := TRUE;
                  FirstFAJnl := TRUE;
                  Window.OPEN(Text001);
                END;

  }
  DATASET
  {
    { 3794;    ;DataItem;                    ;
               DataItemTable=Table5600;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               DepreciationCalc.SetFAFilter(FALedgEntry,'',DeprBookCode,FALSE);
                               WITH FALedgEntry DO BEGIN
                                 SETRANGE("FA Posting Category","FA Posting Category"::" ");
                                 SETRANGE(
                                   "FA Posting Type",
                                   "FA Posting Type"::"Acquisition Cost","FA Posting Type"::"Salvage Value");
                                 SETRANGE("FA Posting Date",StartingDate,EndingDate2);
                               END;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                  IF Inactive OR Blocked THEN
                                    CurrReport.SKIP;
                                  IF NOT FADeprBook.GET("No.",DeprBookCode) THEN
                                    CurrReport.SKIP;
                                  FALedgEntry.SETRANGE("FA No.","No.");
                                  IF FALedgEntry.FIND('+') THEN
                                    REPEAT
                                      SetJournalType(FALedgEntry);
                                      IF NewPostingDate > 0D THEN
                                        FALedgEntry."Posting Date" := NewPostingDate;
                                      CASE JournalType OF
                                        JournalType::SkipType:
                                          ;
                                        JournalType::GenJnlType:
                                          InsertGenJnlLine(FALedgEntry);
                                        JournalType::FAJnlType:
                                          InsertFAJnlLine(FALedgEntry);
                                      END;
                                    UNTIL FALedgEntry.NEXT(-1) = 0;
                                END;

               ReqFilterFields=No.,FA Class Code,FA Subclass Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF DeprBookCode = '' THEN BEGIN
                     FASetup.GET;
                     DeprBookCode := FASetup."Default Depr. Book";
                   END;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 1   ;2   ;Field     ;
                  Name=CancelBook;
                  CaptionML=[ENU=Cancel Book;
                             ESM=Anular libro;
                             FRC=Annuler livre;
                             ENC=Cancel Book];
                  SourceExpr=DeprBookCode;
                  TableRelation="Depreciation Book" }

      { 20  ;2   ;Field     ;
                  CaptionML=[ENU=Starting Date;
                             ESM=Fecha inicial;
                             FRC=Date d‚but;
                             ENC=Starting Date];
                  SourceExpr=StartingDate }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Ending Date;
                             ESM=Fecha final;
                             FRC=Date fin;
                             ENC=Ending Date];
                  SourceExpr=EndingDate }

      { 23  ;2   ;Field     ;
                  CaptionML=[ENU=Use New Posting Date;
                             ESM=Usar nueva fecha registro;
                             FRC=Utiliser nouvelle date compta.;
                             ENC=Use New Posting Date];
                  SourceExpr=UseNewPostingDate }

      { 19  ;2   ;Field     ;
                  CaptionML=[ENU=New Posting Date;
                             ESM=Nueva fecha registro;
                             FRC=Nouvelle date compta.;
                             ENC=New Posting Date];
                  SourceExpr=NewPostingDate }

      { 16  ;2   ;Field     ;
                  CaptionML=[ENU=Document No.;
                             ESM=N§ documento;
                             FRC=Nø de document;
                             ENC=Document No.];
                  SourceExpr=DocumentNo }

      { 13  ;2   ;Field     ;
                  CaptionML=[ENU=Posting Description;
                             ESM=Texto de registro;
                             FRC=Description du report;
                             ENC=Posting Description];
                  SourceExpr=PostingDescription }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Insert Bal. Account;
                             ESM=Insertar contrapartida;
                             FRC=Ins‚rer compte contr“le;
                             ENC=Insert Bal. Account];
                  SourceExpr=BalAccount }

      { 1904404401;2;Group  ;
                  CaptionML=[ENU=Cancel;
                             ESM=Cancelar;
                             FRC=Annuler;
                             ENC=Cancel] }

      { 8   ;3   ;Field     ;
                  CaptionML=[ENU=Acquisition Cost;
                             ESM=Costo;
                             FRC=Co–t acquisition;
                             ENC=Acquisition Cost];
                  SourceExpr=CancelChoices[1] }

      { 9   ;3   ;Field     ;
                  CaptionML=[ENU=Depreciation;
                             ESM=Amortizaci¢n;
                             FRC=Amortissement;
                             ENC=Depreciation];
                  SourceExpr=CancelChoices[2] }

      { 10  ;3   ;Field     ;
                  CaptionML=[ENU=Write-Down;
                             ESM=Depreciaci¢n;
                             FRC=D‚valuation;
                             ENC=Write-Down];
                  SourceExpr=CancelChoices[3] }

      { 11  ;3   ;Field     ;
                  CaptionML=[ENU=Appreciation;
                             ESM=Apreciaci¢n;
                             FRC=Appr‚ciation;
                             ENC=Appreciation];
                  SourceExpr=CancelChoices[4] }

      { 12  ;3   ;Field     ;
                  CaptionML=[ENU=Custom 1;
                             ESM=Especial;
                             FRC=ParamŠtre 1;
                             ENC=Custom 1];
                  SourceExpr=CancelChoices[5] }

      { 17  ;3   ;Field     ;
                  CaptionML=[ENU=Custom 2;
                             ESM=Provisi¢n;
                             FRC=ParamŠtre 2;
                             ENC=Custom 2];
                  SourceExpr=CancelChoices[6] }

      { 18  ;3   ;Field     ;
                  CaptionML=[ENU=Salvage Value;
                             ESM=Valor residual;
                             FRC=Valeur r‚siduelle;
                             ENC=Salvage Value];
                  SourceExpr=CancelChoices[9] }

      { 5   ;3   ;Field     ;
                  Name=Disposal;
                  CaptionML=[ENU=Disposal;
                             ESM=Venta/Baja;
                             FRC=Cession;
                             ENC=Disposal];
                  SourceExpr=CancelChoices[7] }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The Starting Date is later than the Ending Date.;ESM=La fecha inicial es posterior a la fecha final.;FRC=La date de d‚but est ult‚rieure … la date de fin.;ENC=The Starting Date is later than the Ending Date.';
      Text001@1001 : TextConst 'ENU=Canceling fixed asset    #1##########;ESM=Cancelando activo        #1##########;FRC=Annuler immobilisations   #1##########;ENC=Cancelling fixed asset    #1##########';
      GenJnlLine@1002 : Record 81;
      FASetup@1003 : Record 5603;
      FAJnlLine@1004 : Record 5621;
      FADeprBook@1005 : Record 5612;
      DeprBook@1006 : Record 5611;
      FALedgEntry@1007 : Record 5601;
      FAJnlSetup@1008 : Record 5605;
      DepreciationCalc@1009 : Codeunit 5616;
      Window@1011 : Dialog;
      CancelChoices@1012 : ARRAY [9] OF Boolean;
      GLIntegration@1013 : ARRAY [9] OF Boolean;
      DocumentNo@1014 : Code[20];
      DocumentNo2@1015 : Code[20];
      DocumentNo3@1016 : Code[20];
      NoSeries2@1017 : Code[10];
      NoSeries3@1018 : Code[10];
      PostingDescription@1019 : Text[50];
      JournalType@1020 : 'SkipType,GenJnlType,FAJnlType';
      DeprBookCode@1021 : Code[10];
      BalAccount@1022 : Boolean;
      StartingDate@1023 : Date;
      EndingDate@1024 : Date;
      EndingDate2@1025 : Date;
      FirstGenJnl@1026 : Boolean;
      FirstFAJnl@1027 : Boolean;
      FAJnlNextLineNo@1028 : Integer;
      GenJnlNextLineNo@1029 : Integer;
      Text002@1032 : TextConst 'ENU=You must specify New Posting Date.;ESM=Debe especificar nueva fecha registro.;FRC=Vous devez remplir le champ Nouvelle date compta.;ENC=You must specify New Posting Date.';
      Text003@1031 : TextConst 'ENU=You must not specify New Posting Date.;ESM=No debe especificar nueva fecha registro.;FRC=Vous ne devez pas remplir le champ Nouvelle date compta.;ENC=You must not specify New Posting Date.';
      Text004@1030 : TextConst 'ENU=You must not specify a closing date.;ESM=No debe especificar una fecha de cierre.;FRC=Vous ne devez pas sp‚cifier de date de fermeture.;ENC=You must not specify a closing date.';
      UseNewPostingDate@1034 : Boolean;
      NewPostingDate@1033 : Date;

    LOCAL PROCEDURE InsertGenJnlLine@2(VAR FALedgEntry@1000 : Record 5601);
    VAR
      FAInsertGLAcc@1001 : Codeunit 5601;
    BEGIN
      IF FirstGenJnl THEN BEGIN
        GenJnlLine.LOCKTABLE;
        FAJnlSetup.GenJnlName(DeprBook,GenJnlLine,GenJnlNextLineNo);
        NoSeries2 := FAJnlSetup.GetGenNoSeries(GenJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo2 := FAJnlSetup.GetGenJnlDocumentNo(GenJnlLine,GetPostingDate(FALedgEntry."FA Posting Date"),TRUE)
        ELSE
          DocumentNo2 := DocumentNo;
      END;
      FirstGenJnl := FALSE;

      WITH GenJnlLine DO BEGIN
        FALedgEntry.MoveToGenJnl(GenJnlLine);
        "Shortcut Dimension 1 Code" := FALedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := FALedgEntry."Global Dimension 2 Code";
        "Dimension Set ID" := FALedgEntry."Dimension Set ID";
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,-Amount);
        "FA Error Entry No." := FALedgEntry."Entry No.";
        VALIDATE(Correction,DeprBook."Mark Errors as Corrections");
        "Document No." := DocumentNo2;
        "Posting No. Series" := NoSeries2;
        "Document Type" := "Document Type"::" ";
        "External Document No." := '';
        IF PostingDescription <> '' THEN
          Description := PostingDescription;
        GenJnlNextLineNo := GenJnlNextLineNo + 10000;
        "Line No." := GenJnlNextLineNo;
        INSERT(TRUE);
        IF BalAccount THEN BEGIN
          FAInsertGLAcc.GetBalAcc(GenJnlLine);
          IF FIND('+') THEN ;
          GenJnlNextLineNo := "Line No.";
        END;
      END;
    END;

    LOCAL PROCEDURE InsertFAJnlLine@3(VAR FALedgEntry@1000 : Record 5601);
    BEGIN
      IF FirstFAJnl THEN BEGIN
        FAJnlLine.LOCKTABLE;
        FAJnlSetup.FAJnlName(DeprBook,FAJnlLine,FAJnlNextLineNo);
        NoSeries3 := FAJnlSetup.GetFANoSeries(FAJnlLine);
        IF DocumentNo = '' THEN
          DocumentNo3 := FAJnlSetup.GetFAJnlDocumentNo(FAJnlLine,GetPostingDate(FALedgEntry."FA Posting Date"),TRUE)
        ELSE
          DocumentNo3 := DocumentNo;
      END;
      FirstFAJnl := FALSE;

      WITH FAJnlLine DO BEGIN
        FALedgEntry.MoveToFAJnl(FAJnlLine);
        "Shortcut Dimension 1 Code" := FALedgEntry."Global Dimension 1 Code";
        "Shortcut Dimension 2 Code" := FALedgEntry."Global Dimension 2 Code";
        "Dimension Set ID" := FALedgEntry."Dimension Set ID";
        VALIDATE("Depreciation Book Code",DeprBookCode);
        VALIDATE(Amount,-Amount);
        "FA Error Entry No." := FALedgEntry."Entry No.";
        VALIDATE(Correction,DeprBook."Mark Errors as Corrections");
        "Document No." := DocumentNo3;
        "Posting No. Series" := NoSeries3;
        "Document Type" := "Document Type"::" ";
        "External Document No." := '';
        IF PostingDescription <> '' THEN
          Description := PostingDescription;
        FAJnlNextLineNo := FAJnlNextLineNo + 10000;
        "Line No." := FAJnlNextLineNo;
        INSERT(TRUE);
      END;
    END;

    LOCAL PROCEDURE SetJournalType@10(VAR FALedgEntry@1000 : Record 5601);
    VAR
      Index@1001 : Integer;
    BEGIN
      Index := FALedgEntry.ConvertPostingType + 1;
      IF CancelChoices[Index] THEN BEGIN
        IF GLIntegration[Index] AND NOT "Fixed Asset"."Budgeted Asset" THEN
          JournalType := JournalType::GenJnlType
        ELSE
          JournalType := JournalType::FAJnlType
      END ELSE
        JournalType := JournalType::SkipType;
    END;

    PROCEDURE InitializeRequest@1(DeprBookCodeFrom@1000 : Code[10];StartingDateFrom@1001 : Date;EndingDateFrom@1002 : Date;UseNewPostingDateFrom@1003 : Boolean;NewPostingDateFrom@1004 : Date;DocumentNoFrom@1005 : Code[20];PostingDescriptionFrom@1006 : Text[50];BalAccountFrom@1007 : Boolean);
    BEGIN
      DeprBookCode := DeprBookCodeFrom;
      StartingDate := StartingDateFrom;
      EndingDate := EndingDateFrom;
      UseNewPostingDate := UseNewPostingDateFrom;
      NewPostingDate := NewPostingDateFrom;
      DocumentNo := DocumentNoFrom;
      PostingDescription := PostingDescriptionFrom;
      BalAccount := BalAccountFrom;
    END;

    PROCEDURE SetCancelDepreciation@4(Choice@1000 : Boolean);
    BEGIN
      CancelChoices[2] := Choice;
    END;

    PROCEDURE SetCancelAcquisitionCost@5(Choice@1000 : Boolean);
    BEGIN
      CancelChoices[1] := Choice;
    END;

    LOCAL PROCEDURE GetPostingDate@6(FAPostingDate@1000 : Date) : Date;
    BEGIN
      IF NewPostingDate <> 0D THEN
        EXIT(NewPostingDate);
      EXIT(FAPostingDate);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

