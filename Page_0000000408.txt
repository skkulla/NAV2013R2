OBJECT Page 408 G/L Balance by Dimension
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=G/L Balance by Dimension;
               ESM=Saldo cuenta por dimensi¢n;
               FRC=Solde de grand livre par dimension;
               ENC=G/L Balance by Dimension];
    SaveValues=Yes;
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    LinksAllowed=No;
    SourceTable=Table367;
    PageType=Card;
    OnInit=VAR
             Field@1000 : Record 2000000041;
           BEGIN
             Dim2FilterEnable := TRUE;
             Dim1FilterEnable := TRUE;
             Field.GET(DATABASE::"G/L Account",42);
             BusUnitFilterCaption := Field."Field Caption";
           END;

    OnOpenPage=VAR
                 MATRIX_Step@1000 : 'Initial,Previous,Same,Next';
               BEGIN
                 GlobalDim1Filter := GLAcc.GETFILTER("Global Dimension 1 Filter");
                 GlobalDim2Filter := GLAcc.GETFILTER("Global Dimension 2 Filter");

                 GLSetup.GET;
                 Dim1FilterEnable :=
                   (GLSetup."Global Dimension 1 Code" <> '') AND
                   (GLAcc.GETFILTER("Global Dimension 1 Filter") = '');
                 Dim2FilterEnable :=
                   (GLSetup."Global Dimension 2 Code" <> '') AND
                   (GLAcc.GETFILTER("Global Dimension 2 Filter") = '');

                 IF GLSetup."Additional Reporting Currency" = '' THEN
                   ShowInAddCurr := FALSE;

                 IF (LineDimCode = '') AND (ColumnDimCode = '') THEN BEGIN
                   LineDimCode := GLAcc.TABLECAPTION;
                   ColumnDimCode := Text001;
                 END;
                 LineDimOption := DimCodeToOption(LineDimCode);
                 ColumnDimOption := DimCodeToOption(ColumnDimCode);

                 FindPeriod('');

                 MATRIX_NoOfColumns := 32;
                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Initial);
               END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 8       ;1   ;ActionGroup;
                      CaptionML=[ENU=&Actions;
                                 ESM=&Acciones;
                                 FRC=&Actions;
                                 ENC=&Actions];
                      Image=Action }
      { 13      ;2   ;Action    ;
                      CaptionML=[ENU=Reverse Lines and Columns;
                                 ESM=Invertir l°neas y columnas;
                                 FRC=Inverser lignes et colonnes;
                                 ENC=Reverse Lines and Columns];
                      Image=Undo;
                      OnAction=VAR
                                 TempDimCode@1001 : Text[30];
                                 MATRIX_Step@1002 : 'First,Previous,Next';
                               BEGIN
                                 TempDimCode := ColumnDimCode;
                                 ColumnDimCode := LineDimCode;
                                 LineDimCode := TempDimCode;
                                 ValidateLineDimCode;
                                 ValidateColumnDimCode;
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 119     ;1   ;Action    ;
                      Name=ShowMatrix;
                      CaptionML=[ENU=&Show Matrix;
                                 ESM=Mo&strar matriz;
                                 FRC=&Afficher matrice;
                                 ENC=&Show Matrix];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ShowMatrix;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MatrixForm@1284 : Page 9233;
                               BEGIN
                                 CLEAR(MatrixForm);
                                 MatrixForm.Load(
                                   LineDimCode,ColumnDimCode,PeriodType,DateFilter,GLAccFilter,BusUnitFilter,
                                   BudgetFilter,GlobalDim1Filter,GlobalDim2Filter,
                                   ShowActualBudg,AmountField,ClosingEntryFilter,RoundingFactor,ShowInAddCurr,
                                   MATRIX_ColumnCaptions,MATRIX_PrimaryKeyFirstColInSet,
                                   AmountType,MATRIX_CurrSetLength);
                                 MatrixForm.RUNMODAL;
                               END;
                                }
      { 18      ;1   ;Action    ;
                      CaptionML=[ENU=Next Set;
                                 ESM=Conjunto siguiente;
                                 FRC=Jeu suivant;
                                 ENC=Next Set];
                      ToolTipML=[ENU=Next Set;
                                 ESM=Conjunto siguiente;
                                 FRC=Jeu suivant;
                                 ENC=Next Set];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=NextSet;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MATRIX_Step@1001 : 'Initial,Previous,Same,Next';
                               BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Next);
                               END;
                                }
      { 14      ;1   ;Action    ;
                      CaptionML=[ENU=Previous Set;
                                 ESM=Conjunto anterior;
                                 FRC=Jeu prÇcÇdent;
                                 ENC=Previous Set];
                      ToolTipML=[ENU=Previous Set;
                                 ESM=Conjunto anterior;
                                 FRC=Jeu prÇcÇdent;
                                 ENC=Previous Set];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PreviousSet;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 MATRIX_Step@1001 : 'Initial,Previous,Same,Next';
                               BEGIN
                                 MATRIX_GenerateColumnCaptions(MATRIX_Step::Previous);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[ENU=General;
                           ESM=General;
                           FRC=GÇnÇral;
                           ENC=General] }

    { 6   ;2   ;Field     ;
                CaptionML=[ENU=Show as Lines;
                           ESM=Muestra como l°neas;
                           FRC=Afficher lignes;
                           ENC=Show as Lines];
                SourceExpr=LineDimCode;
                OnValidate=BEGIN
                             IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                               ColumnDimCode := '';
                               ValidateColumnDimCode;
                             END;
                             DimCodAmtBuf.DELETEALL;
                             ValidateLineDimCode;
                             LineDimCodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           NewCode@1002 : Text[30];
                         BEGIN
                           NewCode := GetDimSelection(LineDimCode);
                           IF NewCode = LineDimCode THEN
                             EXIT(FALSE);

                           Text := NewCode;
                           LineDimCode := NewCode;
                           ValidateLineDimCode;
                           CurrPage.UPDATE;
                           EXIT(TRUE);
                         END;
                          }

    { 9   ;2   ;Field     ;
                CaptionML=[ENU=Show as Columns;
                           ESM=Muestra como columnas;
                           FRC=Afficher colonnes;
                           ENC=Show as Columns];
                SourceExpr=ColumnDimCode;
                OnValidate=BEGIN
                             IF (UPPERCASE(LineDimCode) = UPPERCASE(ColumnDimCode)) AND (LineDimCode <> '') THEN BEGIN
                               LineDimCode := '';
                               ValidateLineDimCode;
                             END;
                             DimCodAmtBuf.DELETEALL;
                             ValidateColumnDimCode;
                             ColumnDimCodeOnAfterValidate;
                           END;

                OnLookup=VAR
                           NewCode@1002 : Text[30];
                         BEGIN
                           NewCode := GetDimSelection(ColumnDimCode);
                           IF NewCode = ColumnDimCode THEN
                             EXIT(FALSE);

                           Text := NewCode;
                           ColumnDimCode := NewCode;
                           ValidateColumnDimCode;
                           ColumnDimCodeOnAfterValidate;
                           EXIT(TRUE);
                         END;
                          }

    { 1907524401;1;Group  ;
                CaptionML=[ENU=Filters;
                           ESM=Filtros;
                           FRC=Filtres;
                           ENC=Filters] }

    { 36  ;2   ;Field     ;
                CaptionML=[ENU=Date Filter;
                           ESM=Filtro fecha;
                           FRC=Filtre date;
                           ENC=Date Filter];
                SourceExpr=DateFilter;
                OnValidate=VAR
                             ApplicationManagement@1001 : Codeunit 1;
                             Date1@1002 : Date;
                           BEGIN
                             IF ApplicationManagement.MakeDateFilter(DateFilter) = 0 THEN;
                             IF DateFilter <> '' THEN
                               IF EVALUATE(Date1,DateFilter) THEN
                                 IF Date1 <> NORMALDATE(Date1) THEN
                                   DateFilter := STRSUBSTNO('%1..%2',NORMALDATE(Date1),Date1);
                             GLAcc.SETFILTER("Date Filter",DateFilter);
                             DateFilter := GLAcc.GETFILTER("Date Filter");
                             InternalDateFilter := DateFilter;
                             DateFilterOnAfterValidate;
                           END;
                            }

    { 38  ;2   ;Field     ;
                CaptionML=[ENU=G/L Account Filter;
                           ESM=Filtro cuenta;
                           FRC=Filtre compte du grand livre;
                           ENC=G/L Account Filter];
                SourceExpr=GLAccFilter;
                OnValidate=BEGIN
                             GLAccFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           GLAccList@1002 : Page 18;
                         BEGIN
                           GLAccList.LOOKUPMODE(TRUE);
                           IF NOT (GLAccList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE);

                           Text := GLAccList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;
                          }

    { 53  ;2   ;Field     ;
                CaptionML=[ENU=Budget Filter;
                           ESM=Filtro presupuesto;
                           FRC=Filtre budget;
                           ENC=Budget Filter];
                SourceExpr=BudgetFilter;
                TableRelation="G/L Budget Name".Name;
                LookupPageID=G/L Budget Names;
                OnValidate=BEGIN
                             BudgetFilterOnAfterValidate;
                           END;
                            }

    { 16  ;2   ;Field     ;
                SourceExpr=BusUnitFilter;
                CaptionClass='3,' + BusUnitFilterCaption;
                OnValidate=BEGIN
                             BusUnitFilterOnAfterValidate;
                           END;

                OnLookup=VAR
                           BusUnitList@1002 : Page 240;
                         BEGIN
                           BusUnitList.LOOKUPMODE(TRUE);
                           IF NOT (BusUnitList.RUNMODAL = ACTION::LookupOK) THEN
                             EXIT(FALSE);
                           Text := BusUnitList.GetSelectionFilter;
                           EXIT(TRUE);
                         END;
                          }

    { 34  ;2   ;Field     ;
                Name=Dim1Filter;
                SourceExpr=GlobalDim1Filter;
                CaptionClass='1,3,1';
                Enabled=Dim1FilterEnable;
                OnValidate=BEGIN
                             GlobalDim1FilterOnAfterValidat;
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimFilter(GLSetup."Global Dimension 1 Code",Text));
                         END;
                          }

    { 30  ;2   ;Field     ;
                Name=Dim2Filter;
                SourceExpr=GlobalDim2Filter;
                CaptionClass='1,3,2';
                Enabled=Dim2FilterEnable;
                OnValidate=BEGIN
                             GlobalDim2FilterOnAfterValidat;
                           END;

                OnLookup=BEGIN
                           EXIT(LookUpDimFilter(GLSetup."Global Dimension 2 Code",Text));
                         END;
                          }

    { 1903121901;1;Group  ;
                CaptionML=[ENU=Options;
                           ESM=Opciones;
                           FRC=Options;
                           ENC=Options] }

    { 29  ;2   ;Field     ;
                CaptionML=[ENU=Show;
                           ESM=Muestra;
                           FRC=Afficher;
                           ENC=Show];
                OptionCaptionML=[ENU=Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%;
                                 ESM=Importes reales,Importes pptados.,Desviaci¢n,% Desviaci¢n,% ÷ndice;
                                 FRC=RÇalisÇ,BudgÇtÇ,Variance,% Çcart,% indice;
                                 ENC=Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%];
                SourceExpr=ShowActualBudg;
                OnValidate=BEGIN
                             DimCodAmtBuf.DELETEALL;

                             // Amount := CalcAmount(FALSE);
                           END;
                            }

    { 47  ;2   ;Field     ;
                CaptionML=[ENU=Show Amount Field;
                           ESM=Muestra Importe;
                           FRC=Afficher champ montant;
                           ENC=Show Amount Field];
                OptionCaptionML=[ENU=Amount,Debit Amount,Credit Amount;
                                 ESM=Importe,Importe deudor,Importe acreedor;
                                 FRC=Montant,Montant de dÇbit,Montant de crÇdit;
                                 ENC=Amount,Debit Amount,Credit Amount];
                SourceExpr=AmountField;
                OnValidate=BEGIN
                             DimCodAmtBuf.DELETEALL;
                             // Amount := CalcAmount(FALSE);
                           END;
                            }

    { 20  ;2   ;Field     ;
                CaptionML=[ENU=Closing Entries;
                           ESM=Movs. regularizaci¢n;
                           FRC=êcritures de fermeture;
                           ENC=Closing Entries];
                OptionCaptionML=[ENU=Include,Exclude;
                                 ESM=Incluir,Excluir;
                                 FRC=Inclure,Exclure;
                                 ENC=Include,Exclude];
                SourceExpr=ClosingEntryFilter;
                OnValidate=BEGIN
                             DimCodAmtBuf.DELETEALL;
                             FindPeriod('=');
                             // Amount := CalcAmount(FALSE);
                           END;
                            }

    { 19  ;2   ;Field     ;
                CaptionML=[ENU=Rounding Factor;
                           ESM=Factor redondeo;
                           FRC=Facteur d'arrondissement;
                           ENC=Rounding Factor];
                OptionCaptionML=[ENU=None,1,1000,1000000;
                                 ESM=No,1,1000,1000000;
                                 FRC=Aucun,1,1000,1000000;
                                 ENC=None,1,1000,1000000];
                SourceExpr=RoundingFactor }

    { 49  ;2   ;Field     ;
                CaptionML=[ENU=Show Amounts in Add. Reporting Currency;
                           ESM=Muestra importes en div.-adic.;
                           FRC=Afficher les montants dans la devise de report additionnelle;
                           ENC=Show Amounts in Add. Reporting Currency];
                SourceExpr=ShowInAddCurr;
                MultiLine=Yes;
                OnValidate=BEGIN
                             DimCodAmtBuf.DELETEALL;
                             // Amount := CalcAmount(FALSE);
                           END;
                            }

    { 4   ;2   ;Field     ;
                CaptionML=[ENU=Show Column Name;
                           ESM=Muestra nombre columna;
                           FRC=Afficher nom colonne;
                           ENC=Show Column Name];
                SourceExpr=ShowColumnName;
                OnValidate=BEGIN
                             ShowColumnNameOnAfterValidate;
                           END;
                            }

    { 1906098301;1;Group  ;
                CaptionML=[ENU=Matrix Options;
                           ESM=Opciones matriz;
                           FRC=Options de matrice;
                           ENC=Matrix Options] }

    { 26  ;2   ;Field     ;
                CaptionML=[ENU=View by;
                           ESM=Ver por;
                           FRC=Afficher par;
                           ENC=View by];
                OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period;
                                 ESM=D°a,Semana,Mes,Trimestre,A§o,Periodo contable;
                                 FRC=Jour,Semaine,Mois,Trimestre,AnnÇe,PÇriode comptable;
                                 ENC=Day,Week,Month,Quarter,Year,Accounting Period];
                SourceExpr=PeriodType;
                OnValidate=BEGIN
                             FindPeriod('');
                             PeriodTypeOnAfterValidate;
                           END;
                            }

    { 11  ;2   ;Field     ;
                CaptionML=[ENU=View as;
                           ESM=Ver como;
                           FRC=Afficher en tant que;
                           ENC=View as];
                OptionCaptionML=[ENU=Net Change,Balance at Date;
                                 ESM=Saldo periodo,Saldo a la fecha;
                                 FRC=Variation nette,Solde en date du;
                                 ENC=Net Change,Balance at Date];
                SourceExpr=AmountType;
                OnValidate=BEGIN
                             FindPeriod('');
                             AmountTypeOnAfterValidate;
                           END;
                            }

    { 22  ;2   ;Field     ;
                CaptionML=[ENU=Column Set;
                           ESM=Conjunto de columnas;
                           FRC=Jeu de colonnes;
                           ENC=Column Set];
                SourceExpr=MATRIX_ColumnSet;
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Period;ESM=Periodo;FRC=PÇriode;ENC=Period';
      Text002@1002 : TextConst 'ENU=%1 is not a valid line definition.;ESM=%1 no es una def. de l°nea v†lida.;FRC=%1 n''est pas une dÇfinition de ligne correcte.;ENC=%1 is not a valid line definition.';
      Text003@1003 : TextConst 'ENU=%1 is not a valid column definition.;ESM=%1 no es una def. de columna v†lida.;FRC=%1 n''est pas une dÇfinition de colonne correcte.;ENC=%1 is not a valid column definition.';
      GLSetup@1004 : Record 98;
      GLAcc@1005 : Record 15;
      BusUnit@1038 : Record 220;
      DimCodAmtBuf@1006 : TEMPORARY Record 375;
      LineDimOption@1008 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4';
      ColumnDimOption@1009 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2,Dimension 3,Dimension 4';
      LineDimCode@1010 : Text[30];
      ColumnDimCode@1011 : Text[30];
      PeriodType@1012 : 'Day,Week,Month,Quarter,Year,Accounting Period';
      AmountType@1013 : 'Net Change,Balance at Date';
      RoundingFactor@1014 : 'None,1,1000,1000000';
      AmountField@1015 : 'Amount,Debit Amount,Credit Amount';
      ShowActualBudg@1016 : 'Actual Amounts,Budgeted Amounts,Variance,Variance%,Index%';
      ShowInAddCurr@1017 : Boolean;
      ClosingEntryFilter@1018 : 'Include,Exclude';
      ShowColumnName@1019 : Boolean;
      DateFilter@1020 : Text[1024];
      InternalDateFilter@1021 : Text[1024];
      GLAccFilter@1023 : Code[250];
      BudgetFilter@1024 : Code[250];
      BusUnitFilter@1025 : Code[250];
      GlobalDim1Filter@1026 : Code[250];
      GlobalDim2Filter@1027 : Code[250];
      BusUnitFilterCaption@1000 : Text[80];
      PeriodInitialized@1030 : Boolean;
      MATRIX_ColumnCaptions@1032 : ARRAY [32] OF Text[1024];
      MATRIX_NoOfColumns@1033 : Integer;
      MATRIX_ColumnSet@1035 : Text[1024];
      MATRIX_PrimaryKeyFirstColInSet@1034 : Text[1024];
      MATRIX_CurrSetLength@1037 : Integer;
      Dim1FilterEnable@19054192 : Boolean INDATASET;
      Dim2FilterEnable@19062512 : Boolean INDATASET;

    LOCAL PROCEDURE DimCodeToOption@21(DimCode@1000 : Text[30]) : Integer;
    VAR
      BusUnit@1001 : Record 220;
    BEGIN
      CASE DimCode OF
        '':
          EXIT(-1);
        GLAcc.TABLECAPTION:
          EXIT(0);
        Text001:
          EXIT(1);
        BusUnit.TABLECAPTION:
          EXIT(2);
        GLSetup."Global Dimension 1 Code":
          EXIT(3);
        GLSetup."Global Dimension 2 Code":
          EXIT(4);
        ELSE
          EXIT(-1);
      END;
    END;

    LOCAL PROCEDURE CopyGLAccToBuf@1(VAR TheGLAcc@1000 : Record 15;VAR TheDimCodeBuf@1001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheGLAcc."No.";
        Name := TheGLAcc.Name;
        Totaling := TheGLAcc.Totaling;
        Indentation := TheGLAcc.Indentation;
        "Show in Bold" := TheGLAcc."Account Type" <> TheGLAcc."Account Type"::Posting;
      END;
    END;

    LOCAL PROCEDURE CopyPeriodToBuf@8(VAR ThePeriod@1000 : Record 2000000007;VAR TheDimCodeBuf@1001 : Record 367);
    VAR
      Period2@1002 : Record 2000000007;
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := FORMAT(ThePeriod."Period Start");
        "Period Start" := ThePeriod."Period Start";
        IF ClosingEntryFilter = ClosingEntryFilter::Include THEN
          "Period End" := CLOSINGDATE(ThePeriod."Period End")
        ELSE
          "Period End" := ThePeriod."Period End";
        IF DateFilter <> '' THEN BEGIN
          Period2.SETFILTER("Period End",DateFilter);
          IF Period2.GETRANGEMAX("Period End") < "Period End" THEN
            "Period End" := Period2.GETRANGEMAX("Period End");
        END;
        Name := ThePeriod."Period Name";
      END;
    END;

    LOCAL PROCEDURE CopyBusUnitToBuf@5(VAR TheBusUnit@1000 : Record 220;VAR TheDimCodeBuf@1001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheBusUnit.Code;
        Name := TheBusUnit.Name;
      END;
    END;

    LOCAL PROCEDURE CopyDimValueToBuf@9(VAR TheDimVal@1000 : Record 349;VAR TheDimCodeBuf@1001 : Record 367);
    BEGIN
      WITH TheDimCodeBuf DO BEGIN
        INIT;
        Code := TheDimVal.Code;
        Name := TheDimVal.Name;
        Totaling := TheDimVal.Totaling;
        Indentation := TheDimVal.Indentation;
        "Show in Bold" :=
          TheDimVal."Dimension Value Type" <> TheDimVal."Dimension Value Type"::Standard;
      END;
    END;

    LOCAL PROCEDURE FindPeriod@2(SearchText@1000 : Code[10]);
    VAR
      Period@1001 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
    BEGIN
      IF DateFilter <> '' THEN BEGIN
        Period.SETFILTER("Period Start",DateFilter);
        IF NOT PeriodFormMgt.FindDate('+',Period,PeriodType) THEN
          PeriodFormMgt.FindDate('+',Period,PeriodType::Day);
        Period.SETRANGE("Period Start");
      END;
      IF PeriodFormMgt.FindDate(SearchText,Period,PeriodType) THEN
        IF ClosingEntryFilter = ClosingEntryFilter::Include THEN
          Period."Period End" := CLOSINGDATE(Period."Period End");
      IF AmountType = AmountType::"Net Change" THEN BEGIN
        GLAcc.SETRANGE("Date Filter",Period."Period Start",Period."Period End");
        IF GLAcc.GETRANGEMIN("Date Filter") = GLAcc.GETRANGEMAX("Date Filter") THEN
          GLAcc.SETRANGE("Date Filter",GLAcc.GETRANGEMIN("Date Filter"));
      END ELSE
        GLAcc.SETRANGE("Date Filter",0D,Period."Period End");

      InternalDateFilter := GLAcc.GETFILTER("Date Filter");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN
        DateFilter := InternalDateFilter;
      DimCodAmtBuf.DELETEALL;
    END;

    LOCAL PROCEDURE GetDimSelection@3(OldDimSelCode@1000 : Text[30]) : Text[30];
    VAR
      DimSelection@1001 : Page 568;
    BEGIN
      DimSelection.InsertDimSelBuf(FALSE,GLAcc.TABLECAPTION,GLAcc.TABLECAPTION);
      DimSelection.InsertDimSelBuf(FALSE,BusUnit.TABLECAPTION,BusUnit.TABLECAPTION);
      DimSelection.InsertDimSelBuf(FALSE,Text001,Text001);
      IF GLSetup."Global Dimension 1 Code" <> '' THEN
        DimSelection.InsertDimSelBuf(FALSE,GLSetup."Global Dimension 1 Code",'');
      IF GLSetup."Global Dimension 2 Code" <> '' THEN
        DimSelection.InsertDimSelBuf(FALSE,GLSetup."Global Dimension 2 Code",'');

      DimSelection.LOOKUPMODE := TRUE;
      IF DimSelection.RUNMODAL = ACTION::LookupOK THEN
        EXIT(DimSelection.GetDimSelCode);

      EXIT(OldDimSelCode);
    END;

    LOCAL PROCEDURE LookUpDimFilter@57(Dim@1000 : Code[20];VAR Text@1001 : Text[250]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
      DimValList@1003 : Page 560;
    BEGIN
      IF Dim = '' THEN
        EXIT(FALSE);
      DimValList.LOOKUPMODE(TRUE);
      DimVal.SETRANGE("Dimension Code",Dim);
      DimValList.SETTABLEVIEW(DimVal);
      IF DimValList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValList.GETRECORD(DimVal);
        Text := DimValList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE ValidateLineDimCode@19();
    VAR
      BusUnit@1000 : Record 220;
    BEGIN
      IF (UPPERCASE(LineDimCode) <> UPPERCASE(GLAcc.TABLECAPTION)) AND
         (UPPERCASE(LineDimCode) <> UPPERCASE(BusUnit.TABLECAPTION)) AND
         (UPPERCASE(LineDimCode) <> UPPERCASE(Text001)) AND
         (UPPERCASE(LineDimCode) <> GLSetup."Global Dimension 1 Code") AND
         (UPPERCASE(LineDimCode) <> GLSetup."Global Dimension 2 Code") AND
         (LineDimCode <> '')
      THEN BEGIN
        MESSAGE(Text002,LineDimCode);
        LineDimCode := '';
      END;
      LineDimOption := DimCodeToOption(LineDimCode);
      InternalDateFilter := GLAcc.GETFILTER("Date Filter");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN BEGIN
        DateFilter := InternalDateFilter;
        IF STRPOS(DateFilter,'&') > 1 THEN
          DateFilter := COPYSTR(DateFilter,1,STRPOS(DateFilter,'&') - 1);
      END ELSE
        PeriodInitialized := FALSE;
    END;

    LOCAL PROCEDURE ValidateColumnDimCode@20();
    VAR
      BusUnit@1000 : Record 220;
    BEGIN
      IF (UPPERCASE(ColumnDimCode) <> UPPERCASE(GLAcc.TABLECAPTION)) AND
         (UPPERCASE(ColumnDimCode) <> UPPERCASE(BusUnit.TABLECAPTION)) AND
         (UPPERCASE(ColumnDimCode) <> UPPERCASE(Text001)) AND
         (UPPERCASE(ColumnDimCode) <> GLSetup."Global Dimension 1 Code") AND
         (UPPERCASE(ColumnDimCode) <> GLSetup."Global Dimension 2 Code") AND
         (ColumnDimCode <> '')
      THEN BEGIN
        MESSAGE(Text003,ColumnDimCode);
        ColumnDimCode := '';
      END;
      ColumnDimOption := DimCodeToOption(ColumnDimCode);
      InternalDateFilter := GLAcc.GETFILTER("Date Filter");
      IF (LineDimOption <> LineDimOption::Period) AND (ColumnDimOption <> ColumnDimOption::Period) THEN BEGIN
        DateFilter := InternalDateFilter;
        IF STRPOS(DateFilter,'&') > 1 THEN
          DateFilter := COPYSTR(DateFilter,1,STRPOS(DateFilter,'&') - 1);
      END ELSE
        PeriodInitialized := FALSE;
    END;

    LOCAL PROCEDURE FindRec@1223(DimOption@1000 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2';VAR DimCodeBuf@1001 : Record 367;Which@1002 : Text[250]) : Boolean;
    VAR
      GLAcc@1224 : Record 15;
      BusUnit@1225 : Record 220;
      Period@1226 : Record 2000000007;
      DimVal@1227 : Record 349;
      PeriodFormMgt@1228 : Codeunit 359;
      Found@1229 : Boolean;
    BEGIN
      CASE DimOption OF
        DimOption::"G/L Account":
          BEGIN
            GLAcc."No." := DimCodeBuf.Code;
            IF GLAccFilter <> '' THEN
              GLAcc.SETFILTER("No.",GLAccFilter);
            Found := GLAcc.FIND(Which);
            IF Found THEN
              CopyGLAccToBuf(GLAcc,DimCodeBuf);
          END;
        DimOption::Period:
          BEGIN
            IF NOT PeriodInitialized THEN
              DateFilter := '';
            PeriodInitialized := TRUE;
            EVALUATE(Period."Period Start",DimCodeBuf.Code);
            IF DateFilter <> '' THEN
              Period.SETFILTER("Period Start",DateFilter)
            ELSE
              IF NOT PeriodInitialized AND (InternalDateFilter <> '') THEN
                Period.SETFILTER("Period Start",InternalDateFilter);
            Found := PeriodFormMgt.FindDate(Which,Period,PeriodType);
            IF Found THEN
              CopyPeriodToBuf(Period,DimCodeBuf);
          END;
        DimOption::"Business Unit":
          BEGIN
            BusUnit.Code := DimCodeBuf.Code;
            IF BusUnitFilter <> '' THEN
              BusUnit.SETFILTER(Code,BusUnitFilter);
            Found := BusUnit.FIND(Which);
            IF Found THEN
              CopyBusUnitToBuf(BusUnit,DimCodeBuf);
          END;
        DimOption::"Dimension 1":
          BEGIN
            IF GlobalDim1Filter <> '' THEN
              DimVal.SETFILTER(Code,GlobalDim1Filter);
            DimVal."Dimension Code" := GLSetup."Global Dimension 1 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            Found := DimVal.FIND(Which);
            IF Found THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 2":
          BEGIN
            IF GlobalDim2Filter <> '' THEN
              DimVal.SETFILTER(Code,GlobalDim2Filter);
            DimVal."Dimension Code" := GLSetup."Global Dimension 2 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            Found := DimVal.FIND(Which);
            IF Found THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
      END;
      EXIT(Found);
    END;

    LOCAL PROCEDURE NextRec@1230(DimOption@1000 : 'G/L Account,Period,Business Unit,Dimension 1,Dimension 2';VAR DimCodeBuf@1001 : Record 367;Steps@1002 : Integer) : Integer;
    VAR
      GLAcc@1231 : Record 15;
      BusUnit@1232 : Record 220;
      Period@1233 : Record 2000000007;
      DimVal@1234 : Record 349;
      PeriodFormMgt@1235 : Codeunit 359;
      ResultSteps@1236 : Integer;
    BEGIN
      CASE DimOption OF
        DimOption::"G/L Account":
          BEGIN
            GLAcc."No." := DimCodeBuf.Code;
            IF GLAccFilter <> '' THEN
              GLAcc.SETFILTER("No.",GLAccFilter);
            ResultSteps := GLAcc.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyGLAccToBuf(GLAcc,DimCodeBuf);
          END;
        DimOption::Period:
          BEGIN
            IF DateFilter <> '' THEN
              Period.SETFILTER("Period Start",DateFilter);
            EVALUATE(Period."Period Start",DimCodeBuf.Code);
            ResultSteps := PeriodFormMgt.NextDate(Steps,Period,PeriodType);
            IF ResultSteps <> 0 THEN
              CopyPeriodToBuf(Period,DimCodeBuf);
          END;
        DimOption::"Business Unit":
          BEGIN
            BusUnit.Code := DimCodeBuf.Code;
            IF BusUnitFilter <> '' THEN
              BusUnit.SETFILTER(Code,BusUnitFilter);
            ResultSteps := BusUnit.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyBusUnitToBuf(BusUnit,DimCodeBuf);
          END;
        DimOption::"Dimension 1":
          BEGIN
            IF GlobalDim1Filter <> '' THEN
              DimVal.SETFILTER(Code,GlobalDim1Filter);
            DimVal."Dimension Code" := GLSetup."Global Dimension 1 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            ResultSteps := DimVal.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
        DimOption::"Dimension 2":
          BEGIN
            IF GlobalDim2Filter <> '' THEN
              DimVal.SETFILTER(Code,GlobalDim2Filter);
            DimVal."Dimension Code" := GLSetup."Global Dimension 2 Code";
            DimVal.SETRANGE("Dimension Code",DimVal."Dimension Code");
            DimVal.Code := DimCodeBuf.Code;
            ResultSteps := DimVal.NEXT(Steps);
            IF ResultSteps <> 0 THEN
              CopyDimValueToBuf(DimVal,DimCodeBuf);
          END;
      END;
      EXIT(ResultSteps);
    END;

    PROCEDURE MATRIX_GenerateColumnCaptions@7(Step@1000 : 'Initial,Previous,Same,Next');
    VAR
      CurrentColumn@1003 : Record 367;
      Continue@1002 : Boolean;
      Found@1001 : Boolean;
      Which@1004 : Text[30];
    BEGIN
      MATRIX_CurrSetLength := 0;
      Continue := TRUE;
      CLEAR(MATRIX_ColumnCaptions);
      MATRIX_ColumnSet := '';

      CASE Step OF
        Step::Initial:
          BEGIN
            IF (ColumnDimOption = ColumnDimOption::Period) AND (PeriodType <> PeriodType::"Accounting Period")
               AND (DateFilter = '')
            THEN BEGIN
              EVALUATE(CurrentColumn.Code,FORMAT(WORKDATE));
              Which := '=><';
            END ELSE
              Which := '-';
            Found := FindRec(ColumnDimOption,CurrentColumn,Which);
          END;
        Step::Previous:
          BEGIN
            CurrentColumn.SETPOSITION(MATRIX_PrimaryKeyFirstColInSet);
            Found := FindRec(ColumnDimOption,CurrentColumn,'=');
            NextRec(ColumnDimOption,CurrentColumn,-MATRIX_NoOfColumns)
          END;
        Step::Same:
          BEGIN
            CurrentColumn.SETPOSITION(MATRIX_PrimaryKeyFirstColInSet);
            Found := FindRec(ColumnDimOption,CurrentColumn,'=');
          END;
        Step::Next:
          BEGIN
            CurrentColumn.SETPOSITION(MATRIX_PrimaryKeyFirstColInSet);
            Found := FindRec(ColumnDimOption,CurrentColumn,'=');
            IF NOT (NextRec(ColumnDimOption,CurrentColumn,MATRIX_NoOfColumns) = MATRIX_NoOfColumns) THEN BEGIN
              CurrentColumn.SETPOSITION(MATRIX_PrimaryKeyFirstColInSet);
              Found := FindRec(ColumnDimOption,CurrentColumn,'=');
            END
          END;
      END;

      MATRIX_PrimaryKeyFirstColInSet := CurrentColumn.GETPOSITION;

      IF Found THEN BEGIN
        REPEAT
          MATRIX_CurrSetLength := MATRIX_CurrSetLength + 1;
          IF ShowColumnName THEN
            MATRIX_ColumnCaptions[MATRIX_CurrSetLength] := CurrentColumn.Name
          ELSE
            MATRIX_ColumnCaptions[MATRIX_CurrSetLength] := CurrentColumn.Code;
        UNTIL (MATRIX_CurrSetLength = MATRIX_NoOfColumns) OR (NextRec(ColumnDimOption,CurrentColumn,1) <> 1);

        IF MATRIX_CurrSetLength = 1 THEN
          MATRIX_ColumnSet := MATRIX_ColumnCaptions[1]
        ELSE
          MATRIX_ColumnSet := MATRIX_ColumnCaptions[1] + '..' + MATRIX_ColumnCaptions[MATRIX_CurrSetLength];
      END;
    END;

    LOCAL PROCEDURE LineDimCodeOnAfterValidate@19031201();
    BEGIN
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE ColumnDimCodeOnAfterValidate@19023109();
    VAR
      MATRIX_Steps@1001 : 'First,Previous,Next';
    BEGIN
      CurrPage.UPDATE;
      MATRIX_GenerateColumnCaptions(MATRIX_Steps::First);
    END;

    LOCAL PROCEDURE DateFilterOnAfterValidate@19006009();
    VAR
      MATRIX_Step@1001 : 'First,Previous,Next';
    BEGIN
      DimCodAmtBuf.DELETEALL;
      CurrPage.UPDATE;
      IF ColumnDimOption = ColumnDimOption::Period THEN BEGIN
        PeriodInitialized := TRUE;
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
      END;
    END;

    LOCAL PROCEDURE GLAccFilterOnAfterValidate@19040142();
    VAR
      MATRIX_Step@1001 : 'First,Previous,Next';
    BEGIN
      DimCodAmtBuf.DELETEALL;
      CurrPage.UPDATE;
      IF ColumnDimOption = ColumnDimOption::"G/L Account" THEN
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
    END;

    LOCAL PROCEDURE GlobalDim2FilterOnAfterValidat@19025957();
    VAR
      MATRIX_Step@1001 : 'First,Previous,Next';
    BEGIN
      DimCodAmtBuf.DELETEALL;
      CurrPage.UPDATE;
      IF ColumnDimOption = ColumnDimOption::"Dimension 2" THEN
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
    END;

    LOCAL PROCEDURE GlobalDim1FilterOnAfterValidat@19037628();
    VAR
      MATRIX_Step@1001 : 'First,Previous,Next';
    BEGIN
      DimCodAmtBuf.DELETEALL;
      CurrPage.UPDATE;
      IF ColumnDimOption = ColumnDimOption::"Dimension 1" THEN
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
    END;

    LOCAL PROCEDURE BudgetFilterOnAfterValidate@19069837();
    BEGIN
      IF ShowActualBudg <> ShowActualBudg::"Actual Amounts" THEN
        DimCodAmtBuf.DELETEALL;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE BusUnitFilterOnAfterValidate@19015355();
    VAR
      MATRIX_Step@1000 : 'First,Previous,Next';
    BEGIN
      DimCodAmtBuf.DELETEALL;
      CurrPage.UPDATE;
      IF ColumnDimOption = ColumnDimOption::"Business Unit" THEN
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
    END;

    LOCAL PROCEDURE ShowColumnNameOnAfterValidate@19074585();
    VAR
      MATRIX_Step@1000 : 'Initial,Previous,Same,Next';
    BEGIN
      MATRIX_GenerateColumnCaptions(MATRIX_Step::Same);
    END;

    LOCAL PROCEDURE PeriodTypeOnAfterValidate@19015506();
    VAR
      MATRIX_Step@1001 : 'First,Previous,Next';
    BEGIN
      IF ColumnDimOption = ColumnDimOption::Period THEN
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
    END;

    LOCAL PROCEDURE AmountTypeOnAfterValidate@19077637();
    VAR
      MATRIX_Step@1001 : 'First,Previous,Next';
    BEGIN
      IF ColumnDimOption = ColumnDimOption::Period THEN
        MATRIX_GenerateColumnCaptions(MATRIX_Step::First);
    END;

    BEGIN
    END.
  }
}

