OBJECT XMLport 10 IC G/L Account Import/Export
{
  OBJECT-PROPERTIES
  {
    Date=11/05/08;
    Time=12:00:00 PM;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=IC G/L Account Import/Export;
               ESM=Importaci¢n/Exportaci¢n de cuenta IC;
               FRC=Import/export comptes du grand livre IC;
               ENC=IC G/L Account Import/Export];
    OnPreXMLport=VAR
                   ICGLAcc@1000 : Record 410;
                 BEGIN
                   TempICGLAcc.DELETEALL;
                   IF ICGLAcc.FIND('-') THEN
                     REPEAT
                       TempICGLAcc := ICGLAcc;
                       TempICGLAcc.INSERT;
                     UNTIL ICGLAcc.NEXT = 0;
                 END;

    OnPostXMLport=VAR
                    OrgICGLAcc@1000 : Record 410;
                    MsgTxt@1001 : Text[1024];
                  BEGIN
                    IF XMLInbound THEN BEGIN
                      IF TempICGLAcc.FIND('-') THEN
                        REPEAT
                          Deleted := Deleted + 1;
                          OrgICGLAcc.GET(TempICGLAcc."No.");
                          OrgICGLAcc.DELETE;
                        UNTIL TempICGLAcc.NEXT = 0;

                      IF Inserted > 0 THEN
                        IF Inserted = 1 THEN
                          MsgTxt := STRSUBSTNO(Text001,Inserted,OrgICGLAcc.TABLECAPTION)
                        ELSE
                          MsgTxt := STRSUBSTNO(Text002,Inserted);

                      IF Modified > 0 THEN BEGIN
                        IF MsgTxt <> '' THEN
                          MsgTxt := MsgTxt + '\';
                        IF Modified = 1 THEN
                          MsgTxt := MsgTxt + STRSUBSTNO(Text003,Modified,OrgICGLAcc.TABLECAPTION)
                        ELSE
                          MsgTxt := MsgTxt + STRSUBSTNO(Text004,Modified);
                      END;

                      IF Deleted > 0 THEN BEGIN
                        IF MsgTxt <> '' THEN
                          MsgTxt := MsgTxt + '\';
                        IF Deleted = 1 THEN
                          MsgTxt := MsgTxt + STRSUBSTNO(Text005,Deleted,OrgICGLAcc.TABLECAPTION)
                        ELSE
                          MsgTxt := MsgTxt + STRSUBSTNO(Text006,Deleted);
                      END;

                      IF Inserted + Deleted + Modified = 0 THEN
                        MsgTxt := Text000;

                      MESSAGE(MsgTxt);
                    END;
                  END;

  }
  ELEMENTS
  {
    { [{1E89538F-0A82-40BD-8DF1-44A5C22F9C57}];  ;GLAccounts          ;Element ;Text     }

    { [{9D19931E-BC36-42C2-B6CD-B7EFB0661449}];1 ;ICGLAccount         ;Element ;Table   ;
                                                  VariableName=ICGLAcc;
                                                  SourceTable=Table410;
                                                  Import::OnBeforeInsertRecord=VAR
                                                                                 OrgICGLAcc@1000 : Record 410;
                                                                               BEGIN
                                                                                 XMLInbound := TRUE;

                                                                                 IF TempICGLAcc.GET(ICGLAcc."No.") THEN BEGIN
                                                                                   IF (ICGLAcc.Name <> TempICGLAcc.Name) OR (ICGLAcc."Account Type" <> TempICGLAcc."Account Type") OR
                                                                                      (ICGLAcc."Income/Balance" <> TempICGLAcc."Income/Balance") OR (ICGLAcc.Blocked <> TempICGLAcc.Blocked)
                                                                                   THEN
                                                                                     Modified := Modified + 1;
                                                                                   ICGLAcc."Map-to G/L Acc. No." := TempICGLAcc."Map-to G/L Acc. No.";
                                                                                   OrgICGLAcc.GET(ICGLAcc."No.");
                                                                                   OrgICGLAcc.DELETE;
                                                                                   TempICGLAcc.DELETE;
                                                                                 END ELSE
                                                                                   Inserted := Inserted + 1;
                                                                               END;
                                                                                }

    { [{11763B3F-4E93-45E1-9E70-53CCC8AF0957}];2 ;No                  ;Attribute;Field  ;
                                                  DataType=Code;
                                                  SourceField=ICGLAcc::No. }

    { [{713D14AA-F768-41E1-95A9-E5BF8E79EDC3}];2 ;Name                ;Attribute;Field  ;
                                                  DataType=Text;
                                                  SourceField=ICGLAcc::Name }

    { [{44D2D0F5-F6CC-4A14-815E-65890604ACE2}];2 ;AccountType         ;Attribute;Field  ;
                                                  DataType=Option;
                                                  SourceField=ICGLAcc::Account Type }

    { [{EC03F478-8924-4A44-8D4A-4A58DCBC4497}];2 ;IncomeBalance       ;Attribute;Field  ;
                                                  DataType=Option;
                                                  SourceField=ICGLAcc::Income/Balance }

    { [{6A89E279-D0B2-4487-97DB-3E6784F861E0}];2 ;Blocked             ;Attribute;Field  ;
                                                  DataType=Boolean;
                                                  SourceField=ICGLAcc::Blocked }

    { [{1D6A6364-E468-480F-BE29-0E50ED6179F7}];2 ;Indentation         ;Attribute;Field  ;
                                                  DataType=Integer;
                                                  SourceField=ICGLAcc::Indentation }

  }
  EVENTS
  {
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      TempICGLAcc@1000 : TEMPORARY Record 410;
      XMLInbound@1011 : Boolean;
      Inserted@1003 : Integer;
      Deleted@1002 : Integer;
      Modified@1001 : Integer;
      Text000@1010 : TextConst 'ENU=There were no changes.;ESM=No hubo cambios.;FRC=Il n''y a eu aucune modification.;ENC=There were no changes.';
      Text001@1004 : TextConst 'ENU=%1 %2 was added.;ESM=%2: se agreg¢ %1.;FRC=%1 %2 a ‚t‚ ajout‚.;ENC=%1 %2 was added.';
      Text002@1005 : TextConst 'ENU=%1 IC G/L Accounts were added.;ESM=Se agregaron %1 cuentas IC.;FRC=%1 comptes de grand livre IC ont ‚t‚ ajout‚s.;ENC=%1 IC G/L Accounts were added.';
      Text003@1007 : TextConst 'ENU=%1 %2 was updated.;ESM=%2: se actualiz¢ %1.;FRC=%1 %2 a ‚t‚ mis … jour.;ENC=%1 %2 was updated.';
      Text004@1006 : TextConst 'ENU=%1 IC G/L Accounts were updated.;ESM=Se actualizaron %1 cuentas IC.;FRC=%1 comptes de grand livre IC ont ‚t‚ mis … jour.;ENC=%1 IC G/L Accounts were updated.';
      Text005@1009 : TextConst 'ENU=%1 %2 was deleted.;ESM=%2: se elimin¢ %1.;FRC=%1 %2 a ‚t‚ supprim‚.;ENC=%1 %2 was deleted.';
      Text006@1008 : TextConst 'ENU=%1 IC G/L Accounts were deleted.;ESM=Se eliminaron %1 cuentas IC.;FRC=%1 comptes de grand livre IC ont ‚t‚ supprim‚s.;ENC=%1 IC G/L Accounts were deleted.';

    BEGIN
    END.
  }
}

