OBJECT Report 1195 Adjust Resource Costs/Prices
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Adjust Resource Costs/Prices;
               ESM=Modificar precios recursos;
               FRC=Ajuster le co–t/prix des ressources;
               ENC=Adjust Resource Costs/Prices];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RoundingMethod.SETRANGE(Code,RoundingMethod.Code);
                END;

  }
  DATASET
  {
    { 5508;    ;DataItem;                    ;
               DataItemTable=Table156;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000);
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");

                                  CASE Selection OF
                                    Selection::"Direct Unit Cost":
                                      OldFieldNo := "Direct Unit Cost";
                                    Selection::"Indirect Cost %":
                                      OldFieldNo := "Indirect Cost %";
                                    Selection::"Unit Cost":
                                      OldFieldNo := "Unit Cost";
                                    Selection::"Profit %":
                                      OldFieldNo := "Profit %";
                                    Selection::"Unit Price":
                                      OldFieldNo := "Unit Price";
                                  END;

                                  NewFieldNo := OldFieldNo * AdjFactor;

                                  IF RoundingMethod.Code <> '' THEN BEGIN
                                    RoundingMethod."Minimum Amount" := NewFieldNo;
                                    IF RoundingMethod.FIND('=<') THEN BEGIN
                                      NewFieldNo := NewFieldNo + RoundingMethod."Amount Added Before";
                                      IF RoundingMethod.Precision > 0 THEN
                                        NewFieldNo := ROUND(NewFieldNo,RoundingMethod.Precision,COPYSTR('=><',RoundingMethod.Type + 1,1));
                                      NewFieldNo := NewFieldNo + RoundingMethod."Amount Added After";
                                    END;
                                  END;

                                  CASE Selection OF
                                    Selection::"Direct Unit Cost":
                                      VALIDATE("Direct Unit Cost",NewFieldNo);
                                    Selection::"Indirect Cost %":
                                      VALIDATE("Indirect Cost %",NewFieldNo);
                                    Selection::"Unit Cost":
                                      VALIDATE("Unit Cost",NewFieldNo);
                                    Selection::"Profit %":
                                      VALIDATE("Profit %",NewFieldNo);
                                    Selection::"Unit Price":
                                      VALIDATE("Unit Price",NewFieldNo);
                                  END;

                                  MODIFY;
                                END;

               ReqFilterFields=No.,Resource Group No. }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   IF AdjFactor = 0 THEN
                     AdjFactor := 1;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 10  ;2   ;Field     ;
                  CaptionML=[ENU=Adjust Field;
                             ESM=Campo ajuste;
                             FRC=Ajuster champ;
                             ENC=Adjust Field];
                  OptionCaptionML=[ENU=Direct Unit Cost,Indirect Cost %,Unit Cost,Profit %,Unit Price;
                                   ESM=Costo unit. directo,% Costo indirecto,Costo unitario,% Beneficio,Precio unitario;
                                   FRC=Co–t unitaire direct,% co–t indirect,Co–t unitaire,% profit,Prix unitaire;
                                   ENC=Direct Unit Cost,Indirect Cost %,Unit Cost,Profit %,Unit Price];
                  SourceExpr=Selection }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Adjustment Factor;
                             ESM=Factor ajuste;
                             FRC=Facteur d'ajustement;
                             ENC=Adjustment Factor];
                  DecimalPlaces=0:5;
                  SourceExpr=AdjFactor }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Rounding Method;
                             ESM=M‚todo redondeo;
                             FRC=M‚thode d'arrondissement;
                             ENC=Rounding Method];
                  SourceExpr=RoundingMethod.Code;
                  TableRelation="Rounding Method" }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing resources  #1##########;ESM=Procesando recursos   #1##########;FRC=Ressources en traitement  #1##########;ENC=Processing resources  #1##########';
      NewFieldNo@1001 : Decimal;
      OldFieldNo@1002 : Decimal;
      RoundingMethod@1003 : Record 42;
      AdjFactor@1004 : Decimal;
      Selection@1005 : 'Direct Unit Cost,Indirect Cost %,Unit Cost,Profit %,Unit Price';
      Window@1006 : Dialog;

    PROCEDURE InitializeRequest@1(SelectionFrom@1000 : Option;AdjFactorFrom@1001 : Decimal;RoundingMethodCode@1002 : Code[10]);
    BEGIN
      Selection := SelectionFrom;
      AdjFactor := AdjFactorFrom;
      RoundingMethod.Code := RoundingMethodCode;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

