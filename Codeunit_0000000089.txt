OBJECT Codeunit 89 Sales-Post + Email
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=BEGIN
            SalesHeader.COPY(Rec);
            Code;
            Rec := SalesHeader;
          END;

  }
  CODE
  {
    VAR
      PostAndSendInvoiceQst@1001 : TextConst 'ENU=Do you want to post and send the %1?;ESM=¨Confirma que desea registrar y enviar el/la %1?;FRC=Voulez-vous reporter et envoyer la %1?;ENC=Do you want to post and send the %1?';
      SalesHeader@1003 : Record 36;
      SalesInvHeader@1005 : Record 112;
      SalesCrMemoHeader@1006 : Record 114;
      FileManagement@1009 : Codeunit 419;
      OutlookMessageHelper@1008 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=7.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Outlook.IOutlookMessage" RUNONCLIENT;
      HideMailDialog@1007 : Boolean;
      PostAndSaveInvoiceQst@1000 : TextConst 'ENU=Do you want to post and save the %1?;ESM=¨Confirma que desea registrar y guardar el %1?;FRC=Souhaitez-vous reporter et enregistrer le %1?;ENC=Do you want to post and save the %1?';
      NotSupportedDocumentTypeSendingErr@1004 : TextConst 'ENU=The %1 is not posted because sending document of type %1 is not supported.;ESM=El %1 no se ha registrado porque no se pueden enviar documentos de tipo %1.;FRC=Le %1 n''a pas ‚t‚ report‚ car l''envoi d''un document de type %1 n''est pas pris en charge.;ENC=The %1 is not posted because sending document of type %1 is not supported.';
      NotSupportedDocumentTypeSavingErr@1002 : TextConst 'ENU=The %1 is not posted because saving document of type %1 is not supported.;ESM=El %1 no se ha registrado porque no se pueden guardar documentos de tipo %1.;FRC=Le %1 n''a pas ‚t‚ report‚ car l''enregistrement d''un document de type %1 n''est pas pris en charge.;ENC=The %1 is not posted because saving document of type %1 is not supported.';

    LOCAL PROCEDURE Code@2();
    BEGIN
      WITH SalesHeader DO BEGIN
        CASE "Document Type" OF
          "Document Type"::Invoice,
          "Document Type"::"Credit Memo":
            IF NOT ConfirmPostAndDistribute(SalesHeader) THEN
              EXIT;
          ELSE
            ErrorPostAndDistribute(SalesHeader);
        END;

        CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader);
        COMMIT;
        SendDocumentReport(SalesHeader);
      END;
    END;

    LOCAL PROCEDURE SendDocumentReport@3(VAR SalesHeader@1000 : Record 36);
    VAR
      ReportDistributionManagement@1001 : Codeunit 452;
    BEGIN
      WITH SalesHeader DO
        CASE "Document Type" OF
          "Document Type"::Invoice:
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesInvHeader."No." := "No."
              ELSE
                SalesInvHeader."No." := "Last Posting No.";
              SalesInvHeader.GET(SalesInvHeader."No.");
              ReportDistributionManagement.InitializeFrom(OutlookMessageHelper,HideMailDialog);
              IF FileManagement.IsWebClient THEN
                ReportDistributionManagement.SaveDocumentReport(SalesInvHeader)
              ELSE
                ReportDistributionManagement.EmailDocumentReport(SalesInvHeader);
            END;
          "Document Type"::"Credit Memo":
            BEGIN
              IF "Last Posting No." = '' THEN
                SalesCrMemoHeader."No." := "No."
              ELSE
                SalesCrMemoHeader."No." := "Last Posting No.";
              SalesCrMemoHeader.GET(SalesCrMemoHeader."No.");
              ReportDistributionManagement.InitializeFrom(OutlookMessageHelper,HideMailDialog);
              IF FileManagement.IsWebClient THEN
                ReportDistributionManagement.SaveDocumentReport(SalesCrMemoHeader)
              ELSE
                ReportDistributionManagement.EmailDocumentReport(SalesCrMemoHeader);
            END
        END
    END;

    PROCEDURE InitializeFrom@10(NewOutlookMessageHelper@1002 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=7.1.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Outlook.IOutlookMessage";NewHideMailDialog@1001 : Boolean);
    BEGIN
      OutlookMessageHelper := NewOutlookMessageHelper;
      HideMailDialog := NewHideMailDialog;
    END;

    LOCAL PROCEDURE ConfirmPostAndDistribute@1(VAR SalesHeader@1000 : Record 36) : Boolean;
    VAR
      PostAndDistributeQuestion@1001 : Text;
      ConfirmOK@1002 : Boolean;
    BEGIN
      IF FileManagement.IsWebClient THEN
        PostAndDistributeQuestion := PostAndSaveInvoiceQst
      ELSE
        PostAndDistributeQuestion := PostAndSendInvoiceQst;

      ConfirmOK := CONFIRM(PostAndDistributeQuestion,FALSE,SalesHeader."Document Type");

      EXIT(ConfirmOK);
    END;

    LOCAL PROCEDURE ErrorPostAndDistribute@4(VAR SalesHeader@1000 : Record 36);
    VAR
      NotSupportedDocumentType@1001 : Text;
    BEGIN
      IF FileManagement.IsWebClient THEN
        NotSupportedDocumentType := NotSupportedDocumentTypeSavingErr
      ELSE
        NotSupportedDocumentType := NotSupportedDocumentTypeSendingErr;

      ERROR(NotSupportedDocumentType,SalesHeader."Document Type");
    END;

    BEGIN
    END.
  }
}

