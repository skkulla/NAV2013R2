OBJECT Codeunit 449 Job Queue Start Codeunit
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=472;
    OnRun=VAR
            AllObjWithCaption@1001 : Record 2000000058;
            WinAccControl@1002 : Record 2000000053;
            HasPermission@1004 : Boolean;
          BEGIN
            IF NOT "Run in User Session" THEN BEGIN
              HasPermission := WinAccControl.ISEMPTY;
              IF NOT HasPermission THEN
                HasPermission := HasWinPermission("User ID","Object Type to Run","Object ID to Run");
              IF NOT HasPermission THEN BEGIN
                IF AllObjWithCaption.GET("Object Type to Run","Object ID to Run") THEN;
                ERROR(Text001,"User ID","Object Type to Run","Object ID to Run",AllObjWithCaption."Object Caption");
              END;
            END;

            CASE "Object Type to Run" OF
              "Object Type to Run"::Codeunit:
                CODEUNIT.RUN("Object ID to Run",Rec);
              "Object Type to Run"::Report:
                REPORT.RUN("Object ID to Run",FALSE);
            END;

            // Commit any remaining transactions from the target codeunit\report. This is necessary due
            // to buffered record insertion which may not have surfaced errors in CODEUNIT.RUN above.
            COMMIT;
          END;

  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=User %1 has not been set up to be allowed to run %2 %3 - %4.;ESM=El usuario %1 no se ha configurado para poder ejecutar %2 %3 - %4.;FRC=L''utilisateur %1 n''a pas ‚t‚ configur‚ de sorte … pouvoir ex‚cuter %2 %3 - %4.;ENC=User %1 has not been set up to be allowed to run %2 %3 - %4.';

    LOCAL PROCEDURE HasWinPermission@2(UserId2@1002 : Text[65];ObjectType@1000 : Integer;ObjectID@1001 : Integer) : Boolean;
    VAR
      User@1005 : Record 2000000120;
      User2@1009 : Record 2000000120;
      PermissionSet@1006 : Record 2000000053;
      Permission@1008 : Record 2000000005;
      Found@1007 : Boolean;
      HasPermission@1003 : Boolean;
    BEGIN
      IF User2.FINDSET THEN
        REPEAT
          Found := UPPERCASE(User2."User Name") = UPPERCASE(UserId2);
          IF Found THEN
            User := User2;
        UNTIL Found OR (User2.NEXT = 0);
      IF NOT Found THEN
        EXIT(FALSE);

      HasPermission := PermissionSet.GET(User."User Security ID",'SUPER',COMPANYNAME);
      IF NOT HasPermission THEN
        HasPermission := PermissionSet.GET(User."User Security ID",'SUPER','');
      IF HasPermission THEN
        EXIT(TRUE);

      Permission.SETRANGE("Object Type",ObjectType);
      Permission.SETRANGE("Object ID",ObjectID);
      Permission.SETRANGE("Execute Permission",Permission."Execute Permission"::Yes);
      IF Permission.FINDSET THEN
        REPEAT
          HasPermission := PermissionSet.GET(User."User Security ID",Permission."Role ID",COMPANYNAME);
          IF NOT HasPermission THEN
            HasPermission := PermissionSet.GET(User."User Security ID",Permission."Role ID",'');
        UNTIL HasPermission OR (Permission.NEXT = 0);
      EXIT(HasPermission);
    END;

    BEGIN
    END.
  }
}

