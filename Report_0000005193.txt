OBJECT Report 5193 Create Conts. from Bank Accs.
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Conts. from Bank Accs.;
               ESM=Crear contactos desde bancos;
               FRC=Cr‚er contacts de cptes banc.;
               ENC=Create Conts. from Bank Accs.];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  RMSetup.GET;
                  RMSetup.TESTFIELD("Bus. Rel. Code for Bank Accs.");
                END;

  }
  DATASET
  {
    { 4558;    ;DataItem;                    ;
               DataItemTable=Table270;
               OnPreDataItem=BEGIN
                               Window.OPEN(Text000 +
                                 Text001,"No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1);

                                  WITH ContBusRel DO BEGIN
                                    SETRANGE("Link to Table","Link to Table"::"Bank Account");
                                    SETRANGE("No.","Bank Account"."No.");
                                    IF FINDFIRST THEN
                                      CurrReport.SKIP;
                                  END;

                                  Cont.INIT;
                                  Cont.TRANSFERFIELDS("Bank Account");
                                  Cont."No." := '';
                                  Cont.SetSkipDefault(FALSE);
                                  Cont.INSERT(TRUE);
                                  DuplMgt.MakeContIndex(Cont);

                                  IF NOT DuplicateContactExist THEN
                                    DuplicateContactExist := DuplMgt.DuplicateExist(Cont);

                                  WITH ContBusRel DO BEGIN
                                    INIT;
                                    "Contact No." := Cont."No.";
                                    "Business Relation Code" := RMSetup."Bus. Rel. Code for Bank Accs.";
                                    "Link to Table" := "Link to Table"::"Bank Account";
                                    "No." := "Bank Account"."No.";
                                    INSERT;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                Window.CLOSE;

                                IF DuplicateContactExist THEN BEGIN
                                  COMMIT;
                                  PAGE.RUNMODAL(PAGE::"Contact Duplicates");
                                END;
                              END;

               ReqFilterFields=No.,Search Name,Bank Acc. Posting Group,Currency Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Processing vendors...\\;ESM=Procesando proveedores...\\;FRC=Traitement des fournisseurs...\\;ENC=Processing vendors...\\';
      Text001@1001 : TextConst 'ENU=Bank Account No.  #1##########;ESM=N§ banco  #1##########;FRC=Nø compte bancaire  #1##########;ENC=Bank Account No.  #1##########';
      RMSetup@1002 : Record 5079;
      Cont@1003 : Record 5050;
      ContBusRel@1004 : Record 5054;
      Window@1005 : Dialog;
      DuplMgt@1006 : Codeunit 5060;
      DuplicateContactExist@1007 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

