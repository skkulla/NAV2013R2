OBJECT Codeunit 10092 Export Payments (Cecoban)
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVNA7.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      BankAccount@1001 : Record 270;
      CompanyInformation@1002 : Record 79;
      RBMgt@1480006 : Codeunit 419;
      TotalFileDebit@1006 : Decimal;
      TotalFileCredit@1007 : Decimal;
      TotalBatchDebit@1008 : Decimal;
      TotalBatchCredit@1009 : Decimal;
      RecordLength@1010 : Integer;
      BlockingFactor@1011 : Integer;
      EntryAddendaCount@1013 : Integer;
      FileEntryAddendaCount@1014 : Integer;
      NoOfRec@1015 : Integer;
      ExportFile@1017 : File;
      Justification@1018 : 'Left,Right';
      BatchNo@1019 : Integer;
      BatchCount@1020 : Integer;
      FileHashTotal@1022 : Decimal;
      BatchHashTotal@1023 : Decimal;
      FileName@1025 : Text;
      FileIsInProcess@1027 : Boolean;
      BatchIsInProcess@1028 : Boolean;
      FileDate@1030 : Date;
      FileTime@1031 : Time;
      ModifierValues@1032 : ARRAY [26] OF Code[1];
      PayeeAcctType@1480005 : Integer;
      TraceNo@1034 : Integer;
      BatchDay@1480003 : Integer;
      Text000@1035 : TextConst 'ENU=Cannot start new Export File while %1 is in process.;ESM=No se puede iniciar el nuevo archivo de exportaci¢n mientras %1 est  en curso.;FRC=Ne peut commencer un nouveau fichier d''exportation pendant que le %1 est en traitement.;ENC=Cannot start new Export File while %1 is in process.';
      Text001@1036 : TextConst 'ENU=is not valid.;ESM=no es v lido.;FRC=est non valide.;ENC=is not valid.';
      Text002@1037 : TextConst 'ENU=%1 in %2 %3 is invalid.;ESM=%1 en %2 %3 no es v lido.;FRC=%1 dans %2 %3 est non valide.;ENC=%1 in %2 %3 is invalid.';
      Text003@1038 : TextConst 'ENU=File %1 already exists. Check the %2 in %3 %4.;ESM=Ya existe el archivo %1. Compruebe %2 en %3 %4.;FRC=Fichier %1 existe d‚j…. V‚rifier le %2 dans %3 %4.;ENC=File %1 already exists. Check the %2 in %3 %4.';
      Text004@1039 : TextConst 'ENU=Cannot start export batch until an export file is started.;ESM=No se puede iniciar la secci¢n de exportaci¢n hasta que se inicie un archivo de exportaci¢n.;FRC=Ne peut commencer un traitement d''exportation avant le d‚but d''un fichier d''exportation.;ENC=Cannot start export batch until an export file is started.';
      Text005@1040 : TextConst 'ENU=Cannot start new export batch until previous batch is completed.;ESM=No se puede iniciar una nueva secci¢n de exportaci¢n hasta que se complete la secci¢n anterior.;FRC=Ne peut commencer un nouveau traitement d''exportation avant que le traitement pr‚c‚dent ne soit compl‚t‚.;ENC=Cannot start new export batch until previous batch is completed.';
      Text006@1041 : TextConst 'ENU=Cannot export details until an export file is started.;ESM=No se puede exportar detalles hasta que se inicie un archivo de exportaci¢n.;FRC=Ne peut exporter de d‚tails avant qu''un fichier d''exportation ne soit commenc‚.;ENC=Cannot export details until an export file is started.';
      Text007@1042 : TextConst 'ENU=Cannot export details until an export batch is started.;ESM=No se puede exportar detalles hasta que se inicie una secci¢n de exportaci¢n.;FRC=Ne peut exporter de d‚tails avant qu''un lot d''exportation ne soit commenc‚.;ENC=Cannot export details until an export batch is started.';
      Text008@1043 : TextConst 'ENU=Vendor No. %1 has no bank account setup for electronic payments.;ESM=El proveedor %1 no tiene una cuenta configurada para pagos electr¢nicos.;FRC=Le compte bancaire du fournisseur Nø %1 n''est pas configur‚ pour les paiements ‚lectroniques.;ENC=Vendor No. %1 has no bank account setup for electronic payments.';
      Text009@1044 : TextConst 'ENU=Vendor No. %1 has more than one bank account setup for electronic payments.;ESM=El proveedor %1 tiene m s de una cuenta configurada para pagos electr¢nicos.;FRC=Le fournisseur Nø %1 possŠde plus d''une configuration de compte bancaire pour les paiements ‚lectroniques.;ENC=Vendor No. %1 has more than one bank account setup for electronic payments.';
      Text010@1045 : TextConst 'ENU=Customer No. %1 has no bank account setup for electronic payments.;ESM=El cliente %1 no tiene una cuenta configurada para pagos electr¢nicos.;FRC=Le client Nø %1 n''a pas de configuration de compte bancaire pour les paiements ‚lectroniques.;ENC=Customer No. %1 has no bank account setup for electronic payments.';
      Text011@1046 : TextConst 'ENU=Customer No. %1 has more than one bank account setup for electronic payments.;ESM=El cliente %1 tiene m s de una cuenta configurada para pagos electr¢nicos.;FRC=Le client Nø %1 a plus d''une configuration de compte bancaire pour les paiements ‚lectroniques.;ENC=Customer No. %1 has more than one bank account setup for electronic payments.';
      Text012@1047 : TextConst 'ENU=Cannot end export batch until an export file is started.;ESM=No se puede terminar la secci¢n de exportaci¢n hasta que se inicie un archivo de exportaci¢n.;FRC=Ne peut terminer une exportation avant qu''un fichier d''exportation ne soit commenc‚.;ENC=Cannot end export batch until an export file is started.';
      Text013@1048 : TextConst 'ENU=Cannot end export batch until an export batch is started.;ESM=No se puede terminar la secci¢n de exportaci¢n hasta que se inicie una secci¢n de exportaci¢n.;FRC=Ne peut terminer une exportation avant qu''un lot d''exportation ne soit commenc‚.;ENC=Cannot end export batch until an export batch is started.';
      Text014@1049 : TextConst 'ENU=Cannot end export file until an export file is started.;ESM=No se puede terminar el archivo de exportaci¢n hasta que se inicie uno.;FRC=Ne peut terminer une exportation avant qu''un fichier ne soit commenc‚.;ENC=Cannot end export file until an export file is started.';
      Text015@1050 : TextConst 'ENU=Cannot end export file until export batch is ended.;ESM=No se puede terminar el archivo de exportaci¢n hasta que se termine la secci¢n de exportaci¢n.;FRC=Ne peut terminer une exportation avant qu''un lot ne soit termin‚.;ENC=Cannot end export file until export batch is ended.';
      Text016@1051 : TextConst 'ENU=File %1 does not exist.;ESM=El archivo %1 no existe.;FRC=Fichier %1 n''existe pas.;ENC=File %1 does not exist.';
      Text017@1052 : TextConst 'ENU=Did the transmission work properly?;ESM=¨La transmisi¢n se realiz¢ correctamente?;FRC=Est-ce que la transmission a fonctionn‚ correctement?;ENC=Did the transmission work properly?';
      Text018@1053 : TextConst 'ENU=Either %1 or %2 must refer to either a %3 or a %4 for an electronic payment.;ESM=%1 o %2 deben hacer referencia a un %3 o %4 para un pago electr¢nico.;FRC=%1 ou %2 doit renvoyer … un %3 ou … un %4 pour un paiement ‚lectronique.;ENC=Either %1 or %2 must refer to either a %3 or a %4 for an electronic payment.';
      Text1020100@1020100 : TextConst 'ENU=%1 is blocked for %2 processing;ESM=%1 est  bloqueado para el proceso %2;FRC=%1 est bloqu‚ pour le traitement de %2;ENC=%1 is blocked for %2 processing';

      Text019@1480000 : TextConst
        'ENU=You must now run the program that transmits the payments file to the bank. Transmit the file named %1 located at %2 to %3 (%4 %5 %6).  After the transmission is completed, you will be asked if it worked correctly.  Are you ready to transmit (answer No to cancel the transmission process)?',
        'ESM=Debe ejecutar el programa que transmite el archivo de pagos al banco. Transmita el archivo denominado %1 que se encuentra en %2 a %3 (%4 %5 %6). Una vez finalizada la transmisi¢n, deber  confirmar que funcion¢ correctamente. ¨Est  listo para transmitir (responda No para cancelar el proceso de transmisi¢n)?',
        'FRC=Vous devez … pr‚sent ex‚cuter le programme qui transmet le fichier de paiements … la banque. Transmettez le fichier nomm‚ %1 situ‚ … l''emplacement %2 vers %3 (%4 %5 %6). Une fois la transmission termin‚e, vous serez invit‚ … indiquer si la proc‚dure a bien fonctionn‚. Òtes-vous prˆt … transmettre (r‚pondez Non pour annuler la proc‚dure de transmission) ?',
        'ENC=You must now run the program that transmits the payments file to the bank. Transmit the file named %1 located at %2 to %3 (%4 %5 %6).  After the transmission is completed, you will be asked if it worked correctly.  Are you ready to transmit (answer No to cancel the transmission process)?';
      Text1480004@1480004 : TextConst 'ENU=Bank Account number must be either the 18 character CLABE format for checking, or 16 characters for Debit Card.;ESM=El n£mero de cuenta debe estar en formato CLABE de 18 caracteres para cuenta de cheques o de 16 caracteres para tarjeta de d‚bito.;FRC=Le num‚ro de compte bancaire doit comporter 18 caractŠres en format CLABE pour la v‚rification ou 16 caractŠres pour la carte de d‚bit.;ENC=Bank Account number must be either the 18 character CLABE format for chequing, or 16 characters for Debit Card.';
      SequenceNo@1480001 : Integer;
      OpCode@1480002 : Integer;

    PROCEDURE StartExportFile@6(BankAccountNo@1000 : Code[20];ReferenceCode@1001 : Code[10]);
    BEGIN
      BuildIDModifier;
      IF FileIsInProcess THEN
        ERROR(Text000,FileName);

      CompanyInformation.GET;
      CompanyInformation.TESTFIELD("Federal ID No.");

      WITH BankAccount DO BEGIN
        LOCKTABLE;
        GET(BankAccountNo);
        TESTFIELD("Export Format","Export Format"::MX);
        TESTFIELD("Transit No.");
        TESTFIELD("E-Pay Export File Path");
        IF "E-Pay Export File Path"[STRLEN("E-Pay Export File Path")] <> '\' THEN
          ERROR(Text002,
            FIELDCAPTION("E-Pay Export File Path"),
            TABLECAPTION,
            "No.");
        TESTFIELD("Bank Acc. Posting Group");
        TESTFIELD(Blocked,FALSE);

        "Last E-Pay Export File Name" := ExportFileName;
        FileName := RBMgt.ServerTempFileName('');
        MODIFY;

        IF EXISTS(FileName) THEN
          ERROR(Text003,
            FileName,
            FIELDCAPTION("Last E-Pay Export File Name"),
            TABLECAPTION,
            "No.");
        ExportFile.TEXTMODE(TRUE);
        ExportFile.WRITEMODE(TRUE);
        ExportFile.CREATE(FileName);

        FileIsInProcess := TRUE;
        FileDate := TODAY;
        FileTime := TIME;
        NoOfRec := 0;
        FileHashTotal := 0;
        TotalFileDebit := 0;
        TotalFileCredit := 0;
        FileEntryAddendaCount := 0;
        BatchCount := 0;
        BlockingFactor := 10;
        RecordLength := 422;
        BatchNo := 0;
      END;
    END;

    PROCEDURE StartExportBatch@7(OperationCode@1000 : Integer;SourceCode@1002 : Code[10];SettleDate@1003 : Date);
    VAR
      BatchHeaderRec@1004 : Text[422];
    BEGIN
      IF NOT FileIsInProcess THEN
        ERROR(Text004);
      IF BatchIsInProcess THEN
        ERROR(Text005);

      BatchIsInProcess := TRUE;
      BatchNo := BatchNo + 1;
      BatchHashTotal := 0;
      TotalBatchDebit := 0;
      TotalBatchCredit := 0;
      EntryAddendaCount := 0;
      TraceNo := 0;
      SequenceNo := 1;
      BatchHeaderRec := '';
      OpCode := OperationCode;
      EVALUATE(BatchDay,FORMAT(TODAY,2,'<Day>'));
      // Cecoban layout
      AddToPrnString(BatchHeaderRec,'1',1,2,Justification::Right,'0');                  // Record Type of Input "01" is Batch Header
      AddNumToPrnString(BatchHeaderRec,SequenceNo,3,7);                                          // Sequence Number
      AddNumToPrnString(BatchHeaderRec,OpCode,10,2);                                    // Operation Code
      AddToPrnString(BatchHeaderRec,BankAccount."Bank Account No.",12,3,Justification::Left,' ');    // Bank 3 digit #
      AddToPrnString(BatchHeaderRec,'E',15,1,Justification::Right,'');                    // Export Type
      AddNumToPrnString(BatchHeaderRec,2,16,1);                                          // Service
      AddNumToPrnString(BatchHeaderRec,BatchDay,17,2);                                  // Batch Number day of month part
      AddNumToPrnString(BatchHeaderRec,BatchNo,19,5);                                   // Batch Number sequence part
      AddToPrnString(BatchHeaderRec,FORMAT(SettleDate,0,'<Year,2><Month,2><Day,2>'),24,8,Justification::Right,'0');
      // Date of Presentation  AAAAMMDD
      AddToPrnString(BatchHeaderRec,'01',32,2,Justification::Left,' ');                    // Currency Code, 01 - MX peso 05 - USD
      AddNumToPrnString(BatchHeaderRec,0,34,2);                                           // Rejection code
      AddNumToPrnString(BatchHeaderRec,2,36,1);                                          // System
      AddToPrnString(BatchHeaderRec,'',37,41,Justification::Left,' ');                     // future Cecoban use
      AddToPrnString(BatchHeaderRec,'',78,345,Justification::Left,' ');                     // future Bank use
      ExportPrnString(BatchHeaderRec);
    END;

    PROCEDURE ExportElectronicPayment@17(GenJnlLine@1000 : Record 81;PaymentAmount@1001 : Decimal;SettleDate@1014 : Date) : Code[30];
    VAR
      Vendor@1002 : Record 23;
      VendorBankAcct@1003 : Record 288;
      Customer@1004 : Record 18;
      CustBankAcct@1005 : Record 287;
      AcctType@1006 : Text[1];
      AcctNo@1007 : Code[20];
      AcctName@1008 : Text[30];
      BankAcctNo@1009 : Text[30];
      TransitNo@1010 : Text[20];
      RFCNo@1013 : Text[20];
      DetailRec@1011 : Text[422];
      DemandCredit@1012 : Boolean;
    BEGIN
      IF NOT FileIsInProcess THEN
        ERROR(Text006);
      IF NOT BatchIsInProcess THEN
        ERROR(Text007);

      // NOTE:  If PaymentAmount is Positive, then we are Receiving money.
      //    If PaymentAmount is Negative, then we are Sending money.
      IF PaymentAmount = 0 THEN
        EXIT('');
      DemandCredit := (PaymentAmount < 0);
      PaymentAmount := ABS(PaymentAmount);

      WITH GenJnlLine DO BEGIN
        IF "Account Type" = "Account Type"::Vendor THEN BEGIN
          AcctType := 'V';
          AcctNo := "Account No.";
        END ELSE
          IF "Account Type" = "Account Type"::Customer THEN BEGIN
            AcctType := 'C';
            AcctNo := "Account No.";
          END ELSE
            IF "Bal. Account Type" = "Bal. Account Type"::Vendor THEN BEGIN
              AcctType := 'V';
              AcctNo := "Bal. Account No.";
            END ELSE
              IF "Bal. Account Type" = "Bal. Account Type"::Customer THEN BEGIN
                AcctType := 'C';
                AcctNo := "Bal. Account No.";
              END ELSE
                ERROR(Text018,
                  FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"),Vendor.TABLECAPTION,Customer.TABLECAPTION);

        IF AcctType = 'V' THEN BEGIN
          Vendor.GET(AcctNo);
          Vendor.TESTFIELD(Blocked,Vendor.Blocked::" ");
          AcctName := Vendor.Name;
          RFCNo := Vendor."VAT Registration No.";

          VendorBankAcct.SETRANGE("Vendor No.",AcctNo);
          VendorBankAcct.SETRANGE("Use for Electronic Payments",TRUE);
          VendorBankAcct.FINDFIRST;

          IF VendorBankAcct.COUNT < 1 THEN
            ERROR(Text008,AcctNo);
          IF VendorBankAcct.COUNT > 1 THEN
            ERROR(Text009,AcctNo);
          IF NOT PayeeCheckDigit(VendorBankAcct."Transit No.") THEN
            VendorBankAcct.FIELDERROR("Transit No.",Text001 + ' ' + Text1480004);

          VendorBankAcct.TESTFIELD("Bank Account No.");
          TransitNo := VendorBankAcct."Transit No.";
          BankAcctNo := VendorBankAcct."Bank Account No.";
        END ELSE
          IF AcctType = 'C' THEN BEGIN
            Customer.GET(AcctNo);
            IF Customer.Blocked = Customer.Blocked::All THEN
              ERROR(
                STRSUBSTNO(
                  Text1020100,
                  "Account Type",Customer.Blocked));
            AcctName := Customer.Name;
            RFCNo := Customer."VAT Registration No.";

            CustBankAcct.SETRANGE("Customer No.",AcctNo);
            CustBankAcct.SETRANGE("Use for Electronic Payments",TRUE);
            CustBankAcct.FINDFIRST;

            IF CustBankAcct.COUNT < 1 THEN
              ERROR(Text010,AcctNo);
            IF CustBankAcct.COUNT > 1 THEN
              ERROR(Text011,AcctNo);
            IF NOT PayeeCheckDigit(CustBankAcct."Transit No.") THEN
              CustBankAcct.FIELDERROR("Transit No.",Text001 + ' ' + Text1480004);
            CustBankAcct.TESTFIELD("Bank Account No.");
            TransitNo := CustBankAcct."Transit No.";
            BankAcctNo := CustBankAcct."Bank Account No.";
          END;

        TraceNo := TraceNo + 1;
        SequenceNo := SequenceNo + 1;
        DetailRec := '';
        // Cecoban Detail rec
        AddToPrnString(DetailRec,'2',1,2,Justification::Right,'0');                      // Record Type of Input "02" is Detail
        AddNumToPrnString(DetailRec,SequenceNo,3,7);                                     // Sequence Number
        AddNumToPrnString(DetailRec,OpCode,10,2);                                            // Operation Code
        AddToPrnString(DetailRec,'01',12,2,Justification::Left,' ');                    // Currency Code, 01 - MX peso 05 - USD
        AddToPrnString(DetailRec,FORMAT(FileDate,0,'<Year,4><Month,2><Day,2>'),14,8,Justification::Right,'0');
        // Transfer Date AAAAMMDD

        AddToPrnString(DetailRec,BankAccount."Bank Account No.",22,3,Justification::Left,' ');                    // ODFI
        AddToPrnString(DetailRec,BankAcctNo,25,3,Justification::Left,' ');                // RDFI
        AddAmtToPrnString(DetailRec,PaymentAmount,28,15);                                           // Operation Fee
        AddToPrnString(DetailRec,' ',43,16,Justification::Left,' ');                    // Future use
        AddNumToPrnString(DetailRec,OpCode,59,2);                                        // Operation Type
        AddToPrnString(DetailRec,FORMAT(SettleDate,0,'<Year,2><Month,2><Day,2>'),61,8,Justification::Right,'0');
        // Date Entered AAAAMMDD
        AddNumToPrnString(DetailRec,1,69,2);                    // '?????' Originator Account Type
        AddToPrnString(DetailRec,BankAccount."Transit No.",71,20,Justification::Left,'');          // Originator Account No.
        AddToPrnString(DetailRec,AcctName,91,40,Justification::Left,'');                    // Originator Account Name
        AddToPrnString(DetailRec,'',131,18,Justification::Left,'');                   // Originator RFC/CURP
        AddNumToPrnString(DetailRec,PayeeAcctType,149,2);                    // Payee Account Type
        AddToPrnString(DetailRec,TransitNo,151,20,Justification::Left,'');              // Payee Account No.
        AddToPrnString(DetailRec,AcctName,171,40,Justification::Left,'');                    // Payee Account Name
        AddToPrnString(DetailRec,RFCNo,211,18,Justification::Left,'');                   // Payee RFC/CURP
        AddToPrnString(DetailRec,'',229,40,Justification::Left,'');                   // Transmitter Service Reference
        AddToPrnString(DetailRec,'',269,40,Justification::Left,'');                   // Service Owner
        AddAmtToPrnString(DetailRec,0,309,15);                                            // Operation Tax Cost
        AddNumToPrnString(DetailRec,0,324,7);                                       // Originator Numeric reference
        AddToPrnString(DetailRec,'',331,40,Justification::Left,'');                   // Originator alpha reference
        AddToPrnString(DetailRec,GenerateTraceNoCode(TraceNo),371,30,Justification::Left,'');                   // Tracking code
        AddNumToPrnString(DetailRec,0,401,2);                                            // Return Reason
        AddToPrnString(DetailRec,FORMAT(TODAY,0,'<Year><Month,2><Day,2>'),403,8,Justification::Left,' ');   // Initial Presentation Date
        AddToPrnString(DetailRec,'',411,12,Justification::Left,' ');                   // future use

        ExportPrnString(DetailRec);
        EntryAddendaCount := EntryAddendaCount + 1;
        IF DemandCredit THEN
          TotalBatchCredit := TotalBatchCredit + PaymentAmount
        ELSE
          TotalBatchDebit := TotalBatchDebit + PaymentAmount;
        IncrementHashTotal(BatchHashTotal,MakeHash(COPYSTR(TransitNo,1,8)));
      END;

      EXIT(GenerateFullTraceNoCode(TraceNo));
    END;

    PROCEDURE EndExportBatch@9();
    VAR
      BatchControlRec@1001 : Text[422];
    BEGIN
      IF NOT FileIsInProcess THEN
        ERROR(Text012);
      IF NOT BatchIsInProcess THEN
        ERROR(Text013);

      BatchIsInProcess := FALSE;
      BatchControlRec := '';
      SequenceNo := SequenceNo + 1;
      // cecoban batch summary
      AddNumToPrnString(BatchControlRec,9,1,2);                  // Record Type
      AddNumToPrnString(BatchControlRec,SequenceNo,3,7);                  // sequence number
      AddNumToPrnString(BatchControlRec,OpCode,10,2);                  // op code
      AddNumToPrnString(BatchControlRec,BatchDay,12,2);                  // Batch Number day of month part
      AddNumToPrnString(BatchControlRec,BatchNo,14,5);                  // batch number sequence part
      AddNumToPrnString(BatchControlRec,SequenceNo - 2,19,7);          // operation number
      AddAmtToPrnString(BatchControlRec,BatchHashTotal,26,18);                  // TCO
      AddToPrnString(BatchControlRec,' ',44,40,Justification::Left,' ');            // future use
      AddToPrnString(BatchControlRec,' ',84,339,Justification::Left,' ');            // future use
      ExportPrnString(BatchControlRec);

      BatchCount := BatchCount + 1;
      IncrementHashTotal(FileHashTotal,BatchHashTotal);
      TotalFileDebit := TotalFileDebit + TotalBatchDebit;
      TotalFileCredit := TotalFileCredit + TotalBatchCredit;
      FileEntryAddendaCount := FileEntryAddendaCount + EntryAddendaCount;
    END;

    PROCEDURE EndExportFile@10() : Boolean;
    VAR
      ClientFile@1020000 : Text;
    BEGIN
      IF NOT FileIsInProcess THEN
        ERROR(Text014);
      IF BatchIsInProcess THEN
        ERROR(Text015);

      FileIsInProcess := FALSE;
      ExportFile.CLOSE;

      ClientFile := BankAccount."E-Pay Export File Path" + BankAccount."Last E-Pay Export File Name";
      RBMgt.DownloadToFile(FileName,ClientFile);
      ERASE(FileName);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GenerateFullTraceNoCode@14(TraceNo@1000 : Integer) : Code[30];
    VAR
      TraceCode@1001 : Text[250];
    BEGIN
      TraceCode := '';
      AddToPrnString(TraceCode,FORMAT(FileDate,0,'<Year,2><Month,2><Day,2>'),1,6,Justification::Left,' ');
      AddToPrnString(TraceCode,BankAccount."Last ACH File ID Modifier",7,1,Justification::Right,'0');
      AddNumToPrnString(TraceCode,BatchNo,8,7);
      AddToPrnString(TraceCode,GenerateTraceNoCode(TraceNo),15,15,Justification::Left,' ');
      EXIT(TraceCode);
    END;

    LOCAL PROCEDURE GenerateTraceNoCode@15(TraceNo@1000 : Integer) : Code[15];
    VAR
      TraceCode@1001 : Text[250];
    BEGIN
      TraceCode := '';
      AddToPrnString(TraceCode,BankAccount."Transit No.",1,8,Justification::Left,' ');
      AddNumToPrnString(TraceCode,TraceNo,9,7);
      EXIT(TraceCode);
    END;

    LOCAL PROCEDURE AddNumToPrnString@12(VAR PrnString@1000 : Text[422];Number@1001 : Integer;StartPos@1002 : Integer;Length@1003 : Integer);
    VAR
      TmpString@1004 : Text[422];
    BEGIN
      TmpString := DELCHR(FORMAT(Number),'=','.,-');
      AddToPrnString(PrnString,TmpString,StartPos,Length,Justification::Right,'0');
    END;

    LOCAL PROCEDURE AddAmtToPrnString@3(VAR PrnString@1000 : Text[422];Amount@1001 : Decimal;StartPos@1002 : Integer;Length@1003 : Integer);
    VAR
      TmpString@1004 : Text[422];
      I@1005 : Integer;
    BEGIN
      TmpString := FORMAT(Amount);
      I := STRPOS(TmpString,'.');
      CASE TRUE OF
        I = 0:
          TmpString := TmpString + '.00';
        I = STRLEN(TmpString) - 1:
          TmpString := TmpString + '0';
      END;
      TmpString := DELCHR(TmpString,'=','.,-');
      AddToPrnString(PrnString,TmpString,StartPos,Length,Justification::Right,'0');
    END;

    LOCAL PROCEDURE AddToPrnString@4(VAR PrnString@1000 : Text[422];SubString@1001 : Text[345];StartPos@1002 : Integer;Length@1003 : Integer;Justification@1004 : 'Left,Right';Filler@1005 : Text[1]);
    VAR
      I@1007 : Integer;
      SubStrLen@1008 : Integer;
    BEGIN
      SubString := UPPERCASE(DELCHR(SubString,'<>',' '));
      SubStrLen := STRLEN(SubString);

      IF SubStrLen > Length THEN BEGIN
        SubString := COPYSTR(SubString,1,Length);
        SubStrLen := Length;
      END;

      IF Justification = Justification::Right THEN
        FOR I := 1 TO (Length - SubStrLen) DO
          SubString := Filler + SubString
      ELSE
        FOR I := SubStrLen + 1 TO Length DO
          SubString := SubString + Filler;

      IF STRLEN(PrnString) >= StartPos THEN
        IF StartPos > 1 THEN
          PrnString := COPYSTR(PrnString,1,StartPos - 1) + SubString + COPYSTR(PrnString,StartPos)
        ELSE
          PrnString := SubString + PrnString
      ELSE BEGIN
        FOR I := STRLEN(PrnString) + 1 TO StartPos - 1 DO
          PrnString := PrnString + ' ';
        PrnString := PrnString + SubString;
      END;
    END;

    LOCAL PROCEDURE ExportPrnString@11(VAR PrnString@1000 : Text[422]);
    BEGIN
      PrnString := PADSTR(PrnString,RecordLength,' ');
      ExportFile.WRITE(PrnString);
      NoOfRec := NoOfRec + 1;
      PrnString := '';
    END;

    PROCEDURE PayeeCheckDigit@2(DigitString@1000 : Code[20]) : Boolean;
    BEGIN
      IF STRLEN(DigitString) = 18 THEN BEGIN
        PayeeAcctType := 1;
        EXIT(TRUE);                                   // checking Account
      END;
      IF STRLEN(DigitString) = 16 THEN BEGIN
        PayeeAcctType := 3;                                   // debit card
        EXIT(TRUE);                                   // checking Account
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE IncrementHashTotal@13(VAR HashTotal@1000 : Decimal;HashIncrement@1001 : Decimal);
    VAR
      SubTotal@1002 : Decimal;
    BEGIN
      SubTotal := HashTotal + HashIncrement;
      IF SubTotal < 10000000000.0 THEN
        HashTotal := SubTotal
      ELSE
        HashTotal := SubTotal - 10000000000.0;
    END;

    LOCAL PROCEDURE MakeHash@21(InputString@1000 : Text[30]) : Decimal;
    VAR
      HashAmt@1001 : Decimal;
    BEGIN
      InputString := DELCHR(InputString,'=','.,- ');
      IF EVALUATE(HashAmt,InputString) THEN
        EXIT(HashAmt);

      EXIT(0);
    END;

    LOCAL PROCEDURE BuildIDModifier@5();
    BEGIN
      ModifierValues[1] := 'A';
      ModifierValues[2] := 'B';
      ModifierValues[3] := 'C';
      ModifierValues[4] := 'D';
      ModifierValues[5] := 'E';
      ModifierValues[6] := 'F';
      ModifierValues[7] := 'G';
      ModifierValues[8] := 'H';
      ModifierValues[9] := 'I';
      ModifierValues[10] := 'J';
      ModifierValues[11] := 'K';
      ModifierValues[12] := 'L';
      ModifierValues[13] := 'M';
      ModifierValues[14] := 'N';
      ModifierValues[15] := 'O';
      ModifierValues[16] := 'P';
      ModifierValues[17] := 'Q';
      ModifierValues[18] := 'R';
      ModifierValues[19] := 'S';
      ModifierValues[20] := 'T';
      ModifierValues[21] := 'U';
      ModifierValues[22] := 'V';
      ModifierValues[23] := 'W';
      ModifierValues[24] := 'X';
      ModifierValues[25] := 'Y';
      ModifierValues[26] := 'Z';
    END;

    PROCEDURE TransmitExportedFile@16(BankAccountNo@1000 : Code[20];FName@1001 : Text);
    VAR
      ExportFullPathName@1002 : Text;
      TransmitFullPathName@1003 : Text;
    BEGIN
      WITH BankAccount DO BEGIN
        GET(BankAccountNo);
        TESTFIELD("E-Pay Export File Path");
        IF "E-Pay Export File Path"[STRLEN("E-Pay Export File Path")] <> '\' THEN
          ERROR(Text002,
            FIELDCAPTION("E-Pay Export File Path"),
            TABLECAPTION,
            "No.");
        TESTFIELD("E-Pay Trans. Program Path");
        IF "E-Pay Trans. Program Path"[STRLEN("E-Pay Trans. Program Path")] <> '\' THEN
          ERROR(Text002,
            FIELDCAPTION("E-Pay Trans. Program Path"),
            TABLECAPTION,
            "No.");
        ExportFullPathName := "E-Pay Export File Path" + FName;
        TransmitFullPathName := "E-Pay Trans. Program Path" + FName;

        IF NOT RBMgt.ClientFileExists(ExportFullPathName) THEN
          ERROR(Text016,FName);
        RBMgt.CopyClientFile(ExportFullPathName,TransmitFullPathName,TRUE);

        IF CONFIRM(Text019,TRUE,FName,"E-Pay Trans. Program Path",Name,TABLECAPTION,FIELDCAPTION("No."),"No.") THEN
          IF CONFIRM(Text017) THEN
            RBMgt.DeleteClientFile(ExportFullPathName);
      END;
    END;

    PROCEDURE ExportFileName@1480000() : Text[30];
    VAR
      FileName@1020000 : Text[30];
    BEGIN
      IF BankAccount."Last E-Pay Export File Name" = '' THEN BEGIN
        FileName := '';
        AddToPrnString(FileName,'S01',1,3,Justification::Right,'');                      // Record Type of Input "02" is Detail
        AddToPrnString(FileName,BankAccount."Bank Account No.",4,3,Justification::Left,' ');    // Bank 3 digit #
        AddToPrnString(FileName,'A2.A10 ',7,6,Justification::Right,'');
        AddToPrnString(FileName,FORMAT(TODAY,2,'0' + '<weekDay>'),13,2,Justification::Right,''); // weekday
        AddNumToPrnString(FileName,BatchNo + 1,15,2);                                     // Sequence Number
      END ELSE BEGIN
        FileName := BankAccount."Last E-Pay Export File Name";
        AddToPrnString(FileName,FORMAT(TODAY,2,'0' + '<weekDay>'),13,2,Justification::Right,''); // weekday
        AddNumToPrnString(FileName,BatchNo + 1,15,2);                                     // Sequence Number
      END;
      EXIT(FileName);
    END;

    BEGIN
    END.
  }
}

