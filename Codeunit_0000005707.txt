OBJECT Codeunit 5707 TransferOrder-Post + Print
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    TableNo=5740;
    OnRun=BEGIN
            TransHeader.COPY(Rec);
            Code;
            Rec := TransHeader;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=&Ship,&Receive;ESM=&Enviar,&Recibir;FRC=&Livrer,&R‚ceptionner;ENC=&Ship,&Receive';
      TransHeader@1001 : Record 5740;
      TransShptHeader@1002 : Record 5744;
      TransRcptHeader@1003 : Record 5746;

    LOCAL PROCEDURE Code@2();
    VAR
      TransLine@1000 : Record 5741;
      DefaultNumber@1001 : Integer;
      TransferPostShipment@1002 : Codeunit 5704;
      TransferPostReceipt@1003 : Codeunit 5705;
      Selection@1004 : ' ,Shipment,Receipt';
    BEGIN
      WITH TransHeader DO BEGIN
        TransLine.SETRANGE("Document No.","No.");
        IF TransLine.FIND('-') THEN
          REPEAT
            IF (TransLine."Quantity Shipped" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := 1;
            IF (TransLine."Quantity Received" < TransLine.Quantity) AND
               (DefaultNumber = 0)
            THEN
              DefaultNumber := 2;
          UNTIL (TransLine.NEXT = 0) OR (DefaultNumber > 0);

        IF DefaultNumber = 0 THEN
          DefaultNumber := 1;
        Selection := STRMENU(Text000,DefaultNumber);
        CASE Selection OF
          0:
            EXIT;
          1:
            BEGIN
              TransferPostShipment.RUN(TransHeader);
              TransShptHeader."No." := "Last Shipment No.";
              TransShptHeader.SETRECFILTER;
              TransShptHeader.PrintRecords(FALSE);
            END;
          2:
            BEGIN
              TransferPostReceipt.RUN(TransHeader);
              TransRcptHeader."No." := "Last Receipt No.";
              TransRcptHeader.SETRECFILTER;
              TransRcptHeader.PrintRecords(FALSE);
            END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

