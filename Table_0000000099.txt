OBJECT Table 99 Item Vendor
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               xRec.INIT;
               UpdateItemCrossReference;
             END;

    OnModify=BEGIN
               UpdateItemCrossReference;
             END;

    OnDelete=BEGIN
               DeleteItemCrossReference;
             END;

    OnRename=BEGIN
               UpdateItemCrossReference;
             END;

    CaptionML=[ENU=Item Vendor;
               ESM=Tarifa compra producto;
               FRC=Fournisseur de l'article;
               ENC=Item Vendor];
    LookupPageID=Page114;
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESM=N§ producto;
                                                              FRC=Nø d'article;
                                                              ENC=Item No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   OnValidate=BEGIN
                                                                Vend.GET("Vendor No.");
                                                                "Lead Time Calculation" := Vend."Lead Time Calculation";
                                                              END;

                                                   CaptionML=[ENU=Vendor No.;
                                                              ESM=N§ proveedor;
                                                              FRC=Nø fournisseur;
                                                              ENC=Vendor No.];
                                                   NotBlank=Yes }
    { 6   ;   ;Lead Time Calculation;DateFormula  ;CaptionML=[ENU=Lead Time Calculation;
                                                              ESM=Plazo entrega (d¡as);
                                                              FRC=D‚lai de r‚approvisionnement;
                                                              ENC=Lead Time Calculation] }
    { 7   ;   ;Vendor Item No.     ;Text20        ;CaptionML=[ENU=Vendor Item No.;
                                                              ESM=C¢d. producto proveedor;
                                                              FRC=Nø article fournisseur;
                                                              ENC=Vendor Item No.] }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ESM=C¢d. variante;
                                                              FRC=Code variante;
                                                              ENC=Variant Code] }
  }
  KEYS
  {
    {    ;Vendor No.,Item No.,Variant Code        ;Clustered=Yes }
    {    ;Item No.,Variant Code,Vendor No.         }
    {    ;Vendor No.,Vendor Item No.               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Vend@1000 : Record 23;
      DistIntegration@1001 : Codeunit 5702;

    PROCEDURE DeleteItemCrossReference@2();
    BEGIN
      IF CheckItemCrossRefLicense THEN
        IF ("Vendor No." <> '') AND ("Item No." <> '') THEN
          DistIntegration.DeleteItemCrossReference(Rec);
    END;

    PROCEDURE UpdateItemCrossReference@6();
    BEGIN
      IF CheckItemCrossRefLicense THEN
        IF ("Vendor No." <> '') AND ("Item No." <> '') THEN
          DistIntegration.UpdateItemCrossReference(Rec,xRec);
    END;

    PROCEDURE CheckItemCrossRefLicense@3() : Boolean;
    VAR
      LicensePermission@1000 : Record 2000000043;
    BEGIN
      LicensePermission.SETRANGE("Object Type",LicensePermission."Object Type"::Table);
      LicensePermission.SETRANGE("Object Number",DATABASE::"Item Cross Reference");
      LicensePermission.SETFILTER("Insert Permission",'<>%1',LicensePermission."Insert Permission"::" ");
      LicensePermission.SETFILTER("Execute Permission",'<>%1',LicensePermission."Execute Permission"::" ");
      IF LicensePermission.FINDFIRST THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

