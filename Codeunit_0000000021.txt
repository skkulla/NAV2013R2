OBJECT Codeunit 21 Item Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    TableNo=83;
    OnRun=BEGIN
            GLSetup.GET;
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be a closing date;ESM=no puede ser una fecha £ltima;FRC=ne peut ˆtre une date de fermeture;ENC=cannot be a closing date';
      Text001@1001 : TextConst 'ENU=is not within your range of allowed posting dates;ESM=no est  dentro del periodo de fechas de registro permitidas;FRC=n''est pas dans l''intervalle de dates de report permis;ENC=is not within your range of allowed posting dates';
      Text003@1002 : TextConst 'ENU=must not be negative when %1 is %2;ESM=no puede ser negativo cuando %1 es %2;FRC=ne doit pas ˆtre n‚gatif quand le %1 est %2;ENC=must not be negative when %1 is %2';
      Text004@1003 : TextConst 'ENU=must have the same value as %1;ESM=debe tener el mismo valor que %1;FRC=doit avoir la mˆme valeur que %1;ENC=must have the same value as %1';
      Text005@1004 : TextConst 'ENU=must be %1 or %2 when %3 is %4;ESM=debe ser %1 o %2 cuando %3 sea %4.;FRC=doit ˆtre %1 ou %2 lorsque %3 est %4;ENC=must be %1 or %2 when %3 is %4';
      Text006@1005 : TextConst 'ENU=must equal %1 - %2 when %3 is %4 and %5 is %6;ESM=debe igualar %1 - %2 cuando %3 es %4 y %5 es %6;FRC=doit ˆtre ‚gal … %1 - %2 quand %3 est %4 et %5 est %6;ENC=must equal %1 - %2 when %3 is %4 and %5 is %6';
      Text007@1006 : TextConst 'ENU="You cannot post these lines because you have not entered a quantity on one or more of the lines. ";ESM="No puede registrar estas l¡neas porque no ha insertado la cantidad en una o m s l¡neas. ";FRC="Vous ne pouvez pas valider ces lignes car vous n''avez pas entr‚ de quantit‚ sur une ou plusieurs ligne(s). ";ENC="You cannot post these lines because you have not entered a quantity on one or more of the lines. "';
      Text009@1008 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5;ESM=La combin. de dimensiones utilizada en %1 %2, %3, %4 est  bloq. %5;FRC=La combinaison de dimensions utilis‚e dans %1 %2, %3, %4 est bloqu‚e. %5;ENC=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text010@1009 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5;ESM=La dimensi¢n util. en %1 %2, %3, %4 ha causado error. %5;FRC=Une erreur a ‚t‚ caus‚e par une dimension utilis‚e dans %1 %2, %3, %4. %5;ENC=A dimension used in %1 %2, %3, %4 has caused an error. %5';
      Text011@1019 : TextConst 'ENU=%1 must not be equal to %2;ESM=%1 no debe ser igual a %2;FRC=%1 ne doit pas ˆtre ‚gal … %2;ENC=%1 must not be equal to %2';
      Location@1018 : Record 14;
      InvtSetup@1010 : Record 313;
      GLSetup@1011 : Record 98;
      UserSetup@1012 : Record 91;
      ItemLedgEntry@1007 : Record 32;
      ItemJnlLine2@1013 : Record 83;
      ItemJnlLine3@1014 : Record 83;
      ProdOrderLine@1023 : Record 5406;
      DimMgt@1015 : Codeunit 408;
      AllowPostingFrom@1016 : Date;
      AllowPostingTo@1017 : Date;
      Text012@1021 : TextConst 'ENU="Warehouse handling is required for %1 = %2, %3 = %4, %5 = %6.";ESM="Manipulaci¢n almac‚n requerido para %1 = %2, %3 = %4, %5 = %6.";FRC="Manutention … l''entrep“t requise pour %1 = %2, %3 = %4, %5 = %6.";ENC="Warehouse handling is required for %1 = %2, %3 = %4, %5 = %6."';
      CalledFromInvtPutawayPick@1022 : Boolean;
      CalledFromAdjustment@1020 : Boolean;

    PROCEDURE RunCheck@1(VAR ItemJnlLine@1000 : Record 83);
    VAR
      InvtPeriod@1004 : Record 5814;
      WorkCenter@1005 : Record 99000754;
      Item@1001 : Record 27;
      TableID@1003 : ARRAY [10] OF Integer;
      No@1002 : ARRAY [10] OF Code[20];
    BEGIN
      WITH ItemJnlLine DO BEGIN
        IF EmptyLine THEN BEGIN
          IF NOT IsValueEntryForDeletedItem THEN
            EXIT;
        END ELSE
          IF NOT OnlyStopTime THEN
            TESTFIELD("Item No.");

        IF Item.GET("Item No.") THEN
          Item.TESTFIELD("Base Unit of Measure");

        TESTFIELD("Posting Date");
        TESTFIELD("Document No.");
        IF "Posting Date" <> NORMALDATE("Posting Date") THEN
          FIELDERROR("Posting Date",Text000);

        CheckAllowedPostingDate(ItemJnlLine);

        IF NOT InvtPeriod.IsValidDate("Posting Date") THEN
          InvtPeriod.ShowError("Posting Date");

        IF "Document Date" <> 0D THEN
          IF "Document Date" <> NORMALDATE("Document Date") THEN
            FIELDERROR("Document Date",Text000);

        TESTFIELD("Gen. Prod. Posting Group");

        IF InvtSetup."Location Mandatory" AND
           ("Value Entry Type" = "Value Entry Type"::"Direct Cost") AND
           (Quantity <> 0) AND
           NOT Adjustment
        THEN BEGIN
          IF (Type <> Type::Resource) AND (Item.Type = Item.Type::Inventory) THEN
            TESTFIELD("Location Code");
          IF "Entry Type" = "Entry Type"::Transfer THEN
            TESTFIELD("New Location Code");
        END;

        IF ("Item Charge No." = '') AND
           ("Value Entry Type" = "Value Entry Type"::"Direct Cost") AND
           (Quantity <> 0) AND
           NOT Adjustment
        THEN BEGIN
          GetLocation("Location Code");
          IF Location."Bin Mandatory" AND
             NOT Location."Directed Put-away and Pick" AND
             NOT "Drop Shipment"
          THEN BEGIN
            IF ("Entry Type" <> "Entry Type"::Output) OR
               LastOutputOperation(ItemJnlLine)
            THEN
              TESTFIELD("Bin Code");
          END;
          IF "Entry Type" = "Entry Type"::Transfer THEN BEGIN
            GetLocation("New Location Code");
            IF Location."Bin Mandatory" AND NOT Location."Directed Put-away and Pick" THEN
              TESTFIELD("New Bin Code");
          END;
        END;

        IF "Entry Type" IN ["Entry Type"::"Positive Adjmt.","Entry Type"::"Negative Adjmt."] THEN
          TESTFIELD("Discount Amount",0);

        IF "Entry Type" = "Entry Type"::Transfer THEN BEGIN
          IF ("Value Entry Type" = "Value Entry Type"::"Direct Cost") AND
             ("Item Charge No." = '') AND
             NOT Adjustment
          THEN
            TESTFIELD(Amount,0);
          TESTFIELD("Discount Amount",0);
          IF Quantity < 0 THEN
            FIELDERROR(Quantity,STRSUBSTNO(Text003,FIELDCAPTION("Entry Type"),"Entry Type"));
          IF Quantity <> "Invoiced Quantity" THEN
            FIELDERROR("Invoiced Quantity",STRSUBSTNO(Text004,FIELDCAPTION(Quantity)));
        END;

        IF NOT "Phys. Inventory" THEN BEGIN
          IF "Entry Type" = "Entry Type"::Output THEN BEGIN
            IF ("Output Quantity (Base)" = 0) AND ("Scrap Quantity (Base)" = 0) AND
               TimeIsEmpty AND ("Invoiced Qty. (Base)" = 0)
            THEN
              ERROR(Text007)
          END ELSE BEGIN
            IF ("Quantity (Base)" = 0) AND ("Invoiced Qty. (Base)" = 0) THEN
              ERROR(Text007);
          END;
          TESTFIELD("Qty. (Calculated)",0);
          TESTFIELD("Qty. (Phys. Inventory)",0);
        END ELSE BEGIN
          IF NOT
             ("Entry Type" IN
              ["Entry Type"::"Positive Adjmt.",
               "Entry Type"::"Negative Adjmt."])
          THEN BEGIN
            ItemJnlLine2."Entry Type" := ItemJnlLine2."Entry Type"::"Positive Adjmt.";
            ItemJnlLine3."Entry Type" := ItemJnlLine3."Entry Type"::"Negative Adjmt.";
            FIELDERROR(
              "Entry Type",
              STRSUBSTNO(
                Text005,
                ItemJnlLine2."Entry Type",
                ItemJnlLine3."Entry Type",
                FIELDCAPTION("Phys. Inventory"),
                TRUE));
          END;
          IF ("Entry Type" = "Entry Type"::"Positive Adjmt.") AND
             ("Qty. (Phys. Inventory)" - "Qty. (Calculated)" <> Quantity)
          THEN
            FIELDERROR(
              Quantity,
              STRSUBSTNO(
                Text006,
                FIELDCAPTION("Qty. (Phys. Inventory)"),
                FIELDCAPTION("Qty. (Calculated)"),
                FIELDCAPTION("Entry Type"),
                "Entry Type",
                FIELDCAPTION("Phys. Inventory"),
                TRUE));
          IF ("Entry Type" = "Entry Type"::"Negative Adjmt.") AND
             ("Qty. (Calculated)" - "Qty. (Phys. Inventory)" <> Quantity)
          THEN
            FIELDERROR(
              Quantity,
              STRSUBSTNO(
                Text006,
                FIELDCAPTION("Qty. (Calculated)"),
                FIELDCAPTION("Qty. (Phys. Inventory)"),
                FIELDCAPTION("Entry Type"),
                "Entry Type",
                FIELDCAPTION("Phys. Inventory"),
                TRUE));
        END;

        IF "Entry Type" <> "Entry Type"::Output THEN BEGIN
          TESTFIELD("Run Time",0);
          TESTFIELD("Setup Time",0);
          TESTFIELD("Stop Time",0);
          TESTFIELD("Output Quantity",0);
          TESTFIELD("Scrap Quantity",0);
        END;

        IF "Applies-from Entry" <> 0 THEN BEGIN
          ItemLedgEntry.GET("Applies-from Entry");
          ItemLedgEntry.TESTFIELD("Item No.","Item No.");
          ItemLedgEntry.TESTFIELD("Variant Code","Variant Code");
          ItemLedgEntry.TESTFIELD(Positive,FALSE);
          IF "Applies-to Entry" = "Applies-from Entry" THEN
            ERROR(
              Text011,
              FIELDCAPTION("Applies-to Entry"),
              FIELDCAPTION("Applies-from Entry"));
        END;

        IF ("Entry Type" IN ["Entry Type"::Consumption,"Entry Type"::Output]) AND
           NOT ("Value Entry Type" = "Value Entry Type"::Revaluation) AND
           NOT OnlyStopTime
        THEN BEGIN
          TESTFIELD("Source No.");
          TESTFIELD("Order Type","Order Type"::Production);
          IF Subcontracting THEN BEGIN
            CheckFindProdOrderLine("Order No.","Order Line No.");
            TESTFIELD("Item No.",ProdOrderLine."Item No.");
            WorkCenter.GET("Work Center No.");
            WorkCenter.TESTFIELD("Subcontractor No.");
          END;
          IF NOT CalledFromInvtPutawayPick THEN
            CheckWarehouse(ItemJnlLine);
        END;

        IF "Entry Type" = "Entry Type"::"Assembly Consumption" THEN
          CheckWarehouse(ItemJnlLine);

        IF ("Value Entry Type" <> "Value Entry Type"::"Direct Cost") OR ("Item Charge No." <> '') THEN
          IF "Inventory Value Per" = "Inventory Value Per"::" " THEN
            TESTFIELD("Applies-to Entry");

        IF NOT IsValueEntryForDeletedItem AND NOT Correction AND NOT CalledFromAdjustment THEN BEGIN
          IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
            ERROR(
              Text009,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimCombErr);

          TableID[1] := DATABASE::Item;
          No[1] := "Item No.";
          TableID[2] := DATABASE::"Salesperson/Purchaser";
          No[2] := "Salespers./Purch. Code";
          TableID[3] := DATABASE::"Work Center";
          No[3] := "Work Center No.";
          IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN
            IF "Line No." <> 0 THEN
              ERROR(
                Text010,
                TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
                DimMgt.GetDimValuePostingErr)
            ELSE
              ERROR(DimMgt.GetDimValuePostingErr);
          IF ("Entry Type" = "Entry Type"::Transfer) AND
             ("Value Entry Type" <> "Value Entry Type"::Revaluation)
          THEN
            IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
              IF "Line No." <> 0 THEN
                ERROR(
                  Text010,
                  TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
                  DimMgt.GetDimValuePostingErr)
              ELSE
                ERROR(DimMgt.GetDimValuePostingErr);
        END;
      END;
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    LOCAL PROCEDURE CheckFindProdOrderLine@11(ProdOrderNo@1000 : Code[20];LineNo@1001 : Integer);
    BEGIN
      ProdOrderLine.RESET;
      ProdOrderLine.SETFILTER(Status,'>=%1',ProdOrderLine.Status::Released);
      ProdOrderLine.SETRANGE("Prod. Order No.",ProdOrderNo);
      ProdOrderLine.SETRANGE("Line No.",LineNo);
      ProdOrderLine.FINDFIRST;
    END;

    PROCEDURE SetCalledFromInvtPutawayPick@47(NewCalledFromInvtPutawayPick@1000 : Boolean);
    BEGIN
      CalledFromInvtPutawayPick := NewCalledFromInvtPutawayPick;
    END;

    LOCAL PROCEDURE CheckWarehouse@7301(ItemJnlLine@1000 : Record 83);
    VAR
      AssemblyLine@1001 : Record 901;
      ReservationEntry@1004 : Record 337;
      ItemJnlLineReserve@1005 : Codeunit 99000835;
      ReservEngineMgt@1006 : Codeunit 99000831;
      WhseValidateSourceLine@1003 : Codeunit 5777;
      ShowError@1002 : Boolean;
    BEGIN
      IF (ItemJnlLine.Quantity = 0) OR
         (ItemJnlLine."Item Charge No." <> '') OR
         (ItemJnlLine."Value Entry Type" IN
          [ItemJnlLine."Value Entry Type"::Revaluation,ItemJnlLine."Value Entry Type"::Rounding]) OR
         ItemJnlLine.Adjustment
      THEN
        EXIT;

      GetLocation(ItemJnlLine."Location Code");
      IF Location."Directed Put-away and Pick" THEN
        EXIT;

      CASE ItemJnlLine."Entry Type" OF
        ItemJnlLine."Entry Type"::Output:
          IF WhseOrderHandlingReqd(ItemJnlLine,Location) THEN
            IF ItemJnlLine."Applies-to Entry" = 0 THEN BEGIN
              ReservEngineMgt.InitFilterAndSortingLookupFor(ReservationEntry,FALSE);
              ItemJnlLineReserve.FilterReservFor(ReservationEntry,ItemJnlLine);
              ReservationEntry.SETRANGE("Serial No."); // Ignore Serial No
              ReservationEntry.SETRANGE("Lot No."); // Ignore Lot No
              IF ReservationEntry.FINDSET THEN
                REPEAT
                  IF ReservationEntry."Appl.-to Item Entry" = 0 THEN
                    ShowError := TRUE;
                UNTIL (ReservationEntry.NEXT = 0) OR ShowError
              ELSE
                ShowError := ItemJnlLine.LastOutputOperation(ItemJnlLine);
            END;
        ItemJnlLine."Entry Type"::Consumption:
          IF WhseOrderHandlingReqd(ItemJnlLine,Location) THEN
            IF WhseValidateSourceLine.WhseLinesExist(
                 DATABASE::"Prod. Order Component",
                 3,
                 ItemJnlLine."Order No.",
                 ItemJnlLine."Order Line No.",
                 ItemJnlLine."Prod. Order Comp. Line No.",
                 ItemJnlLine.Quantity)
            THEN
              ShowError := TRUE;
        ItemJnlLine."Entry Type"::"Assembly Consumption":
          IF WhseOrderHandlingReqd(ItemJnlLine,Location) THEN
            IF WhseValidateSourceLine.WhseLinesExist(
                 DATABASE::"Assembly Line",
                 AssemblyLine."Document Type"::Order,
                 ItemJnlLine."Order No.",
                 ItemJnlLine."Order Line No.",
                 0,
                 ItemJnlLine.Quantity)
            THEN
              ShowError := TRUE;
      END;
      IF ShowError THEN
        ERROR(
          Text012,
          ItemJnlLine.FIELDCAPTION("Entry Type"),
          ItemJnlLine."Entry Type",
          ItemJnlLine.FIELDCAPTION("Order No."),
          ItemJnlLine."Order No.",
          ItemJnlLine.FIELDCAPTION("Order Line No."),
          ItemJnlLine."Order Line No.");
    END;

    LOCAL PROCEDURE WhseOrderHandlingReqd@3(ItemJnlLine@1000 : Record 83;Location@1001 : Record 14) : Boolean;
    VAR
      InvtPutAwayLocation@1002 : Boolean;
      InvtPickLocation@1003 : Boolean;
    BEGIN
      InvtPutAwayLocation := NOT Location."Require Receive" AND Location."Require Put-away";
      IF InvtPutAwayLocation THEN
        CASE ItemJnlLine."Entry Type" OF
          ItemJnlLine."Entry Type"::Output:
            IF ItemJnlLine.Quantity >= 0 THEN
              EXIT(TRUE);
          ItemJnlLine."Entry Type"::Consumption,
          ItemJnlLine."Entry Type"::"Assembly Consumption":
            IF ItemJnlLine.Quantity < 0 THEN
              EXIT(TRUE);
        END;

      InvtPickLocation := NOT Location."Require Shipment" AND Location."Require Pick" ;
      IF InvtPickLocation THEN
        CASE ItemJnlLine."Entry Type" OF
          ItemJnlLine."Entry Type"::Output:
            IF ItemJnlLine.Quantity < 0 THEN
              EXIT(TRUE);
          ItemJnlLine."Entry Type"::Consumption,
          ItemJnlLine."Entry Type"::"Assembly Consumption":
            IF ItemJnlLine.Quantity >= 0 THEN
              EXIT(TRUE);
        END;

      EXIT(FALSE);
    END;

    PROCEDURE CheckAllowedPostingDate@2(ItemJnlLine@1000 : Record 83);
    BEGIN
      WITH ItemJnlLine DO BEGIN
        IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
          InvtSetup.GET;
          GLSetup.GET;
          IF USERID <> '' THEN
            IF UserSetup.GET(USERID) THEN BEGIN
              AllowPostingFrom := UserSetup."Allow Posting From";
              AllowPostingTo := UserSetup."Allow Posting To";
            END;
          IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
            AllowPostingFrom := GLSetup."Allow Posting From";
            AllowPostingTo := GLSetup."Allow Posting To";
          END;
          IF AllowPostingTo = 0D THEN
            AllowPostingTo := 12319999D;
        END;
        IF ("Posting Date" < AllowPostingFrom) OR ("Posting Date" > AllowPostingTo) THEN
          FIELDERROR("Posting Date",Text001)
      END;
    END;

    PROCEDURE SetCalledFromAdjustment@49(NewCalledFromAdjustment@1000 : Boolean);
    BEGIN
      CalledFromAdjustment := NewCalledFromAdjustment;
    END;

    BEGIN
    END.
  }
}

