OBJECT Report 1130 Delete Cost Entries
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm;
    CaptionML=[ENU=Delete Cost Entries;
               ESM=Eliminar movs. costo;
               FRC=Supprimer ‚critures de co–ts;
               ENC=Delete Cost Entries];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF CostRegister2."No." > CostRegister3."No." THEN
                    ERROR(Text000);

                  IF NOT CONFIRM(Text001,FALSE,CostRegister2."No.",CostRegister3."No.") THEN
                    ERROR('');

                  IF NOT CONFIRM(Text004) THEN
                    ERROR('');

                  Window.OPEN(Text005 +
                    Text006 );
                END;

  }
  DATASET
  {
    { 5327;    ;DataItem;                    ;
               DataItemTable=Table1105;
               DataItemTableView=SORTING(No.)
                                 ORDER(Descending);
               OnPreDataItem=BEGIN
                               SETRANGE("No.",CostRegister2."No.",CostRegister3."No.");
                             END;

               OnAfterGetRecord=VAR
                                  CostEntry@1000 : Record 1104;
                                BEGIN
                                  Window.UPDATE(1,"No.");

                                  IF Closed THEN
                                    ERROR(Text007,"No.");

                                  IF Source = Source::Allocation THEN BEGIN
                                    CostEntry.SETCURRENTKEY("Allocated with Journal No.");
                                    CostEntry.SETRANGE("Allocated with Journal No.","No.");
                                    CostEntry.MODIFYALL(Allocated,FALSE);
                                    CostEntry.MODIFYALL("Allocated with Journal No.",0);
                                  END;

                                  CostEntry.RESET;
                                  CostEntry.SETRANGE("Entry No.","From Cost Entry No.","To Cost Entry No.");
                                  CostEntry.DELETEALL;
                                END;

               OnPostDataItem=VAR
                                CostEntry@1000 : Record 1104;
                              BEGIN
                                DELETEALL;
                                RESET;
                                SETRANGE(Source,Source::Allocation);
                                IF FINDLAST THEN BEGIN
                                  CostEntry.GET("To Cost Entry No.");
                                  CostAccSetup.GET;
                                  CostAccSetup."Last Allocation Doc. No." := CostEntry."Document No.";
                                  CostAccSetup.MODIFY;
                                END;
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnOpenPage=BEGIN
                   CostRegister2.FINDLAST;
                   CostRegister3.FINDLAST;
                 END;

    }
    CONTROLS
    {
      { 1   ;0   ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  CaptionML=[ENU=Options;
                             ESM=Opciones;
                             FRC=Options;
                             ENC=Options] }

      { 3   ;2   ;Field     ;
                  Name=FromRegisterNo;
                  Lookup=Yes;
                  CaptionML=[ENU=From Register No.;
                             ESM=Desde n§ registro;
                             FRC=Nø hist. transaction d'origine;
                             ENC=From Register No.];
                  SourceExpr=CostRegister2."No.";
                  TableRelation="Cost Register" WHERE (Closed=CONST(No)) }

      { 4   ;2   ;Field     ;
                  Name=ToRegisterNo;
                  CaptionML=[ENU=To Register No.;
                             ESM=Hasta n§ registro;
                             FRC=Nø hist. transaction de destination;
                             ENC=To Register No.];
                  SourceExpr=CostRegister3."No.";
                  TableRelation="Cost Register" WHERE (Closed=CONST(No));
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=From Register No. must not be higher than To Register No..;ESM=Desde n§ reg. no puede ser superior a Hasta n§ registro.;FRC=Le nø hist. transaction d''origine ne doit pas ˆtre sup‚rieur au nø hist. transaction de destination.;ENC=From Register No. must not be higher than To Register No..';
      Text001@1001 : TextConst 'ENU=All corresponding cost entries and register entries will be deleted. Do you want to delete cost register %1 to %2?;ESM=Se eliminar n todos los movs. costo y de registro correspondientes. ¨Desea eliminar el registro de costos %1 hasta %2?;FRC=Toutes les ‚critures de transactions et de co–ts correspondantes seront supprim‚es. Voulez-vous supprimer le registre de co–ts %1 de %2ÿ?;ENC=All corresponding cost entries and register entries will be deleted. Do you want to delete cost register %1 to %2?';
      Text004@1002 : TextConst 'ENU=Are you sure?;ESM=¨Est  seguro?;FRC=Òtes-vous certainÿ?;ENC=Are you sure?';
      Text005@1003 : TextConst 'ENU=Delete cost register\;ESM=Eliminar reg. costos\;FRC=Supprimer registre de co–ts\;ENC=Delete cost register\';
      Text006@1004 : TextConst 'ENU=Register  no.      #1######;ESM=N§ reg.       #1######;FRC=Nø hist. transaction      #1######;ENC=Register  no.      #1######';
      Text007@1005 : TextConst 'ENU=Register %1 can no longer be deleted because it is marked as closed.;ESM=El registro %1 ya no se puede borrar porque est  marcado como cerrado.;FRC=Le registre %1 ne peut plus ˆtre supprim‚ car il est marqu‚ comme ferm‚.;ENC=Register %1 can no longer be deleted because it is marked as closed.';
      CostRegister2@1006 : Record 1105;
      CostRegister3@1007 : Record 1105;
      CostAccSetup@1010 : Record 1108;
      Window@1011 : Dialog;

    PROCEDURE InitializeRequest@1(NewFromRegisterNo@1000 : Integer;NewToRegisterNo@1001 : Integer);
    BEGIN
      CostRegister2."No." := NewFromRegisterNo;
      CostRegister3."No." := NewToRegisterNo;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

