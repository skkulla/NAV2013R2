OBJECT Report 593 Intrastat - Make Disk Tax Auth
{
  OBJECT-PROPERTIES
  {
    Date=03/13/14;
    Time=12:00:00 PM;
    Version List=NAVW17.10.00.36281;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Intrastat - Make Disk Tax Auth;
               ESM=Intrastat - Decl. disquete;
               FRC=Intrastat: g‚n‚rer disquette;
               ENC=Intrastat - Make Disk Tax Auth];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   ClientFilename := DefaultFilenameTxt;
                 END;

    OnPreReport=BEGIN
                  FileName := FileMgt.ServerTempFileName('');

                  IntrastatJnlLine4.COPYFILTERS("Intrastat Jnl. Line");
                  IF FileName = '' THEN
                    ERROR(Text000);
                  IntraFile.TEXTMODE := TRUE;
                  IntraFile.WRITEMODE := TRUE;
                  IntraFile.CREATE(FileName);
                END;

  }
  DATASET
  {
    { 2880;    ;DataItem;                    ;
               DataItemTable=Table262;
               DataItemTableView=SORTING(Journal Template Name,Name);
               OnPreDataItem=BEGIN
                               IntrastatJnlLine4.COPYFILTER("Journal Template Name","Journal Template Name");
                               IntrastatJnlLine4.COPYFILTER("Journal Batch Name",Name);
                             END;

               OnAfterGetRecord=BEGIN
                                  TESTFIELD(Reported,FALSE);
                                  IntraReferenceNo := "Statistics Period" + '000000';
                                END;

               ReqFilterFields=Journal Template Name,Name }

    { 9905;1   ;DataItem;                    ;
               DataItemTable=Table263;
               DataItemTableView=SORTING(Type,Country/Region Code,Tariff No.,Transaction Type,Transport Method);
               OnAfterGetRecord=BEGIN
                                  IF ("Tariff No." = '') AND
                                     ("Country/Region Code" = '') AND
                                     ("Transaction Type" = '') AND
                                     ("Transport Method" = '') AND
                                     ("Total Weight" = 0)
                                  THEN
                                    CurrReport.SKIP;

                                  TESTFIELD("Tariff No.");
                                  TESTFIELD("Country/Region Code");
                                  TESTFIELD("Transaction Type");
                                  TESTFIELD("Total Weight");
                                  IF "Supplementary Units" THEN
                                    TESTFIELD(Quantity);
                                  CompoundField :=
                                    FORMAT("Country/Region Code",10) + FORMAT(DELCHR("Tariff No."),10) +
                                    FORMAT("Transaction Type",10) + FORMAT("Transport Method",10);

                                  IF (TempType <> Type) OR (STRLEN(TempCompoundField) = 0) THEN BEGIN
                                    TempType := Type;
                                    TempCompoundField := CompoundField;
                                    IntraReferenceNo := COPYSTR(IntraReferenceNo,1,4) + FORMAT(Type,1,2) + '01001';
                                  END ELSE
                                    IF TempCompoundField <> CompoundField THEN BEGIN
                                      TempCompoundField := CompoundField;
                                      IF COPYSTR(IntraReferenceNo,8,3) = '999' THEN
                                        IntraReferenceNo := INCSTR(COPYSTR(IntraReferenceNo,1,7)) + '001'
                                      ELSE
                                        IntraReferenceNo := INCSTR(IntraReferenceNo);
                                    END;

                                  "Internal Ref. No." := IntraReferenceNo;
                                  MODIFY;
                                END;

               ReqFilterFields=Type;
               DataItemLink=Journal Template Name=FIELD(Journal Template Name),
                            Journal Batch Name=FIELD(Name) }

    { 2177;1   ;DataItem;IntrastatJnlLine2   ;
               DataItemTable=Table263;
               DataItemTableView=SORTING(Internal Ref. No.);
               OnPreDataItem=BEGIN
                               CompanyInfo.GET;
                               VATRegNo := CONVERTSTR(CompanyInfo."VAT Registration No.",Text001,'    ');
                               IntraFile.WRITE(FORMAT('00' + FORMAT(VATRegNo,8) + Text002,80));
                               IntraFile.WRITE(FORMAT('0100004',80));

                               SETRANGE("Internal Ref. No.",COPYSTR(IntraReferenceNo,1,4),COPYSTR(IntraReferenceNo,1,4) + '9');
                               CurrReport.CREATETOTALS(Quantity,"Statistical Value","Total Weight");

                               IntrastatJnlLine3.SETCURRENTKEY("Internal Ref. No.");
                             END;

               OnAfterGetRecord=BEGIN
                                  IF ("Tariff No." = '') AND
                                     ("Country/Region Code" = '') AND
                                     ("Transaction Type" = '') AND
                                     ("Transport Method" = '') AND
                                     ("Total Weight" = 0)
                                  THEN
                                    CurrReport.SKIP;
                                  "Tariff No." := DELCHR("Tariff No.");

                                  TotalWeightAmt += "Total Weight";
                                  QuantityAmt += Quantity;
                                  StatisticalValueAmt += "Statistical Value";

                                  IntrastatJnlLine5.COPY(IntrastatJnlLine2);
                                  IF IntrastatJnlLine5.NEXT = 1 THEN BEGIN
                                    IF (DELCHR(IntrastatJnlLine5."Tariff No.") = "Tariff No.") AND
                                       (IntrastatJnlLine5."Country/Region Code" = "Country/Region Code") AND
                                       (IntrastatJnlLine5."Transaction Type" = "Transaction Type") AND
                                       (IntrastatJnlLine5."Transport Method" = "Transport Method")
                                    THEN
                                      GroupTotal := FALSE
                                    ELSE
                                      GroupTotal := TRUE;
                                  END ELSE
                                    GroupTotal := TRUE;

                                  IF GroupTotal THEN BEGIN
                                    WriteGrTotalsToFile(TotalWeightAmt,QuantityAmt,StatisticalValueAmt);
                                    StatisticalValueTotalAmt += StatisticalValueAmt;
                                    TotalWeightAmt := 0;
                                    QuantityAmt := 0;
                                    StatisticalValueAmt := 0;
                                  END;
                                END;

               OnPostDataItem=BEGIN
                                IF NOT Receipt THEN
                                  IntraFile.WRITE(
                                    FORMAT(
                                      '02000' + FORMAT(IntraReferenceNo,4) + '100000' +
                                      FORMAT(VATRegNo,8) + '1' + FORMAT(IntraReferenceNo,4),
                                      80));
                                IF NOT Shipment THEN
                                  IntraFile.WRITE(
                                    FORMAT(
                                      '02000' + FORMAT(IntraReferenceNo,4) + '200000' +
                                      FORMAT(VATRegNo,8) + '2' + FORMAT(IntraReferenceNo,4),
                                      80));
                                IntraFile.WRITE(FORMAT('10' + DecimalNumeralZeroFormat(StatisticalValueTotalAmt,16),80));
                                IntraFile.CLOSE;

                                "Intrastat Jnl. Batch".Reported := TRUE;
                                "Intrastat Jnl. Batch".MODIFY;

                                IF FileMgt.CanRunDotNetOnClient THEN
                                  FileMgt.DownloadToFile(FileName,ClientFilename)
                                ELSE
                                  DOWNLOAD(FileName,WindowTitleTxt,'',FilterStringTxt,ClientFilename);
                              END;
                               }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1   ;    ;Container ;
                  ContainerType=ContentArea }

      { 2   ;1   ;Group     ;
                  CaptionML=[ENU=Options;
                             ESM=Options;
                             FRC=Options;
                             ENC=Options];
                  GroupType=Group }

      { 3   ;2   ;Field     ;
                  Name=Filename;
                  CaptionML=[ENU=Intrastat Filename;
                             ESM=Intrastat Filename;
                             FRC=Intrastat Filename;
                             ENC=Intrastat Filename];
                  SourceExpr=ClientFilename;
                  OnLookup=BEGIN
                             Text := FileMgt.SaveFileDialog(WindowTitleTxt,DefaultFilenameTxt,FilterStringTxt);
                             IF Text = '' THEN
                               EXIT(FALSE);
                             ClientFilename := Text;
                           END;
                            }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the file name.;ESM=Introduzca el nombre del archivo.;FRC=Entrez le nom du fichier.;ENC=Please enter the file name.';
      Text001@1001 : TextConst 'ENU=WwWw;ESM=WwWw;FRC=WwWw;ENC=WwWw';
      Text002@1002 : TextConst 'ENU=INTRASTAT;ESM=INTRASTAT;FRC=INTRACOMM;ENC=INTRASTAT';
      Text003@1003 : TextConst 'ENU=It is not possible to display %1 in a field with a length of %2.;ESM=No es posible mostrar %1 en un campo con una longitud de %2.;FRC=Il n''est pas possible d''afficher %1 dans un champ d''une longueur de %2.;ENC=It is not possible to display %1 in a field with a length of %2.';
      IntrastatJnlLine3@1004 : Record 263;
      IntrastatJnlLine4@1005 : Record 263;
      IntrastatJnlLine5@1026 : Record 263;
      CompanyInfo@1006 : Record 79;
      Country@1007 : Record 9;
      FileMgt@1030 : Codeunit 419;
      QuantityAmt@1023 : Decimal;
      StatisticalValueAmt@1024 : Decimal;
      StatisticalValueTotalAmt@1028 : Decimal;
      TotalWeightAmt@1025 : Decimal;
      IntraFile@1008 : File;
      FileName@1009 : Text;
      IntraReferenceNo@1010 : Text[10];
      CompoundField@1011 : Text[40];
      TempCompoundField@1012 : Text[40];
      TempType@1013 : Integer;
      NoOfEntries@1014 : Text[3];
      Receipt@1015 : Boolean;
      Shipment@1016 : Boolean;
      VATRegNo@1017 : Code[20];
      ImportExport@1018 : Code[1];
      OK@1019 : Boolean;
      WindowTitleTxt@1020 : TextConst 'ENU=Export;ESM=Exportar;FRC=Exporter;ENC=Export';
      FilterStringTxt@1021 : TextConst 'ENU=All Files (*.*)|*.*;ESM=Todos los archivos (*.*)|*.*;FRC=Tous les fichiers (*.*)|*.*;ENC=All Files (*.*)|*.*';
      DefaultFilenameTxt@1022 : TextConst 'ENU=C:\Default.txt;ESM=C:\Default.txt;FRC=C:\Default.txt;ENC=C:\Default.txt';
      GroupTotal@1027 : Boolean;
      ClientFilename@1029 : Text;

    LOCAL PROCEDURE DecimalNumeralZeroFormat@1(DecimalNumeral@1000 : Decimal;Length@1001 : Integer) : Text[250];
    BEGIN
      EXIT(TextZeroFormat(DELCHR(FORMAT(ROUND(ABS(DecimalNumeral),1,'<'),0,1)),Length));
    END;

    LOCAL PROCEDURE TextZeroFormat@2(Text@1000 : Text[250];Length@1001 : Integer) : Text[250];
    BEGIN
      IF STRLEN(Text) > Length THEN
        ERROR(
          Text003,
          Text,Length);
      EXIT(PADSTR('',Length - STRLEN(Text),'0') + Text);
    END;

    PROCEDURE WriteGrTotalsToFile@3(TotalWeightAmt@1000 : Decimal;QuantityAmt@1001 : Decimal;StatisticalValueAmt@1002 : Decimal);
    BEGIN
      WITH IntrastatJnlLine2 DO BEGIN
        OK := COPYSTR("Internal Ref. No.",8,3) = '001';
        IF OK THEN BEGIN
          IntrastatJnlLine3.SETRANGE(
            "Internal Ref. No.",
            COPYSTR("Internal Ref. No.",1,7) + '000',
            COPYSTR("Internal Ref. No.",1,7) + '999');
          IntrastatJnlLine3.FINDLAST;
          NoOfEntries := COPYSTR(IntrastatJnlLine3."Internal Ref. No.",8,3);
        END;
        ImportExport := INCSTR(FORMAT(Type,1,2));

        IF Type = Type::Receipt THEN
          Receipt := TRUE
        ELSE
          Shipment := TRUE;
        Country.GET("Country/Region Code");
        Country.TESTFIELD("Intrastat Code");

        IF OK THEN
          IntraFile.WRITE(
            FORMAT(
              '02' +
              TextZeroFormat(DELCHR(NoOfEntries),3) +
              FORMAT(COPYSTR(IntrastatJnlLine3."Internal Ref. No.",1,7) + '000',10) +
              FORMAT(VATRegNo,8) + FORMAT(ImportExport,1) + FORMAT(IntraReferenceNo,4),
              80));

        IntraFile.WRITE(
          FORMAT(
            '03' +
            TextZeroFormat(COPYSTR("Internal Ref. No.",8,3),3) +
            FORMAT("Internal Ref. No.",10) + FORMAT(Country."Intrastat Code",3) + FORMAT("Transaction Type",2) +
            '0' + FORMAT("Transport Method",1) + PADSTR("Tariff No.",9,'0') +
            DecimalNumeralZeroFormat(ROUND(TotalWeightAmt,1,'>'),15) +
            DecimalNumeralZeroFormat(QuantityAmt,10) +
            DecimalNumeralZeroFormat(StatisticalValueAmt,15),
            80));
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

